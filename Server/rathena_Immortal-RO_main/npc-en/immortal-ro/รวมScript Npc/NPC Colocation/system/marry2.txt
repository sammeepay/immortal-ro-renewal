c5,192,108,4	script	บาทหลวง	833,{
	set .@npc$,"[บาทหลวง]";
	if (getpartnerid()) goto OnPartner;
	if ( (.WEDstatus == 2) && (.WEDname_girl$ == strcharinfo(0)) ) goto OnStatus2;
	mes .@npc$;
	mes "สวัสดีครับ ยินดีให้บริการครับ";
	mes "คุณต้องการแต่งงานใช่มั้ยครับ";
	next;
	if (select("^009933^000000 ใช่","^FF0000^000000 ไม่ใช่") == 2) goto Cancel;
	mes .@npc$;
	mes "อุปกรณ์ประกอบพิธีครับ";
	mes "====== ชาย ======";
	for (set .@i,0; .@i < getarraysize(.WEDboy_item); set .@i,.@i+1) {
		mes getitemname(.WEDboy_item[.@i])+" 1 ea";
	}
	mes "====== หญิง ======";
	for (set .@i,0; .@i < getarraysize(.WEDgirl_item); set .@i,.@i+1) {
		mes getitemname(.WEDgirl_item[.@i])+" 1 ea";
	}
	mes "================";
	mes "จำนวนเงิน "+.WEDzeny+" Zeny";
	if (Sex == 0) {
		mes "================";
		mes "ให้ฝ่ายชายมาเริ่มพิธีครับ";
		close;
	}
	next;
	if (select("^009933^000000 ตกลง","^FF0000^000000 ยกเลิก") == 2) goto Cancel;
	mes .@npc$;
	if (.WEDstatus > 0) {
		mes "มีคู่อื่นกำลังเข้าพิธีครับ";
		close;
	}
	set .WEDstauts,1;
	mes "ระบุชื่อฝ่ายหญิงครับ";
	set .WEDname_boy$,strcharinfo(0);
	set .@loop,1;
	do {
		input .WEDname_girl$;
		if (.WEDname_girl$ == "") {
			mes "================";
			mes "ชื่อไม่ถูกต้องครับ";
			close;
		} else if (getstrlen(.WEDname_girl$) > 24) {
			mes "================";
			mes "ชื่อไม่ถูกต้องครับ";
		} else if (isloggedin(getcharid(3,.WEDname_girl$))) {
			mes "================";
			attachrid(getcharid(3,.WEDname_girl$));
			set .@check_sex,Sex;
			detachrid;
			attachrid(getcharid(3,.WEDname_boy$));
			if (.@check_sex != 0) {
				mes "ชื่อไม่ถูกต้องครับ";
			} else {
				set .@loop,0;
			}
		} else {
			mes "================";
			mes "ชื่อไม่ถูกต้องครับ";
		}
	} while (.@loop);
	set .@isok,1;
	for (set .@i,0; .@i < getarraysize(.WEDboy_item); set .@i,.@i+1) {
		if (countitem(.WEDboy_item[.@i]) < 1) {
			mes "คุณไม่มี "+getitemname(.WEDboy_item[.@i])+" 1 ea ครับ";
			set .@isok,0;
		}
	}
	if (Zeny < .WEDzeny) {
		mes "คุณไม่มีค่าบริการครับ";
		set .@isok,0;
	}
	if (!.@isok) {
		set .WEDstatus,0;
	} else {
		for (set .@i,0; .@i < getarraysize(.WEDboy_item); set .@i,.@i+1) {
			delitem .WEDboy_item[.@i],1;
		}
		set Zeny,Zeny-.WEDzeny;
		mes "ให้ฝ่ายหญิงมารายงานตัวครับ";
		set .WEDstatus,2;
		donpcevent ""+strnpcinfo(0)+"::OnCheckTime";
	}
	close;

OnStatus2:
	mes .@npc$;
	mes "คุณต้องการแต่งงานกับ";
	mes "คุณ "+.WEDname_boy$+"";
	mes "ใช่มั้ยครับ";
	next;
	if (select("^009933^000000 ใช่","^FF0000^000000 ไม่ใช่") == 2) {
		attachrid(getcharid(3,.WEDname_boy$));
		for (set .@i,0; .@i < getarraysize(.WEDboy_item); set .@i,.@i+1) {
			getitem .WEDboy_item[.@i],1;
		}
		set Zeny,Zeny+.WEDzeny;
		dispbottom "[ บาทหลวง ] : คุณ "+.WEDname_girl$+" ปฏิเสธการแต่งงาน !!";
		detachrid;
		attachrid(getcharid(3,.WEDname_girl$));
		set .WEDstatus,0;
		goto Cancel;
	}
	mes .@npc$;
	set .@isok,1;
	for (set .@i,0; .@i < getarraysize(.WEDgirl_item); set .@i,.@i+1) {
		if (countitem(.WEDgirl_item[.@i]) < 1) {
			mes "คุณไม่มี "+getitemname(.WEDgirl_item[.@i])+" 1 ea ครับ";
			set .@isok,0;
		}
	}
	if (Zeny < .WEDzeny) {
		mes "คุณไม่มีค่าบริการครับ";
		set .@isok,0;
	}
	if (.@isok) {
		if ( isloggedin(getcharid(3,.WEDname_boy$)) && marriage(.WEDname_boy$) && (.WEDstatus == 2) ) {
			for (set .@i,0; .@i < getarraysize(.WEDgirl_item); set .@i,.@i+1) {
				if (.WEDgirl_item[.@i] == 2206) continue;
				delitem .WEDgirl_item[.@i],1;
			}
			set Zeny,Zeny-.WEDzeny;

			sc_start SC_Wedding,3600000,1;
			getitem 2635,1;
			attachrid(getcharid(3,.WEDname_boy$));
			sc_start SC_Wedding,3600000,1;
			getitem 2634,1;
			detachrid;
			attachrid(getcharid(3,.WEDname_girl$));
			announce "[ บาทหลวง ] : เจ้าบ่าว [ "+.WEDname_boy$+" ] & เจ้าสาว [ "+.WEDname_girl$+" ] ได้แต่งงานกันแล้ว !!",0,0xFF00CC;
			wedding;
			equip 2206;
		
			mes "เรียบร้อยครับ ยินดีให้บริการ";
		} else {
			if (getpartnerid()) divorce();
			mes "ล้มเหลวครับ ยินดีให้บริการ";
		}
		set .WEDstatus,0;
		set .WEDname_boy$,"";
		set .WEDname_girl$,"";
	}
	close;

OnPartner:
	mes .@npc$;
	mes "สวัสดีครับ ยินดีให้บริการครับ";
	mes "คุณต้องการหย่าใช่มั้ยครับ";
	mes "";
	mes "ค่าบริการ";
	mes ""+(.WEDzeny*2)+" Zeny";
	next;
	if (select("^009933^000000 ใช่","^FF0000^000000 ไม่ใช่") == 2) {
		mes .@npc$;
		mes "เรียบร้อยครับ ยินดีให้บริการ";
		close;
	}
	mes .@npc$;
	if (Zeny < (.WEDzeny*2)) {
		mes "คุณไม่มีค่าบริการครับ";
		close;
	} else if (divorce()) {
		mes "เรียบร้อยครับ ยินดีให้บริการ";
		set Zeny,Zeny-(WEDzeny*2);
	}
	close;

OnCheckTime:
	set .@time,60;
	while ( (.@time > 0) && (.WEDstatus == 2) ) {
		sleep 1000;
		set .@time,.@time-1;
	}
	if (attachrid(getcharid(3,.WEDname_boy$))) {
		for (set .@i,0; .@i < getarraysize(.WEDboy_item); set .@i,.@i+1) {
			getitem .WEDboy_item[.@i],1;
		}
		set Zeny,Zeny+.WEDzeny;
		dispbottom "[ บาทหลวง ] : ยกเลิกพิธีแต่งงาน !!";
	}
	set .WEDstatus,0;
	end;

Cancel:
	mes .@npc$;
	mes "ยินดีให้บริการครับ";
	close;
	end;

OnInit:
	// Set
	set .WEDzeny,1500000;
	setarray .WEDboy_item[0],7170,745,2613;
	setarray .WEDgirl_item[0],2338,2206,2613;

	// System
	set .WEDstatus,0;
	set .WEDname_boy$,"";
	set .WEDname_girl$,"";
	end;
}
