//===== Developers Script ===================================
//= Server Rate
//===== Current Version =====================================
//= 1.0
//===== Description =========================================
//= Server Rate
//===== Additional Comments =================================
//= 1.0 Start Version
//= 1.1 Edit By Ragnarok PVP GVG
//===========================================================

-	script	SystemMVPTW	-1,{
	end;

OnWhisperGlobal:
	if (getgmlevel() < 99) end;
OnInit:
	set $@MVPTWmap$,"guild_vs3";			// กำหนด Map ที่ใช้เริ่มกิจกรรม
	set $@MVPTWdeley_boss,10;			// กำหนดเวลาบอสออกหน่วยเป็นวินาที
	set $@MVPTWmax_rank,20;				// กำหนดจำนวนการนับผล Ranking
	set $@MVPTWdeley_check,15;			// กำหนดเวลาการตรวจสอบจำนวนผู้เล่นใน Map หน่วยเป็นวินาที
	setarray $@MVPTWsavemxy$[0],"rtc_ro","100","79";	// กำหนดจุดวาปกลับเมื่อกิจกรรมเสร็จสิ้น
	
	// Set System
	setarray $@MVPTWmob_id[0],2777,2779,2780,2783,2788,2793,2794,2805,2806,2809,
		2735,2750,2751,2753,2755,2763,2769,2770,2771,2772,
		2706,2708,2709,2710,2712,2713,2715,2717,2719,2723,
		2726,2728,2729,2730;
	set $@MVPTWmob_count,getarraysize($@MVPTWmob_id);
	set $@MVPTWlevel,0;
	set $@MVPTW,0;
	set $@MVPTWparty_name$,"";
	setarray .@mapflag[0], mf_nomemo, mf_noteleport, mf_noicewall ,mf_nowarpto, mf_nosave, 
		mf_pvp, mf_nodrop, mf_partylock, mf_nomobloot, mf_nomvploot;
	
	// Run System Default
	for (set .@i,0; .@i < getarraysize(.@mapflag); set .@i,.@i+1) {
		setmapflag $@MVPTWmap$,.@mapflag[.@i];
	}
	end;

OnStart_Boss:
	set $@MVPTWlevel,$@MVPTWlevel+1;
	for (set .@i,$@MVPTWdeley_boss; .@i >= 1; set .@i,.@i-1) {
		if (!$@MVPTW) end;
		mapannounce $@MVPTWmap$,"[MvpTower] : Boss ของด่านที่ "+$@MVPTWlevel+" จะออกในอีก "+.@i+" วินาที",0;
		sleep 1000;
	}
	mapannounce $@MVPTWmap$,"[MvpTower] : Boss ออกแล้วมาแล้ว",0;
	monster $@MVPTWmap$,0,0,"Mvp Tower [ LV "+$@MVPTWlevel+"]",$@MVPTWmob_id[rand(0,$@MVPTWmob_count-1)],1,"SystemMVPTW::OnKillBoss";
	end;

OnKillBoss:
	mapannounce $@MVPTWmap$,"[MvpTower] : ปาร์ตี้ "+$@MVPTWparty_name$+" ได้ผ่าน Mvp Tower Level "+$@MVPTWlevel+" แล้ว !!",0;
	sleep 10000;
	goto OnStart_Boss;
	end;

OnTimer:
	set .@loop,1;
	while (.@loop) {
		sleep $@MVPTWdeley_check*1000;
		if (getmapusers($@MVPTWmap$) <= 0) {
			set .@loop,0;
		}
	}
	goto EndEvent;	
	end;

EndEvent:
	mapwarp $@MVPTWmap$,$@MVPTWsavemxy$[0],$@MVPTWsavemxy$[1],$@MVPTWsavemxy$[2];
	announce "[MvpTower] : ปาร์ตี้ "+$@MVPTWparty_name$+" ได้พ่ายแพ้ใน Mvp Tower Level "+$@MVPTWlevel+" แล้ว !! ",0;
	set $@MVPTWlevel,$@MVPTWlevel-1;
	for (set .@i,0; .@i < $@MVPTWmax_rank; set .@i,.@i+1) {
		if ($MVPTWrank_level[.@i] < $@MVPTWlevel) {
			set .@temp_name$,$MVPTWrank_name$[.@i];
			set .@temp_level,$MVPTWrank_level[.@i];
			set $MVPTWrank_name$[.@i],$@MVPTWparty_name$;
			set $MVPTWrank_level[.@i],$@MVPTWlevel;
			for (set .@p,.@i+1; .@p < $@MVPTWmax_rank; set .@p,.@p+1) {
				set .@swap_name$,.@temp_name$;
				set .@swap_level,.@temp_level;
				set .@temp_name$,$MVPTWrank_name$[.@p];
				set .@temp_level,$MVPTWrank_level[.@p];
				set $MVPTWrank_name$[.@p],.@swap_name$;
				set $MVPTWrank_level[.@p],.@swap_level;
			}
			break;
		}
	}
	sleep 3000;
	if ($@MVPTWlevel == 0) {
		announce "[MvpTower] : ปาร์ตี้ "+$@MVPTWparty_name$+" ไม่ติดอันดับ",0;
	} else if (.@i > $@MVPTWmax_rank) {
		announce "[MvpTower] : ปาร์ตี้ "+$@MVPTWparty_name$+" ไม่ติดอันดับ 1 ใน "+$@MVPTWmax_rank+"",0;
	} else {
		announce "[MvpTower] : ปาร์ตี้ "+$@MVPTWparty_name$+" ได้ติดอันดับที่ "+(.@i+1)+"",0;
	}
	killmonsterall $@MVPTWmap$;
	set $@MVPTWlevel,0;
	set $@MVPTW,0;
	set $@MVPTWparty_id,0;
	set $@MVPTWparty_name$,"";
	end;
}

-	script	Msevent::MVPtow	-1,{
	set .@npc$,"[MvpTower]";
	mes .@npc$;
	mes "ยินดีต้อนรับสู่หอคอย";
	mes "แห่งความโหดเหี้ยม";
	mes "หากท่านจะเข้าสู่หอคอยแห่งนี้";
	mes "ท่านจำเป็นต้องมีปาร์ตี้";
	mes "และลูกทีมที่เก่ง";
	next;
	switch (select("- ตรวจสอบ Ranking","- เข้าสู่หอคอย")) {
	case 1:
		mes .@npc$;
		mes "แสดงผลอันดับที่ ^FF00001^000000 - ^FF0000"+$@MVPTWmax_rank+"^000000";
		for (set .@i,0; .@i < $@MVPTWmax_rank; set .@i,.@i+1) {
			if ($MVPTWrank_name$[.@i] == "") {
				mes (.@i+1)+". ^0000FFNone^000000 [^0000FFLv.^FF00000^000000]";
			} else {
				mes (.@i+1)+". ^0000FF"+$MVPTWrank_name$[.@i]+"^000000 [^0000FFLv.^FF0000"+$MVPTWrank_level[.@i]+"^000000]";
			}
		}
		close;
	break;
	case 2:
		set .@party_id,getcharid(1);
		if (.@party_id == 0) {
			mes .@npc$;
			mes "ท่านจำเป็นต้องมีปาร์ตี้";
			mes "จึงจะสามารถเข้าหอคอยได้";
			close;
		} else if (getpartyleader(.@party_id,2) != getcharid(0)) {
			mes .@npc$;
			mes "ท่านไม่ใช่หัวหน้าปาร์ตี้";
			close;
		} else if ($@MVPTW) {
			mes .@npc$;
			mes "ขณะนี้มี Party อื่น";
			mes "กำลังอยู่ในหอคอย";
			close;
		} else {
			set $@MVPTW,1;
			set $@MVPTWparty_name$,getpartyname(.@party_id);
			warpparty $@MVPTWmap$,0,0,.@party_id;
			sleep 5000;
			donpcevent "SystemMVPTW::OnStart_Boss";
			donpcevent "SystemMVPTW::OnTimer";
		}
	break;
	}
	end;

OnWhisperGlobal:
	if (getgmlevel() < 99) end;
OnInit:
waitingroom "   Mvp Tower",0;
end;
}


rtc_ro,12,106,4	duplicate(MVPtow)	Mvp Tower#01	714