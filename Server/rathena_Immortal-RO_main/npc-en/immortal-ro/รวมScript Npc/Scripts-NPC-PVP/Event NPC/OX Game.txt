//===== Developers Script ===================================
//= Server Rate
//===== Current Version =====================================
//= 1.0
//===== Description =========================================
//= Server Rate
//===== Additional Comments =================================
//= 1.0 Start Version
//= 1.1 Edit By Ragnarok PVP GVG
//===========================================================
quiz_01,61,194,0	warp	warp_random1	1,1,quiz_01,52,182
quiz_01,66,194,0	warp	warp_random2	1,1,quiz_01,75,182
quiz_01,63,196,0	script	warp_random_finish	45,1,1,{
	for( set .@i,0; .@i < getarraysize($randomitem); set .@i,.@i+1 ) {
		getitem $randomitem[.@i],$randomitemamount[.@i];
	}
	warp "rtc_ro",99,114;
	end;
}

-	script	warp_random	-1,{
	mes "[ OX GAME ]";
	mes "ยินดีต้อนรับสู่กิจกรรม OX GAME กิจกรรมง่ายๆ ขอแค่คุณดวงถึง!!";
	next;
	switch(select("  เข้าร่วมกิจกรรม:  ฟังกติกา:  ออก")) {
	case 1:
		if($randomregister != 1) { mes $bossname$; mes "ขณะนี้ยังไม่ถึงเวลากิจกรรม"; close; }
		warp "quiz_01",63,200;
		end;
	case 2:
		mes $bossname$;
		mes "1. กิจกรรมจะถูกจัดขึ้นทุกๆ นาที 25 ของทุกชั่วโมง";
		mes "2. เข้าร่วมได้ไม่จำกัดคน";
		mes "3. ภายในกิจกรรมมีทั้งหมด 7 ด่าน";
		mes "4. เมื่อกิจกรรมเริ่นต้นขึ้น คุณต้องเลือกฝั่งที่คุณต้องการอยู่ หากคุณโชคดีคุณก็จะได้อยู่ต่อไป..";
		mes "5. หากคุณไม่เลือกฝั่งภายในเวลาที่กำหนด คุณจะถูกปรับแพ้และวาร์ปออกมาทันที";
		mes "6. ขอให้ทุกท่านโชคดี";
		close;
	case 3:
		close;
	}
OnInit:
	waitingroom "-> OX  GAME",0;
	end;
}

-	script	warp_hidden	-1,{
OnMinute25:
	set $randomregister,"1";
	disablenpc "warp_random_finish";
	announce $randomname$+" : กิจกรรม OX GAME กำลังจะเริ่มในอีก 3 นาที",bc_all,0xEEF2F7;
	sleep2 120000;
	announce $randomname$+" : กิจกรรม OX GAME กำลังจะเริ่มในอีก 1 นาที",bc_all,0xEEF2F7;
	sleep2 60000;
	set $randomregister,"0";
	announce $randomname$+" : กิจกรรม OX GAME เริ่มขึ้นแล้ว!!",bc_all,0xEEF2F7;
	for( set .@i,0; .@i < 7; set .@i,.@i+1 ) {
		if(getmapusers("quiz_01.gat") == 0) break;
		callfunc("warp_function",.@i+1);
	}
	sleep2 3000;
	announce $randomname$+" : กิจกรรม OX GAME จบลงแล้ว!!",bc_all,0xEEF2F7;
	end;

OnInit:
	set $randomname$,"OX GAME";
	set $randomregister,"0";
	disablenpc "warp_random_finish";
	setcell "quiz_01",62,185,66,181,cell_walkable,0;
	setcell "quiz_01",61,205,66,205,cell_walkable,0;
	set $randomreturnmap$,"rtc_ro";
	set $randomreturnx,0;
	set $randomreturny,0;
	setarray $randomitem[0],29000,29000;
	setarray $randomitemamount[0],1000,5;
	end;
	// หมายเหตุ. บรรทัดไหนไม่มี comment ห้ามปรับแต่งเด็ดขาด
}

function	script	warp_function	{
	mapannounce "quiz_01",$randomname$+" : [ Stage "+getarg(0)+" ]",bc_map,0xEEF2F7;
	sleep2 3000;
	mapannounce "quiz_01",$randomname$+" : คุณมีเวลาเลือกฝั่งที่ต้องการอีก 30 วินาที",bc_map,0xEEF2F7;
	sleep2 27000;
	mapannounce "quiz_01",$randomname$+" : คุณมีเวลาเลือกฝั่งที่ต้องการอีก 3 วินาที",bc_map,0xEEF2F7;
	sleep2 1000;
	mapannounce "quiz_01",$randomname$+" : คุณมีเวลาเลือกฝั่งที่ต้องการอีก 2 วินาที",bc_map,0x99FFFF;
	sleep2 1000;
	mapannounce "quiz_01",$randomname$+" : คุณมีเวลาเลือกฝั่งที่ต้องการอีก 1 วินาที",bc_map,0xEEF2F7;
	sleep2 1000;
	areawarp "quiz_01",61,204,66,194,$randomreturnmap$,$randomreturnx,$randomreturny;
	if(getarg(0) == 7) enablenpc "warp_random_finish";
	switch(rand(2)) {
	case 0:
		mapannounce "quiz_01",$randomname$+" : ผู้เล่นฝั่งซ้ายได้เข้ารอบ!! ขอแสดงความยินดีด้วย",bc_map,0xEEF2F7;
		areawarp "quiz_01",45,191,61,175,"quiz_01",63,196;
		sleep2 1000;
		areawarp "quiz_01",45,191,83,175,$randomreturnmap$,$randomreturnx,$randomreturny;
		break;
	case 1:
		mapannounce "quiz_01",$randomname$+" : ผู้เล่นฝั่งขวาได้เข้ารอบ!! ขอแสดงความยินดีด้วย",bc_map,0xEEF2F7;
		areawarp "quiz_01",67,191,83,175,"quiz_01",63,196;
		sleep2 1000;
		areawarp "quiz_01",45,191,83,175,$randomreturnmap$,$randomreturnx,$randomreturny;
		break;
	}
	return;
}


rtc_ro,30,106,4	duplicate(warp_random)	OX GAME	429
