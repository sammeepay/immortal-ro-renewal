//==============================================
// - Script By : แม่มดน้อยจอมป่วน
// - Script City : Thailand 
// - Script Support Server : rAthena
//==============================================
//===== rAthena Script =======================================
//= Job Master
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= A fully functional job changer.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Fixed reset on Baby job change.
//= 1.2 Added Expanded Super Novice support and initial Kagerou/Oboro support.
//= 1.3 Kagerou/Oboro added.
//= 1.4 Rebellion added.
//============================================================

prontera,153,193,6	script	เปลี่ยนอาชีพ	4_F_VALKYRIE,{
function Job_Menu;

	mes "[Job Master]";
	if (Class > Job_Soul_Linker) {
		mes "ข้าต้องขออภัยด้วย... !!?";
		mes "คุณไม่สามารถทำรายการใดๆ ได้อีก";
		mes "คุณอยู่ในอาชีพขั้นสูงสุดแล้ว..";
		close;
	}
	if (checkfalcon() || checkcart() || checkriding() || ismounting()) {
		mes "กรุณาถอดการติดตั้ง " +
			((checkfalcon()) ? "falcon" : "") +
			((checkcart()) ? "cart" : "") +
			((checkriding()) ? "Peco" : "") +
			((ismounting()) ? "mount" : "") +
			" ก่อนใช้บริการ.";
		close;
	}
	if (.SkillPointCheck && SkillPoint) {
		mes "กรุณาใช้สกิลพ้อยที่เหลืออยู่ของคุณ";
		mes "ก่อนใช้บริการ...";
		close;
	}

	.@eac = eaclass();
	.@i = ((.ThirdClass) ? roclass(.@eac&EAJ_UPPERMASK) : Class);
	if (.@i >= Job_Knight && .@i <= Job_Crusader2) {
		if (BaseLevel < .Rebirth[0] || JobLevel < .Rebirth[1]) {
			.@blvl = .Rebirth[0] - BaseLevel;
			.@jlvl = .Rebirth[1] - JobLevel;
			mes "คุณยังขาด " +
				((.@blvl > 0) ? .@blvl + " more base levels " + ((.@jlvl > 0) ? "/ " : "") : "") +
				((.@jlvl > 0) ? .@jlvl + " more job levels " : "") + "จึงจะสามารถเปลี่ยนเป็นอาชีพขั้นต่อไปได้... ขอบคุณที่ใช้บริการ.";
			close;
		}
		if (Class > Job_Crusader2) {
			mes "ยินดีต้อนรับ สู่ขั้นตอน";
			mes "การเปลี่ยนอาชีพ";
			mes "โปรดเลือก...";
			mes "อาชีพที่คุณต้องการ";
			next;
			Job_Menu(roclass(.@eac|EAJL_THIRD));
			close;
		}
		while(1) {
			mes "ยินดีต้อนรับ สู่ขั้นตอน";
			mes "การเปลี่ยนอาชีพ";
			mes "โปรดเลือก...";
			mes "อาชีพที่คุณต้องการ";
			next;
			.@i = select(" ^FF0000^000000 ^0055FFเปลี่ยนอาชีพ Hi-Class^000000:" + ((.ThirdClass) ? " ^FF0000^000000 ^FF0000เปลี่ยนอาชีพ Third Class^000000" : "") + ": ^FF0000^000000 ^777777ยกเลิก^000000");
			if (.@i == 3)
				close;
			mes "คุณแน่ใจแล้วใช่ไหม";
			mes "ที่ต้องการจะเปลี่ยน";
			mes "เป็นอาชีพนี้ ว่าไงล่ะ..?";
			next;
			Job_Menu(((.@i == 1) ? Job_Novice_High : roclass(.@eac|EAJL_THIRD)));
			mes "[Job Master]";
		}
	}
	.@j1 = roclass(.@eac|EAJL_2_1);
	.@j2 = roclass(.@eac|EAJL_2_2);
	if ((.@eac&EAJ_UPPERMASK) == EAJ_SUPER_NOVICE)
		setarray .@exp[0], roclass(.@eac|EAJL_THIRD), 99;
	if (Class == Job_Ninja || Class == Job_Gunslinger)
		setarray .@exp[0], .@j1, 70;
	if (.@exp[0] && .SecondExpanded) {
		if (BaseLevel < .Rebirth[0] || JobLevel < .@exp[1]) {
			.@blvl = .Rebirth[0] - BaseLevel;
			.@jlvl = .@exp[1] - JobLevel;
			mes "คุณยังขาด " +
				((.@blvl > 0) ? .@blvl + " more base levels " + ((.@jlvl > 0) ? "/ " : "") : "") +
				((.@jlvl > 0) ? .@jlvl + " more job levels " : "") + "จึงจะสามารถเปลี่ยนเป็นอาชีพขั้นต่อไปได้... ขอบคุณที่ใช้บริการ.";
			close;
		}
		mes "คุณต้องการเปลี่ยนอาชีพ";
		mes "เป็น " + jobname(.@exp[0]) + "ใช่ไหม?";
		next;
		Job_Menu(.@exp[0]);
		close;
	}
	if (.@eac&EAJL_2)
		if (.@eac&(EAJL_UPPER|EAJL_BABY) || roclass(.@eac|EAJL_UPPER) == -1) {
			mes "ข้าต้องขออภัยด้วย... !!? คุณไม่สามารถทำรายการใดๆ ได้อีก";
			mes "คุณอยู่ในอาชีพขั้นสูงสุดแล้ว..";
			close;
		}
	if ((.@eac&EAJ_BASEMASK) == EAJ_NOVICE) {
		if (JobLevel < .JobReq[0])
			mes "คุณจำเป็นต้องมีเลเวลจ๊อบมากกว่า " + .JobReq[0] + " สำหรับการเปลี่ยนอาชีพสู่ Class 1st.";
		else if (Class == Job_Novice_High && .LastJob && lastJob) {
			mes "ยินดีต้อนรับ สู่ขั้นตอน";
			mes "การเปลี่ยนอาชีพ";
			mes "โปรดเลือก...";
			mes "อาชีพที่คุณต้องการ";
			next;
			Job_Menu(roclass((eaclass(lastJob)&EAJ_BASEMASK)|EAJL_UPPER));
		} else {
			switch(Class) {
			case Job_Novice:
				Job_Menu(
					Job_Swordman,
					Job_Mage,
					Job_Archer,
					Job_Acolyte,
					Job_Merchant,
					Job_Thief,
					Job_SuperNovice,
					Job_Taekwon,
					Job_Gunslinger,
					Job_Ninja,
					Job_Baby
				);
				break;
			case Job_Novice_High:
				Job_Menu(
					Job_Swordman_High,
					Job_Mage_High,
					Job_Archer_High,
					Job_Acolyte_High,
					Job_Merchant_High,
					Job_Thief_High
				);
				break;
			case Job_Baby:
				Job_Menu(
					Job_Baby_Swordman,
					Job_Baby_Mage,
					Job_Baby_Archer,
					Job_Baby_Acolyte,
					Job_Baby_Merchant,
					Job_Baby_Thief,
					Job_Super_Baby
				);
				break;
			default:
				mes "เกิดข้อผิดพลาด โปรดติดต่อผู้ดูแลระบบ.";
				break;
			}
		}
		close;
	}
	if (roclass(.@eac|EAJL_2_1) == -1 || roclass(.@eac|EAJL_2_2) == -1)
		mes "ข้าต้องขออภัยด้วย... !!? คุณไม่สามารถทำรายการใดๆ ได้อีก.";
	else if (!(.@eac&EAJL_2) && JobLevel < .JobReq[1])
		mes "คุณจำเป็นต้องมีเลเวลจ๊อบมากกว่า " + .JobReq[1] + " สำหรับการเปลี่ยนอาชีพสู่ Class 2nd.";
	else if (.LastJob && lastJob && (.@eac&EAJL_UPPER)) {
		mes "ยินดีต้อนรับ สู่ขั้นตอน";
		mes "การเปลี่ยนอาชีพ";
		mes "โปรดเลือก...";
		mes "อาชีพที่คุณต้องการ";
		next;
		Job_Menu(lastJob + Job_Novice_High);
	} else
		Job_Menu(.@j1, .@j2);
	close;

function Job_Menu {
	while(1) {
		if (getargcount() > 1) {
			mes "ยินดีต้อนรับ สู่ขั้นตอน";
			mes "การเปลี่ยนอาชีพ";
			mes "โปรดเลือก...";
			mes "อาชีพที่คุณต้องการ";
			.@menu$ = "";
			for (.@i = 0; .@i < getargcount(); .@i++)
				.@menu$ = .@menu$ + " ~ " + jobname(getarg(.@i)) + ":";
			.@menu$ = .@menu$+" ^FF0000^000000 ^777777ยกเลิก^000000";
			next;
			.@i = getarg(select(.@menu$) - 1, 0);
			if (!.@i)
				close;
			if ((.@i == Job_SuperNovice || .@i == Job_Super_Baby) && BaseLevel < .SNovice) {
				mes "คุณจำเป็นต้องเลเวลขั้นต่ำคือ " + .SNovice + " สำหรับขั้นตอนการเปลี่ยนอาชีพเป็น " + jobname(.@i) + ".";
				close;
			}
			mes "คุณแน่ใจแล้วใช่ไหม";
			mes "ที่ต้องการจะเปลี่ยน";
			mes "เป็นอาชีพนี้ ว่าไงล่ะ..?";
			next;
		} else
			.@i = getarg(0);
		if (select(" ^FF0000^000000 แน่นอน! ข้าจะเปลี่ยนเป็น  ^0055FF" + jobname(.@i) + "^000000 Class: ^FF0000^000000 ^777777" + ((getargcount() > 1) ? "เมนูก่อนหน้า" : "ยกเลิก") + "^000000") == 1) {
			mes "ขอแสดงความยินดีด้วย";
			mes "ตอนนี้คุณได้เลื่อนขั้น";
			mes "สู่สายอาชีพ " + callfunc("F_InsertArticle", jobname(.@i)) + "แล้ว!";
			if (.@i == Job_Novice_High && .LastJob)
				lastJob = Class;
			jobchange .@i;
			if (.@i == Job_Novice_High)
				resetlvl(1);
			else if (.@i == Job_Baby) {
				resetstatus;
				resetskill;
				set SkillPoint,0;
			}
			specialeffect2 EF_ANGEL2;
			specialeffect2 EF_ELECTRIC;
			if (.Platinum)
				callsub Get_Platinum;
			close;
		}
		if (getargcount() == 1)
			return;
		mes "[Job Master]";
	}
	end;
}

Get_Platinum:
	skill "NV_FIRSTAID",1,SKILL_PERM;
	switch (BaseClass) {
	case Job_Novice:
		if (Class != Job_SuperNovice)
			skill "NV_TRICKDEAD",1,SKILL_PERM;
		break;
	case Job_Swordman:
		skill "SM_MOVINGRECOVERY",1,SKILL_PERM;
		skill "SM_FATALBLOW",1,SKILL_PERM;
		skill "SM_AUTOBERSERK",1,SKILL_PERM;
		break;
	case Job_Mage:
		skill "MG_ENERGYCOAT",1,SKILL_PERM;
		break;
	case Job_Archer:
		skill "AC_MAKINGARROW",1,SKILL_PERM;
		skill "AC_CHARGEARROW",1,SKILL_PERM;
		break;
	case Job_Acolyte:
		skill "AL_HOLYLIGHT",1,SKILL_PERM;
		break;
	case Job_Merchant:
		skill "MC_CARTREVOLUTION",1,SKILL_PERM;
		skill "MC_CHANGECART",1,SKILL_PERM;
		skill "MC_LOUD",1,SKILL_PERM;
		break;
	case Job_Thief:
		skill "TF_SPRINKLESAND",1,SKILL_PERM;
		skill "TF_BACKSLIDING",1,SKILL_PERM;
		skill "TF_PICKSTONE",1,SKILL_PERM;
		skill "TF_THROWSTONE",1,SKILL_PERM;
		break;
	default:
		break;
	}
	switch (BaseJob) {
	case Job_Knight:
		skill "KN_CHARGEATK",1,SKILL_PERM;
		break;
	case Job_Priest:
		skill "PR_REDEMPTIO",1,SKILL_PERM;
		break;
	case Job_Wizard:
		skill "WZ_SIGHTBLASTER",1,SKILL_PERM;
		break;
	case Job_Blacksmith:
		skill "BS_UNFAIRLYTRICK",1,SKILL_PERM;
		skill "BS_GREED",1,SKILL_PERM;
		break;
	case Job_Hunter:
		skill "HT_PHANTASMIC",1,SKILL_PERM;
		break;
	case Job_Assassin:
		skill "AS_SONICACCEL",1,SKILL_PERM;
		skill "AS_VENOMKNIFE",1,SKILL_PERM;
		break;
	case Job_Crusader:
		skill "CR_SHRINK",1,SKILL_PERM;
		break;
	case Job_Monk:
		skill "MO_KITRANSLATION",1,SKILL_PERM;
		skill "MO_BALKYOUNG",1,SKILL_PERM;
		break;
	case Job_Sage:
		skill "SA_CREATECON",1,SKILL_PERM;
		skill "SA_ELEMENTWATER",1,SKILL_PERM;
		skill "SA_ELEMENTGROUND",1,SKILL_PERM;
		skill "SA_ELEMENTFIRE",1,SKILL_PERM;
		skill "SA_ELEMENTWIND",1,SKILL_PERM;
		break;
	case Job_Rogue:
		skill "RG_CLOSECONFINE",1,SKILL_PERM;
		break;
	case Job_Alchemist:
		skill "AM_BIOETHICS",1,SKILL_PERM;
		break;
	case Job_Bard:
		skill "BA_PANGVOICE",1,SKILL_PERM;
		break;
	case Job_Dancer:
		skill "DC_WINKCHARM",1,SKILL_PERM;
		break;
	default:
		break;
	}
	return;

OnInit:
	setarray .Rebirth[0],99,50;	//  กำหนดเลเวลและเลเวลจ๊อบขั้นต่ำในการเปลี่ยนอาชีพ Hi-Class ปกติควรใส่ 99,50
	setarray .JobReq[0],10,40;	//  กำหนดเลเวลและเลเวลจ๊อบขขั้นต่ำในการเปลี่ยนอาชีพ Classs 1 ปกติควรใส่ 10,40
	.ThirdClass = 1;			//  ต้องการเปิดใช้งานอาชีพ Class 3 หรือไม่ (1: ใช่ / 0: ไม่)
	.SecondExpanded = 1;		// เปิดใช้งานสายอาชีพ : Ex. Super Novice, Kagerou/Oboro, Rebellion หรือไม่? (1: ใช่ / 0: ไม่)
	.SNovice = 45;				// กำหนดเลเวลต่ำสุดในการเปลี่ยนอาชีพ Super Novice ปกติควรใส่ 45
	.LastJob = 1;				// ตรวจสายอาชีพก่อนการเปลี่ยนอาชีพหรือไม่? (1: ใช่ / 0: ไม่)
	.SkillPointCheck = 1;		// ตรวจสกิลพ้อยท์ที่หลงเหลืออยู่ของตัวละครหรือไม่? (1: ใช่ / 0: ไม่)
	.Platinum = 1;				// ได้รับสกิลเสริมโดยอัตโนมัติหรือไม่? (1: ใช่ / 0: ไม่)
	end;
}
