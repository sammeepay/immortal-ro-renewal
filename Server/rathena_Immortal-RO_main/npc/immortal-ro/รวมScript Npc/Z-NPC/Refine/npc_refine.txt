//=============================================================
// - Edit Version - 1.1
// - BY exsoulionzzzz
//=============================================================

-	script	refineee	-1,{
	//set .@features,getarg(1);
	mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
	mes "ข้าสามารถอัพเกรดอุปกรณ์ให้ท่านได้อย่างเหลือเชื่อ";
	mes "เลือกอุปกรณ์ที่ท่านจะ Upgrade ด้วยแร่ Elunium & Oridecon เลยครับ";
	next;

	setarray .@position$[1], "ส่วนหัว    ","ชุดเกราะ ","มือซ้าย   ","มือขวา    ","ผ้าคลุม   ","รองเท้า   ","เครื่องประดับ 1","เครื่องประดับ 2","ส่วนหู","ส่วนปาก";
	set .@menu$,"";
	for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
	{
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + " - " + "[ ^FF0000" + getequipname(.@i) + " ^000000]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);
	if(!getequipisequiped(.@part)) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "You're not wearing";
		mes "anything there that";
		mes "I can refine.";
		emotion 6;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "อุปกรณ์ชิ้นนี้ไม่สามารถอัพเกรดได้ ?";
		close;
	}
	//Check if the item is identified... (Don't know why this is in here... but kept it anyway)
	if(!getequipisidentify(.@part)) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "You can't refine this";
		mes "if you haven't appraised";
		mes "it first. Make sure your";
		mes "stuff is identified before";
		mes "I can refine it.";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "ไอเท็มชิ้นนี้มาถึงระดับสุดยอดแล้ว";
		mes "ไม่สามารถอัพเกรดเพิ่มได้อีก ?";
		close;
	}
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	switch(getequipweaponlv(.@part)){
	case 0: 	//Refine Armor
		set .@price,2000;
		set .@material,985;
		set .@safe,4;
		break;
	case 1: 	//Refine Level 1 Weapon
		set .@price,50;
		set .@material,1010;
		set .@safe,7; 
		break;
	case 2: 	//Refine Level 2 Weapon
		set .@price,200;
		set .@material,1011;
		set .@safe,6; 
		break;
	case 3: 	//Refine Level 3 Weapon
		set .@price,5000;
		set .@material,984;
		set .@safe,5; 
		break;
	case 4: 	//Refine Level 4 Weapon
		set .@price,20000;
		set .@material,984;
		set .@safe,4; 
		break;
	case 5: 	//Refine other stuff?
		set .@price,2000;
		set .@material,985;
		set .@safe,4; 
		break;
	}
	if(.@features != 1) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";

		mes "ต้องมี ^003366"+getitemname(.@material)+"^000000 และ";
		mes "ค่าบริการ " + .@price + " Zeny.";
		mes "ท่านยืนยันหรือไม่ ?";
		next;
		if(select("- ใช่:- ไม่") == 2){
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "ฉันเข้าใจ....";
			mes "ไว้พบกันโอกาสหน้านะท่าน";
			close;
		}
		if(getequippercentrefinery(.@part) < 100) {
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "ข้าจะขอบอกท่านไว้ก่อน";
			mes "การที่จะอัพเกรดอุปกรณ์ชิ้นนี้มีโอกาสเสียหายนะ";
			switch(.@material) {
			case 985:
				mes "หากสิ่งของชิ้นนี้มี Card อยู่ถ้าสำเร็จก็อยู่ หากล้มเหลวก็เสียหายพร้อมอุปกรณ์ชิ้นนี้ทันที";
				break;
			default:
				mes "ท่านแน่ใจมากแค่ไหน";
				break;
			}
			next;
			if(select("- ใช่:- ไม่") == 2){
				mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
				mes "";
				//mes "I might be a great refiner, but sometimes even I make mistakes.";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "ไม่สามารถดำเนินการได้";
			mes "คุณมี Zeny ไม่พอที่จะอัพเกรด หรือ "+getitemname(.@material)+" ไม่มี...";
			mes "แล้วค่อยกลับมาหาข้า";
			close;
		}
		set Zeny,Zeny-.@price;
		delitem .@material,1;
		if(getequipisequiped(.@part) == 0) { // hacker has removed the item (not changed, why?)
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "ดูเหมือนว่าท่านจะขาดอะไรไป โปรดตรวจสอบ.";
			close;
		}
		if(getequiprefinerycnt(.@part) != .@refinerycnt || getequipid(.@part) != .@refineitemid) { // hacker has changed the item
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			Emotion e_an;
			mes "รอสักครู่...";
			mes "คุณคิดว่าฉันโง่!";
			mes "คุณจะเปลี่ยนรายการในขณะที่ฉันไม่ได้มอง! ออกจากที่นี่!";
			close;
		}
		if(getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			set .@emo,rand(1,5);
			if (.@emo == 1) {
				Emotion e_cash;
			} else {
				Emotion e_swt;
			}
			set .@lose,rand(1,3);
			if (.@lose == 1) {
				mes "OH! MY GOD!";
				mes "ข้าทำมันเสียหายแล้วท่านนน!";
				mes "ขออภัยจริง จริง.";
			} else if(.@lose == 2) {
				mes "Nooooooo!";
				mes "อุปกรณ์เสียหายสะแล้ว!";
				mes "ขออภัยจริง จริง.";
			} else {
				mes "จบ!";
				mes "ข้าไม่สามารถอัพเกรดมันสำเร็จ";
				mes "ขออภัยจริง จริง.";
			}
			close;
		}
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		successrefitem .@part;
		Emotion e_heh;
		set .@win,rand(1,3);
		if (.@win == 1) {
			mes "สมบูรณ์แบบ !";
			mes "เย้ เย้!";
			mes "แล้วพบกันใหม่ ~,";
		} else if(.@win == 2) {
			mes "สำเร็จ...!";
			mes "ช่างน่าดีใจยิ่งนัก";
			mes "พบกันใหม่ ~";
		} else {
			mes "เย้ เย้!";
			mes "ข้าไม่เคยตีอุปกรณ์พลาดอยู่แล้วว";
			mes "แล้วพบกันใหม่ ~";
		}
		close;
	}

// New Refining Functions ========================
	if(getequiprefinerycnt(.@part) < .@safe) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "ฉันสามารถปรับแต่งนี้เพื่อ จำกัด ปลอดภัยหรือจำนวนที่ต้องการของครั้ง ... เป็นทางเลือกที่ของคุณ...";
		next;
		set .@menu2,select(" - ตีถึงขั้นปลอดภัย.","I'll จำกัดจำนวนตี.","- ฉันไม่ต้องการ...");
	} else set .@menu2,2;
	switch(.@menu2){
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		next;
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "ดังนั้นวิธีการหลายครั้งที่คุณต้องการฉันเพื่อปรับแต่งรายการของคุณหรือไม่";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "[" + getarg(0) + "]";
			mes "ฉันไม่สามารถปรับแต่งรายการนี้.";
			close;
		}
		if(.@refinecheck > .@safe) {
			set .@refinecheck,.@refinecheck - .@safe;
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "นี้จะพยายามที่จะปรับแต่งอุปกรณ์ " + .@refinecheck + " อุปกรณ์ของคุณอาจถูกทำลาย ... ok ";
			next;
			if(select("Yes...","No...") == 2){
				mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
				mes "คุณกล่าวว่าดังนั้น .. อืมช่างมันเถอะ...";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "คุณกล่าวว่าดังนั้น .. อืมช่างมันเถอะ...";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
	mes "That will cost you " + .@refinecnt + " " + getitemname(.@material) + " and " + .@fullprice + " Zeny. Is that ok?";
	next;
	if(select("Yes","No...") == 2){
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "คุณกล่าวว่าดังนั้น .. อืมช่างมันเถอะ...";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
		mes "นั่นคือทั้งหมดที่คุณมี? แต่น่าเสียดายที่ฉันไม่สามารถทำงานให้คุณในราคาที่ต่ำ ลองใส่ตัวเองในรองเท้าของฉัน.";
		close;
	}
	set Zeny,Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "ดูที่นี่ ... คุณไม่ได้มีรายการใด ๆ บน...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "Clan... ไม่มีคุณไม่ แต่คิดว่าฉันอาจจะโง่ดังนั้น!! !?!";
			mes "คุณมีการเปลี่ยนแปลงมัน...";
			mes "ออกไปก่อนที่ผมจะช็อตคุณด้วยค้อนของฉัน!";
			close;
		} 
		mes "Clang, clang!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion 23;
			mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
			mes "WAHHHH! ฉันขอโทษ ... ผมเตือนคุณแล้วนี้อาจเกิดขึ้น...";
			set .@refinecnt,.@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "ที่นี่ไม่ได้ใช้และ Zeny กลับของวัสดุ...";
			getitem .@material,.@refinecnt;
			set .@fullprice,.@refinecnt * .@price;
			set Zeny,Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion 21;
		set .@refinecnt,.@refinecnt - 1;
		next;
	}
	mes "[ ^FF0000ช่างตีบวกระดับโลก ^000000]";
	mes "เสร็จสิ้น ไว้พบกันโอกาสหน้า...";
	close;		
}

morocc,134,79,4	duplicate(refineee)	อัพเกรดอุปกรณ์	63
