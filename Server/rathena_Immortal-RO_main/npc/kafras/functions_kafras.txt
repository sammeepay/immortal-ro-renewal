//===== rAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= These functions handle เซฟ, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed เซฟ from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to เซฟ your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/เซฟ arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to บริการคลังเก็บของ
//=   arg(4): Cost to บริการเช่ารถเข็น
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome){
		default:
		case 0:
			// Default message (obsolete)
			mes "[Kafra Employee]";
			mes "ยินดีต้อนรับเข้าสู่";
			mes "Kafra คอร์ปอเรชั่น.";
			mes "จะทำงานเพื่อให้คุณ";
			mes "ได้รับบริการที่สะดวกสบาย.";
			mes "ต้องการให้ช่วยเหลืออะไรเพค๊ะ";
			break;
		case 1:
			// Niflheim specific message
			mes "[Kafra Employee]";
			mes "^666666ยินนนนนนนดี...............ต้อนรับ สู่ Kafraaaa corp.........Kafra";
			mes "Corp...ที่ตายไปแล้วววว........ยินนนดีให้บริการรรรรร...";
			mes "...ต้อง...การให้ข้าช่วยอะไรรือ...?^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[Kafra Employee]";
	 		mes "Welcome. ^ff0000" + GetGuildName(@GID) + "^000000 Member.";
			mes "The Kafra Coporation will stay with you wherever you go.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Kafra Employee]";
			mes "So, have you come from a faraway land to study our culture, or are you just sightseeing?";
			mes "In either case, why not stay awhile?";
			mes "The air is eternally heavy with the";
			mes "scent of pleasant wildflowers.";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Kafra Employee]";
			mes "With our many Kafra";
			mes "service locations, you're never";
			mes "far from home.";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (1) {
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (.@welcome == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"บริการคลังเก็บของ","บริการคลังเก็บของ Guild","บริการเช่ารถเข็น","บริการ Teleport","ยกเลิก";
		} else {
			switch(.@menu_num){
				// เซฟ and Storage only
				case 1:	setarray @K_Menu0$[0],"เซฟ","บริการคลังเก็บของ","ยกเลิก"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"บริการคลังเก็บของ","ยกเลิก"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"เซฟ","บริการคลังเก็บของ","บริการเช่ารถเข็น","เช็คข้อมูลอื่นๆ","ยกเลิก"; break;
				// Case 4 is Einbroch no tele message.
				// No เซฟ, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"บริการคลังเก็บของ","บริการเช่ารถเข็น","เช็คข้อมูลอื่นๆ","ยกเลิก"; break;
				// Storage and เช็คข้อมูลอื่นๆ only.
				case 6:	setarray @K_Menu0$[0],"บริการคลังเก็บของ","เช็คข้อมูลอื่นๆ","ยกเลิก"; break;
				// เซฟ, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"เซฟ","บริการคลังเก็บของ","บริการเช่ารถเข็น","ยกเลิก"; break;
				// เซฟ, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"เซฟ","บริการคลังเก็บของ","เช็คข้อมูลอื่นๆ","ยกเลิก"; break;
				// No เซฟ (Rune Knight)
				case 9: setarray @K_Menu0$[0],"บริการคลังเก็บของ","บริการเช่ารถเข็น","บริการ Teleport","เช็คข้อมูลอื่นๆ","ยกเลิก"; break;
				// Storage, เซฟ, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"บริการคลังเก็บของ","เซฟ","บริการเช่ารถเข็น","ยกเลิก"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"เซฟ","บริการคลังเก็บของ","บริการ Teleport","บริการเช่ารถเข็น","เช็คข้อมูลอื่นๆ","ยกเลิก"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "เซฟ") {
			return;
		} else if (@K_Menu0$[.@j] == "บริการคลังเก็บของ") {
			// Do not charge for Guild Storage
			if(.@welcome == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		} else if (@K_Menu0$[.@j] == "บริการ Teleport") {
			// Display Einbroch "No Teleport Service" notice.
			if(.@menu_num != 4) callfunc "F_KafTele",.@welcome;
			else {
				mes "[Kafra Employee]";
				mes "Because of the ^FF0000Limited";
				mes "Transport Agreement^000000, the";
				mes "Kafra Corporation cannot";
				mes "provide Teleport Services";
				mes "in the Schwaltzvalt Republic.";
				next;
				mes "[Kafra Employee]";
				mes "We ask that you please";
				mes "use the Airship Service";
				mes "instead. Thank you for your";
				mes "understanding and cooperation.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "บริการเช่ารถเข็น") {
			if(callfunc("F_KafCart",.@welcome,getarg(4)) == 1) 
				next;
		} else if (@K_Menu0$[.@j] == "เช็คข้อมูลอื่นๆ") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "ยกเลิก"){
			callfunc "F_KafEnd",.@welcome,0;
			end;
		} else if (@K_Menu0$[.@j] == "บริการคลังเก็บของ Guild") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if(.@type == 1){
		if(guildopenstorage()){
			mes "[Kafra Employee]";
			mes "ดิฉันของโทษ ขณะนี้มีสมาชิก Guild อื่นใช้บริการ";
			mes "ของเราอยู่  กรุณารอซักครู่ นะเคะ เอ้ย! นะค๊ะ~*";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(!callfunc("F_CanOpenStorage")){
		mes "[Kafra Employee]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-ขะ-ข ขอโทษษษษษษ,";
			mes "คะ-คุณหนู อะ-อะ-อ๊าาาาา";
			mes "Nooviiice... ตะ-ต้องงงงงการ";
			mes "B-basic sssskill l-level 6...^000000";
			return;
		}
		mes "ขอโทษนะค๊ะ"; 
		mes "หนูต้องมี Novice's"; 
		mes "Basic Skill ถึง Level 6";
		mes "เพื่อเปิดใช้บริการฝากของนะจ๊ะ หนูน้อย";
		mes "?(??`)ฺ";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(.@type != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<.@fee){
				mes "[Kafra Employee]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "งะ-เงิน z-zeny...!";
					mes "ตะ-ต้องการ 150... zeny...";
					mes "อ๊าาาา! T-taking นะ-นรก~!^000000";
					return;
				}
				// Standard Message
				mes "ดิฉันขอโทษ"; 
				mes "คุณมี zeny ไม่พอ";
				mes "สำหรับการใช้บริการรับฝากของค่ะ";
				mes "ต้องมีค่าบริการ "+.@fee+" zeny นะค๊ะ ";
				return;
			}
			set Zeny, Zeny-.@fee;
			set RESRVPTS, RESRVPTS + (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Kafra Employee]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666ขอบคุณ.. สำหรับการ... ใช้งานเพค๊ะ...^000000";
		}
		// Normal message
		else {
			mes "[Kafra Employee]";
			mes "ตอนนี้ ดิฉันได้เปิด";
			mes "คลังเก็บของ ของคุณ";
			mes "ขอบคุณสำหรับการใช้";
			mes "บริการ Kafra ค่ะ~* m(_ _)m^";
			mes "^ff0000 ***คลังเก็บของที่ 2 นี้สามารถเปิดใช้งานได้สมาชิก VIP เท่านั้น. คลังเก็บของที่ 3 นี้สามารถเข้าถึงได้ด้วยไอเท็ม Kafra Card.*** ^000000";
		}
	}
	next;
	if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
		setarray @K_M_Storage0$[0],"^0000ffเปิดคลังเก็บของหลัก^000000";
	} else { //VIP
		setarray @K_M_Storage0$[0],"^0000ffเปิดคลังเก็บของหลัก^000000","เปิดคลังเก็บของที่ 2 - ^ff0000Yggdrasil^000000";
	}
	set .@menu$, "";
	for (set .@i, 0; .@i < getarraysize(@K_M_Storage0$); set .@i, .@i + 1)
		set .@menu$, .@menu$ + @K_M_Storage0$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	callfunc("F_CheckKafCode");	//check your storage password, if set
	if(@K_M_Storage0$[.@j] == "^0000ffเปิดคลังเก็บของหลัก^000000") { close2; openstorage; }
	else if(@K_M_Storage0$[.@j] == "เปิดคลังเก็บของที่ 2 - ^ff0000Yggdrasil^000000") { close2; openstorage2 1,STOR_MODE_GET|STOR_MODE_PUT; }
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	if (strnpcinfo(4) == "umbala") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			mes "[Kafra Employee]";
			mes "ดิฉันต้องขอโทษด้วย แต่บริการนี้จะใช้ได้เฉพาะกับผู้ที่มีสถานะ VIP เท่านั้น";
			close2;
			end;
		}
		//VIP
		//Next Mes
	}
	mes "[Kafra Employee]";
	mes "กรุณาเลือก";
	mes "จุดหมายปลายทางของคุณค่ะ";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "ยกเลิก") return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[Kafra Employee]";
			mes "ดิฉันต้องขอโทษด้วย";
			mes "คุณต้องมี zeny เพื่อใช้บริการ Teleport ";
			mes "ค่าธรรมเนียมในการ Teleport";
			mes "ไป "+@wrpD$[.@j]+" จำนวน "+@wrpP[.@j]+" zeny ค่ะ~*";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to บริการเช่ารถเข็น is a merchant
	if(BaseClass != Job_Merchant){
		mes "[Kafra Employee]";
		mes "ขอความกรุณาด้วยนะค๊ะ";
		mes "บริการเช่ารถเข็น";
		if (checkre(0))
			mes "เป็นบริการของ Merchant class นะค๊ะ";
		else {
			mes "ใช้ได้เฉพาะอาชีพ Merchants,";
			mes "Blacksmiths, Master Smiths,";
			mes "Alchemists and Biochemists.";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Kafra Employee]";
		mes "เอ๊......!";
		mes "คุณมีรถเข็นอยู่แล้วนิ";
		mes "น่าเสียดายเนอะ ที่พวกเราไม่สามารถ";
		mes "ให้เช่าได้มากกว่าหนึ่งคัน";
		mes "เพราะมันจะลำบาคนะค๊ะ (~_~;)";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if(getskilllv("MC_PUSHCART") == 0) {
		mes "[Kafra Employee]";
		mes "คุณสามารถเช่ารถเข็นได้ก็ต่อเมื่อคุณมี \"Push Cart\" skill นะค๊ะ~*";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Kafra Employee]";
		mes "การเช่ารถเข็น";
		mes "มีค่าธรรมเนียม "+getarg(1)+" zeny";
		mes "คุณต้องการใช้บริการเช่ารถเข็นหรือเปล่าค๊ะ?";
		next;
		if (select("บริการเช่ารถเข็น.:ยกเลิก") == 2) return 0;
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				mes "ขอโทษนะค๊ะ แต่คุณ";
				mes "มียอดเงินคงเหลือไม่เพียงพอ";
				mes "สำหรับการใช้บริการกรุณาเติม  Zeny";
				mes "ค่าธรรมเนียมคือ "+getarg(1)+" zeny";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "เช็คคะแนนสะสม","บริการ Password คลังเก็บของ","Kafra Employee Locations","ยกเลิก";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "เช็คคะแนนสะสม") {
			mes "[Kafra Employee]";
			mes "มาดูกัน...";
			mes strcharinfo(0) + "...";
			mes "อ๊ะ, คุณมีทั้งหมด";
			mes RESRVPTS+ " คะแนนสะสม";
			next;
			mes "[Kafra Employee]";
			mes "คุณสามารถใช้";
			mes "คะแนนสะสมเพื่อแลก";
			mes "รางวัลที่สำนักงานใหญ่ Kafra ใน Al De Baran";
			mes "โปรดใช้บริการของเราเพื่อผลประโยชน์ แก่ตัวท่านเองค่ะ~*";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "Kafra Employee Locations") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "บริการ Password คลังเก็บของ") {
			callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if เซฟ message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Kafra Employee]";
	if(getarg(1)==1) {	
		// เซฟ
		mes "จุดเซฟของคุณ";
		mes "ได้ถูกบันทึกไว้ที่";
		mes getarg(2);
		mes "ขอบคุณที่ใช้บริการ";
		mes "ของ Kafra ค่ะ";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "พวกเราที่ Kafra คอร์ปอเรชั่น";
		mes "เราจะทำให้ท่านได้รับบริการให้ดีที่สุด เราหวังว่าเราจะเป็นส่วนหนึ่งของการผจญภัยขอบคุณเป็นอย่างดีค่ะ";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666Kaffffra มะ-แม้";
		mes "ตายยยยยยยยยย. กะ...";
		mes "ก็ จะ-จะอยู่ คะ-เคียง ขะ-ข้าง  คะ-คุณ^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "กรุณาใส่ Password คลังเก็บของค่ะ";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "Password คลังเก็บของไม่ถูกต้องค่ะ.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if(#kafra_code) {
		mes "คลังเก็บของของคุณถูกป้องกันด้วย Password คุณต้องการจะทำรายการ ณ ตอนนี้เลยหรือไม่?";
		next;
		switch (select("ทำการเปลี่ยน Password เก่า -> 5000z:ยกเลิก Password คลังเก็บของ -> 1000z:ยกเลิก")) {
			case 1:
				mes .@npc_name$;
				mes "เริ่มการใช้งาน  กรุณาป้อน ^0000FFPassword เก่า^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "Password ผิดค่ะ กรุณาลองใหม่อีกครั้งค่ะ";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,.@npc_name$,.@comp_name$;
				break;
			case 2:
				mes .@npc_name$;
				mes "กรุณา  ใส่ Password เพื่อยกเลิกบริการค่ะ";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "Password ยังไม่ได้ถูกลบออกค่ะ";
					emotion e_hmm;
					break;
				}
				next;
				mes .@npc_name$;
				if(Zeny < 1000) {
					mes "คุณมี zeny ไม่เพียงพอค่ะ!";
					emotion e_cash;
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "Password ของคุณถูกยกเลิกเรียบร้อยแล้วค่ะ";
					mes "ขอบคุณที่ใช้บริการค่ะคุณ "+.@comp_name$+".";
					emotion e_thx;
				} else {
					mes "Password ผิดพลาด เราจะไม่รับผิดชอบกับเงิน 1000z ของคุณนะค๊ะ~*";
					mes "ครั้งหน้ากรุณาป้อน Password ที่ถูกต้องด้วยนะค๊ะ~* (^O^)";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes .@comp_name$+" ภูมิใจนำเสนอบริการใหม่";
		mes "เพิ่มความมั่นใจในการใช้บริการคลั้งเก็บของด้วย Password";
		next;
		switch (select("ป้อน Password ใหม่ค่ะ -> 5000z:ยกเลิก")) {
			case 1: callsub S_SET,.@npc_name$,.@comp_name$; break;
			case 2: break;		
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes .@npc_name$;
	mes "โปรดใส่ ^FF0000Password ใหม่^000000 เพื่อความปลอดภัยจากการโจรกรรมนะค๊ะ";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "Password ยังไม่ได้รับการเปลี่ยนแปลงค่ะ";
		emotion e_hmm;
		return;
	}
	next;
	mes .@npc_name$;
	if(Zeny < 5000) {
		mes "ทำไมไม่พกเงินมาด้วยหละค๊ะ";
		emotion e_cash;
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "Password จะเก็บไวเป็นความลับสุดยอดเลยค่ะ";
	mes "ขอบคุณที่ใช้บริการค่ะ "+.@comp_name$+".";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "กรุณาป้อนตัวเลข 1000~10000000 ค่ะ";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "มันง่ายเกินไปปะค๊ะ กรุณาลองใหม่อีกครั้งค่ะ";
	}
	if(input(@code_) == 1) {
		mes "มันยาวเกินไปหรือเปล่าค๊ะ กรุณาลองใหม่อีกครั้งค่ะ หึๆ";
		return 0;
	}
	if(@code_<1000) {
		mes "มันสั้นไปป่าวค๊ะ กรุณาลองใหม่อีกครั้งนะค๊ะ หึๆ";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta", "Al De Baran", "Comodo", "Umbala";
			setarray @wrpP[0], 2000, 2000, 2000, 2000, 2000, 2000, 3000, 3000;
		} else { //VIP
			setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta", "Al De Baran", "Comodo", "Umbala";
			setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800, 2000, 3000, 3000;
		}
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpP[0], 2000;
			setarray @wrpD$[0], "Payon";
		} else { //VIP
			setarray @wrpP[0], 1200, 1800, 1800;
			setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		}
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpP[0], 2000;
			setarray @wrpD$[0], "Geffen";
		} else { //VIP
			setarray @wrpP[0], 1800, 1200, 1200, 1800, 1700;
			setarray @wrpD$[0], "Prontera", "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
		}
	} else if (strnpcinfo(4) == "comodo" ) {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Morroc";
			setarray @wrpP[0], 2000;
		} else { //VIP
			setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon", "Umbala";
			setarray @wrpP[0], 1800, 1200, 1800;
		}
	} else if (strnpcinfo(4) == "cmd_fild07") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Comodo", "Morroc";
			setarray @wrpP[0], 1200, 1200;
		} else { //VIP
			setarray @wrpD$[0], "Comodo", "Morroc";
			setarray @wrpP[0], 1200, 1200;
		}
	} else if (strnpcinfo(4) == "geffen") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Prontera";
			setarray @wrpP[0], 2000;
		} else { //VIP
			setarray @wrpD$[0], "Prontera", "Al De Baran", "Payon", "Morroc", "Orc Dungeon", "Mjolnir Dead Pit";
			setarray @wrpP[0], 1200, 1200, 1200, 1200, 1700, 1700;
		}
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Prontera";
			setarray @wrpP[0], 2000;
		} else { //VIP
			setarray @wrpD$[0], "Prontera", "Geffen", "Payon", "Morroc", "Al De Baran";
			setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
		}
	} else if (strnpcinfo(4) == "morocc") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Prontera";
			setarray @wrpP[0], 2000;
		} else { //VIP
			setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Geffen", "Al De Baran", "Comodo", "Comodo Pharos Beacon";
			setarray @wrpP[0], 1200, 1200, 1200, 1200, 1800, 1800, 1200;
		}
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Comodo";
			setarray @wrpP[0], 3000;
		} else { //VIP
			setarray @wrpD$[0], "Comodo";
			setarray @wrpP[0], 1800;
		}
	} else if (strnpcinfo(4) == "payon") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Prontera";
			setarray @wrpP[0], 2000;
		} else { //VIP
			setarray @wrpD$[0], "Prontera", "Geffen", "Al De Baran", "Alberta", "Morroc";
			setarray @wrpP[0], 1200, 1200, 1200, 1200, 1200;
		}
	} else if (strnpcinfo(4) == "yuno") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Einbroch", "Lighthalzen", "Hugel", "Rachel";
			setarray @wrpP[0], 2000, 2000, 2000, 2000;
		} else { //VIP
			setarray @wrpD$[0], "Al De Baran", "Einbroch", "Lighthalzen", "Hugel", "Rachel";
			setarray @wrpP[0], 1200, 1200, 1200, 1200, 1200;
		}
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		if (VIP_SCRIPT && !vip_status(1)) { //NON-VIP
			setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
			setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
		} else { //VIP
			setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
			setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
		}
	}

	// VIP pricing varies across the regional servers; this is most practical.
	//if (VIP_SCRIPT && !vip_status(1)) {
	//	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
	//		setd "@wrpP["+.@i+"]",getd("@wrpP["+.@i+"]") * 2;
	//}

	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]")+" z";
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","ยกเลิก";
	return;
}
