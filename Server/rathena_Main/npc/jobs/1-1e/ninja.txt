//===== rAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//= 1.6 Fixed AEGIS amatsu warp typo. [Kisuka]
//= 1.7 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//============================================================ 

alberta,30,65,3	script	Akagi	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[Akagi]";
			mes "อืมม...";
			mes "ท่านต้องรู้สึกว่า";
			mes "มีคนกำลังรอท่านอยู่ที่นี่";
			mes "บอกข้า ท่านกำลังแสวงหา";
			mes "เส้นทางแห่งความอดทน";
			mes "หรือไม่?";
			next;
			if(select("ไม่:ใช่") == 1) {
				mes "[Akagi]";
				mes "ข้าเห็น";
				mes "ข้าคิดว่า";
				mes "ขึนอยู่กับคน";
				mes "ดูแลตัวเองด้วย";
				close;
			}
			mes "[Akagi]";
			mes "ดีมาก";
			mes "จากนั้น ให้ข้า";
			mes "นำพาท่าน ไปยัง";
			mes "เส้นทางนั้นทันที...";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,176;
					end;
			}
		} else {
			mes "[Akagi]";
			mes "หืม? ข้าไม่สามารถ";
			mes "ให้บริการใด ๆ กับท่าน";
			mes "จนกว่าท่านจะเติบโต";
			mes "และ แข็งแกร่งขึ้น...";
			close;
		}
	} else {
		mes "[Akagi]";
		mes "อืม...";
		mes "ท่านและข้า...";
		mes "เรามีความเท่าเทียมกัน";
		mes "ในแง่ของความสามารถใน";
		mes "การต่อสู้ บางทีเราสามารถ";
		mes "ต่อสู้กันได้";
		close;
	}
}

que_ng,30,65,3	script	Kuuga Gai#nq	730,{
	if (Upper == 2) {
		mes "[Kuuga Gai]";
		mes "ข้า... ข้า";
		mes "ไม่เคยเห็นเด็ก";
		mes "ที่มีพลังเท่าท่าน";
		mes "ออกไป ท่าน";
		mes "ประหลาดเหนือธรรมชาติ!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[Kuuga Gai]";
			mes "หืม? ท่านมาเรียนรู้";
			mes "วิธีการเป็น Ninja?";
			mes "ท่านยังมีประสบการณ์";
			mes "ไม่เพียงพอ ดังนั้น";
			mes "โปรดกลับมาหลังจากที่ท่าน";
			mes "คุ้นเคยกับการต่อสู้สัตว์ประหลาด";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "ขออนุญาต";
			mes "สวัสดี?";
			next;
			mes "[Kuuga Gai]";
			mes "...............................";
			mes "ท่านทำได้อย่างไร?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ทำอะไร? ข้าไม่ได้";
			mes "ทำอะไร ข้าไม่คิดว่า...";
			next;
			mes "[Kuuga Gai]";
			mes "ท่านเห็นข้าได้อย่างไร";
			mes "ข้าควรจะมองไม่เห็น";
			mes "ด้วยตาเปล่า อ๊า ตอนนี้";
			mes "ข้าเข้าใจแล้ว Wildcat Joe";
			mes "ต้องส่งท่านมาฆ่าข้า ข้าจะไม่ตกหลุมพรางของท่าน! ตาย!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ข้าไม่รู้ด้วยซ้ำว่า";
			mes "Wildcat Joe คือใคร!";
			mes "ใจเย็น ๆ ไม่จำเป็น";
			mes "ต้องมีความรุนแรง!";
			next;
			mes "[Kuuga Gai]";
			mes "อะไร...?";
			mes "ท่านหลบการโจมตี";
			mes "ทั้งหมดของข้าได้อย่างไร?";
			mes "ท่านมีความสามารถ";
			mes "ข้าจะให้ท่าน";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "ข้ามาที่นี่";
			mes "โดยหวังว่า";
			mes "จะเปลี่ยนเป็น Ninja";
			next;
			mes "[Kuuga Gai]";
			mes "...โอ้ แค่นี้เหรอ?";
			mes "อืม ท่านมีศักยภาพสูง";
			mes "แต่ตอนนี้ข้าไม่สามารถ";
			mes "ช่วยท่านได้ ข้ามีศัตรู";
			mes "มากเกินไป และข้าไม่สามารถ";
			mes "ทิ้งยามไว้ได้แม้แต่วินาทีเดียว";
			next;
			mes "[Kuuga Gai]";
			mes "Wildcat Joe นั้น";
			mes "โหดเหี้ยมอย่างสมบูรณ์...!";
			mes "เขาสามารถโจมตีได้ตลอดเวลา!";
			mes "เขาจะทำทุกอย่างเพื่อให้ได้";
			mes "ชัยชนะเหนือศัตรูของเขา!";
			next;
			mes "[Kuuga Gai]";
			mes "เดี๋ยวก่อน ข้าคิด";
			mes "อะไรบางอย่างออก";
			mes "บางที ท่านสามารถ";
			mes "ช่วยข้า และข้าจะสอน";
			mes "ทักษะสองสามอย่างให้";
			mes "ถ้าท่านอยากเป็น Ninja จริงๆ";
			next;
			if(select("แน่นอน:ไม่เป็นไร ขอบคุณ") == 2) {
				mes "[Kuuga Gai]";
				mes "หืม? ได้เลย, ถึงกระนั้น";
				mes "ข้าไม่เห็นว่า ทำไมเรา";
				mes "ไม่สามารถช่วยเหลือซึ่งกัน";
				mes "และกัน ในสถานการณ์";
				mes "เล็ก ๆ น้อย ๆ นี้...";
				close;
			}
			mes "[Kuuga Gai]";
			mes "ดี! ตอนนี้ ข้า";
			mes "อยากถาม Wildcat Joe";
			mes "ว่า เขาเห็นด้วยกับการ";
			mes "พักรบชั่วคราว ข้ารู้ว่า";
			mes "เราสองคนไม่มีอาวุธ ดังนั้น";
			mes "เราควรจะได้รับความพร้อมก่อน";
			next;
			mes "[Kuuga Gai]";
			mes "โปรดนำจดหมายนี้";
			mes "ไปส่งให้ Wildcat Joe";
			mes "ใน Einbroch ขาเป็น";
			mes "ผู้เชี่ยวชาญในการอำพราง";
			mes "ดังนั้น โปรดระวังให้ดี";
			mes "อ๊า... และมองหาเขาในที่สูง";
			next;
			mes "[Kuuga Gai]";
			mes "ใช่ Wildcat Joe";
			mes "มักจะซ่อนอยู่ในที่สูง";
			mes "อย่างไรก็ตาม หลังจากที่";
			mes "ท่านให้จดหมายแก่เขา";
			mes "โปรดกลับมา และแจ้ง";
			mes "ให้เราทราบคำตอบของเขา";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if (NINJ_Q == 1) {
			mes "[Kuuga Gai]";
			mes "แม้ว่างานนี้";
			mes "จะไม่เร่งด่วน";
			mes "โปรดรีบไปที่";
			mes "Einbroch และส่ง";
			mes "จดหมายของข้าให้ Wildcat Joe";
			close;
		}
		else if (NINJ_Q == 2) {
			mes "[Kuuga Gai]";
			mes "ท่านส่งจดหมายฉบับนั้น";
			mes "ให้กับ Wildcat Joe?";
			mes "ข้ายังต้องรู้คำตอบของเขา";
			mes "ต่อข้อเสนอของข้า สำหรับ";
			mes "การพักรบ อย่างไรก็ตาม";
			mes "ดูว่าท่านสามารถพบเขาได้ไหม";
			close;
		}
		else if (NINJ_Q == 3) {
			mes "[Kuuga Gai]";
			mes "อ่า ท่านกลับมาแล้ว";
			mes "Wildcat Joe ส่งท่าน";
			mes "กลับมาที่นี่ พร้อม";
			mes "คำตอบของเขาหรือไม่?";
			mes "เยี่ยมมาก โปรดให้ข้าอ่าน";
			next;
			mes "[Kuuga Gai]";
			mes "อะไร...?! เขาจะปฏิเสธ";
			mes "ข้อเสนอของข้า สำหรับ";
			mes "การพักรบได้อย่างไร!";
			mes "นี่แปลได้ว่า เขาสร้าง";
			mes "Kunai เท่านั้น ข้าต้อง";
			mes "ตามหาเขา หรือไม่ก็เป็นคนใจร้อน!";
			next;
			mes "[Kuuga Gai]";
			mes "ฟัง ท่านต้องช่วยข้าอีกครั้ง!";
			mes "ข้าต้องการให้ท่านรวบรวมวัสดุบางอย่าง";
			mes "เพื่อที่ข้าจะสามารถสร้าง Kunai ของตัวเอง";
			mes "เพื่อต่อสู้กับ Wildcat Joe จากนั้น";
			mes "ข้าจะไปข้างหน้า และเปลี่ยนท่านเป็น Ninja";
			next;
			mes "[Kuuga Gai]";
			mes "สิ่งที่ท่านต้องนำมา";
			mes "ให้ข้าคือ";
			mes "^3355FF5 Cyfars^000000 และ";
			mes "^3355FF1 Phracon^000000.";
			mes "โปรดหาสิ่งเหล่านี้";
			mes "ให้เร็วที่สุด!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ฮะ? ตลก!";
			mes "Wildcat Joe ขอให้";
			mes "ข้ารวบรวมวัสดุ";
			mes "เดียวกันเหล่านั้น";
			next;
			mes "[Kuuga Gai]";
			mes "คำสาป! นั่นหมายความว่า...";
			mes "ท่านช่วย Joe ในการ";
			mes "ประดิษฐ์ Kunai ของเขา! ไม่!";
			mes "ข้าควรจะคิดก่อนหน้านี้!";
			mes "มันสายเกินไปแล้ว";
			mes "รีบหน่อยสิ!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if (NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[Kuuga Gai]";
				mes "รีบนำ";
				mes "^3355FF5 Cyfars^000000 และ";
				mes "^3355FF1 Phracon^000000 มาให้ข้า";
				mes "เพื่อที่ข้าจะได้สร้าง Kunai";
				mes "เป็นของตัวเอง เพื่อใช้มัน";
				mes "กับ Wildcat Joe!";
				close;
			}
			if (SkillPoint != 0) {
				mes "[Kuuga Gai]";
				mes "โอ้ว โอ้ว...";
				mes "ท่านยังมีแต้ม";
				mes "Skill เหลืออยู่";
				mes "ท่านควรที่จะใช้ทั้งหมด";
				mes "ก่อนที่จะเปลี่ยนอาชีพ";
				mes "ใช่ไหม?";
				close;
			}
			mes "[Kuuga Gai]";
			mes "อา ท่านกลับมา";
			mes "พร้อมทุกสิ่งที่ข้าต้องการ";
			mes "ท่านมาเร็วกว่านี้";
			mes "ข้าคาดหวังใช่มั้ย?";
			mes "เยี่ยมมาก ตามที่สัญญา";
			mes "ข้าจะเปลี่ยนท่านเป็น Ninja";
			next;
			mes "[Kuuga Gai]";
			mes "ให้ข้าแนะนำตัวเอง";
			mes "อย่างเป็นทางการ";
			mes "ข้าเป็น High Ninja ชื่อ";
			mes "Kuuga Gai ใน Kouga Ninja Corps";
			mes "และข้าอยู่ในความดูแลของฝ่ายค้นหา เพื่อค้นหา Sir Kazma";
			next;
			mes "[Kuuga Gai]";
			mes "Sir Kazma เป็น";
			mes "หัวหน้าหมู่บ้านของข้า";
			mes "แต่เขาหนีไป สิ่งนี้ส่งผล";
			mes "ให้เกิดความขัดแย้ง";
			mes "ภายใน Ninja Corps";
			mes "ตอนนี้สิ่งต่าง ๆ ไม่แน่นอน...";
			next;
			mes "[Kuuga Gai]";
			mes "ตอนแรกข้าไม่ต้องการ";
			mes "ที่จะยอมรับท่านเป็น Ninja";
			mes "เพราะสถานการณ์ที่ซับซ้อนนี้";
			mes "อย่างไร ก็ตามท่านได้พิสูจน์แล้วว่า";
			mes "ท่านมีค่าในการเข้าร่วมอันดับ";
			mes "Ninja อย่างแท้จริง";
			next;
			mes "[Kuuga Gai]";
			mes "ตามจดหมายของเขา แม้แต่";
			mes "Joe คิดหนักมากในตัวท่าน";
			mes "เพียงจำไว้ว่าในฐานะ Ninja";
			mes "ภารกิจของท่านคือความสำคัญสูงสุด";
			mes "แต่อย่าให้วัตถุประสงค์ของภารกิจเกินความรู้สึกผิดของท่าน";
			next;
			mes "[Kuuga Gai]";
			mes "''ความลับเหนือสิ่งอื่นใด''";
			mes "เพื่อรักษาความลับของเรา";
			mes "ไว้ในเงามืด ท่านสามารถ";
			mes "ซื้อหรือขายอาวุธ Ninja กับ";
			mes "ตัวแทนจำหน่ายที่ได้รับอนุญาต";
			mes "เท่านั้น! โปรดจำไว้";
			next;
			mes "[Kuuga Gai]";
			mes "ณ วันนี้ ท่านเป็น";
			mes "สมาชิกที่ภาคภูมิใจของ";
			mes "Touga Ninja Corps";
			mes "จงคล่องแคล่วดุจสายลม";
			mes "และนิ่งเงียบเหมือนเงา";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			delitem 7053,5; // Cyfar
			delitem 1010,1; // Phracon
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[Kuuga Gai]";
			mes "ท่านเป็นอย่างไรบ้าง?";
			mes "ฝึกฝนอย่างหนัก: ท่านต้องการ";
			mes "ที่จะหายตัวไปอย่างไร้ร่องรอย";
			mes "หากท่านสามารถทำเช่นนั้น";
			mes "ท่านจะได้รับความเคารพจาก";
			mes "Ninja อื่น~";
			close;
		}
	} else {
		if (BaseClass == Job_Ninja) {
			mes "[Kuuga Gai]";
			mes "ท่านเป็นอย่างไรบ้าง?";
			mes "ฝึกฝนอย่างหนัก: ท่านต้องการ";
			mes "ที่จะหายตัวไปอย่างไร้ร่องรอย";
			mes "หากท่านสามารถทำเช่นนั้น";
			mes "ท่านจะได้รับความเคารพจาก";
			mes "Ninja อื่น~";
			close;
		} else {
			mes "[Kuuga Gai]";
			mes "อะไร...?";
			mes "ท่านจะพบข้า";
			mes "ที่ซ่อนตัวอยู่ในเงามืด";
			mes "ได้อย่างไร! ท่าน";
			mes "ต้องเป็นมากกว่า";
			mes "นักผจญภัยทั่วไป ใช่มั้ย?";
			close;
		}
	}
}

einbroch,184,194,3	script	Suspicious Man#nq	881,{
	if (NINJ_Q == 1) {
		mes "[Suspicious Man]";
		mes "ข้าเดินทางไป";
		mes "หลายประเทศ แต่ข้า";
		mes "ไม่เคยอยู่บนตึกสูงเท่า";
		mes "หอคอ Einbroch อาคาร";
		mes "ทั้งหมดในบ้านเกิดของข้า";
		mes "มีขนาดเล็ก เมื่อเปรียบเทียบ...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "โอ้ ท่านมาจาก";
		mes "Amatsu เหรอ? ข้า";
		mes "กำลังตามหาใครสักคน";
		mes "ที่ชื่อ Wildcat Joe จากที่นั่น";
		next;
		mes "[Suspicious Man]";
		mes "...ไม่ ไม่ ข้ามาจาก";
		mes "Izlude จริง ๆ แล้วข้า";
		mes "มาที่นี่เพื่อหาแร่บางอย่างที่";
		mes "Einbroch เท่านั้น บอกข้าหน่อยว่า";
		mes "ทำไมท่านถึงตามหา Wildcat Joe";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าต้องส่งจดหมายนี้";
		mes "ถึงเขา และรับคำตอบ";
		mes "ของเขา เพื่อที่ข้าจะ";
		mes "สามารถเป็น Ninja ได้";
		next;
		mes "[Suspicious Man]";
		mes "จริงๆ? ตอนนี้ที่ข้าคิด";
		mes "เกี่ยวกับมัน ข้าคิดว่าข้า";
		mes "ได้วิ่งเข้าไปในเขาครั้งหนึ่ง";
		mes "หรือสองครั้งในเมืองนี้ แม้ว่าเขาจะชอบ";
		mes "ที่จะถูกเรียกว่า ''Red Leopard Joe'' แทนที่จะเป็น ''Wildcat Joe''";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าต้องการช่วยท่าน";
		mes "หาเขาจริงๆ แต่ก่อนอื่น";
		mes "ข้าต้องหาแร่ธาตุ ที่ข้า";
		mes "กำลังตามหา ถ้าท่าน";
		mes "ไม่รังเกียจท่านช่วยข้าได้ไหม?";
		mes "จากนั้น ข้าสามารถช่วยท่านหา Joe";
		next;
		if(select("ไม่ต้องกังวล ข้าจะหาเขาคนเดียว:แน่นอน ข้าจะช่วยท่าน") == 1) {
			mes "[Suspicious Man]";
			mes "ท่านแน่ใจเกี่ยวกับที่...?";
			mes "Red Leopard Joe เป็นหัวหน้า";
			mes "ที่แท้จริงของการปลอมตัว";
			mes "ท่านจะต้องการความช่วยเหลือ";
			mes "ทั้งหมด ที่ท่านสามารถหาได้...";
			close;
		}
		mes "[Suspicious Man]";
		mes "เยี่ยมมาก ข้าดีใจ";
		mes "ที่ได้ยินเช่นนั้น";
		mes "โปรดช่วยข้าหา";
		mes "^3355FF5 Cyfars^000000 และ";
		mes "^3355FF1 Phracon^000000.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if (NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[Suspicious Man]";
			mes "โปรดนำ";
			mes "^3355FF5 Cyfars^000000 และ";
			mes "^3355FF1 Phracon^000000 มาหาข้า";
			mes "โดยเร็วที่สุด จากนั้น";
			mes "ข้าสามารถช่วยท่าน";
			mes "ตามหา Red Leopard Joe";
			close;
		}
		mes "[Suspicious Man]";
		mes "ดีมาก";
		mes "ท่านได้นำแร่ธาตุ...";
		mes "ตอนนี้ เป็นตาของข้า";
		mes "ที่จะช่วยท่านในขณะนี้";
		mes "ที่นี่ ให้ข้าดูจดหมายนั้น";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[Suspicious Man]";
		mes "ทำไม? ท่านไม่ได้นำจดหมายของ Kuuga Gai's มาให้ข้า?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ท่าน...";
		mes "ท่านคือ Wildcat Joe?";
		next;
		mes "[Suspicious Man]";
		mes "...ใช่ แต่ข้าชอบ";
		mes "ถูกเรียกว่า Red Leopard Joe";
		mes "Kuuga Gai ส่งท่านมาหาข้าใช่ไหม?";
		mes "เขาเป็นคนเดียวที่เรียกข้าแบบนั้น";
		mes "ท่านอยากเป็น Ninja ใช่มั้ย?";
		mes "อืม... ไม่เป็นไร";
		next;
		mes "[Red Leopard Joe]";
		mes "หากท่านต้องการเป็น Ninja";
		mes "ท่านควรระวังสิ่งที่ท่านเห็น";
		mes "และสิ่งที่ท่านไว้วางใจเสมอ อย่าลืมว่า";
		mes "ความลับของท่านถูกค้นพบ ท่านก็จะกลายเป็น Ninja";
		next;
		mes "[Red Leopard Joe]";
		mes "จำไว้ว่า ให้";
		mes "เคลื่อนที่อย่างรวดเร็ว";
		mes "และหายตัวไปอย่าง";
		mes "ไร้ร่องรอย การซ่อนตัว";
		mes "อยู่ในเงามืด เป็นพลัง";
		mes "ที่แท้จริงของเรา";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าเห็น...";
		mes "...........";
		next;
		mes "[Red Leopard Joe]";
		mes "สำหรับตอนนี้ ให้ข้า";
		mes "อ่านจดหมายนี้ มาดูกัน...";
		mes "ฮึ่ม ข้าคิดว่า Kuuga Gai";
		mes "อยากจะท้าข้าอีกครั้ง";
		mes "แต่จริง ๆ แล้วเขาต้องการ";
		mes "การพักรบชั่วคราว? ฮะ!";
		next;
		mes "[Red Leopard Joe]";
		mes "ขอบคุณสำหรับความช่วยเหลือของท่าน";
		mes "ตอนนี้ข้ามีแร่ธาตุแล้ว";
		mes "ข้าต้องสร้าง Kunai!";
		mes "ฮ่าฮ่าฮ่า! ข้าจะไม่เห็นด้วยกับการพักรบ เมื่อข้าได้เปรียบ!";
		next;
		mes "[Red Leopard Joe]";
		mes "อย่างไรก็ตาม ข้าขอเขียน";
		mes "คำตอบของข้ากับเขา";
		mes "ข้าจะให้คำแนะนำแก่ท่าน...";
		mes "ข้าคิดว่า ท่านจะเป็น";
		mes "Ninja ที่ดีมาก แม้ว่า";
		mes "ข้าจะหลอกท่านเมื่อเร็วๆนี้ เฮ้ เฮ้ เฮ้!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[Red Leopard Joe]";
		mes "ไปได้เลย";
		mes "โปรดนำจดหมายฉบับนี้";
		mes "ไปให้กับ Kuuga Gai";
		mes "ใช้เวลาสักครู่ เพื่อกลับ";
		mes "ไปที่ Amatsu ดังนั้น";
		mes "ข้าจะส่งท่านไปที่นั่นโดยตรง...";
		delitem 1010,1;	// Phracon
		delitem 7053,5;	// Cyfar
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 3) {
		mes "[Red Leopard Joe]";
		mes "ใช่มั้ย? ข้า";
		mes "ไม่แน่ใจว่าเกิดอะไรขึ้น";
		mes "แต่ดูเหมือนว่า ท่าน";
		mes "ยังไม่ได้พบกับ Kuuga Gai";
		mes "ตอนนี้ ข้าจะส่งท่าน";
		mes "ไปที่ Amatsu โดยตรง?";
		next;
		if(select("ไม่เป็นไร ขอบคุณ:ใช้ ได้โปรด") == 1) {
			mes "[Red Leopard Joe]";
			mes "เอาล่ะ ดี";
			mes "ข้าแค่พยายาม";
			mes "ช่วยท่านซักครั้ง";
			close;
		}
		mes "[Red Leopard Joe]";
		mes "เอาล่ะ";
		mes "ลาก่อน~";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 4) {
		mes "[Red Leopard Joe]";
		mes "Kuuga Gai ขอให้ท่าน";
		mes "รวบรวมวัสดุบางอย่าง";
		mes "ด้วยเหรอ? โอ้ว ดี";
		mes "ข้าคิดว่าข้าไม่สามารถ";
		mes "ตำหนิเขาได้ นอกจากนี้ข้าควร";
		mes "จะสามารถเอาชนะเขาในการต่อสู้ที่ยุติธรรม~";
		close;
	}
	else if (NINJ_Q == 5 && BaseClass == Job_Ninja) {
		mes "[Red Leopard Joe]";
		mes "โอ้ว ท่านคือ Ninja~";
		mes "ข้าหวังว่า ท่านจะฝึกฝน";
		mes "ตัวเองต่อไป และฝึกฝนทักษะ";
		mes "Ninja ทั้งหมดที่ท่าน";
		mes "สามารถทำได้ จำไว้เสมอว่า";
		mes "ให้กลมกลืนเหมือนกับเงา";
		close;
	}
	else {
		mes "[Tourist]";
		mes "ข้าเดินทางไปหลาย";
		mes "ประเทศ แต่ข้าไม่เคย";
		mes "อยู่บนตึกสูงเท่าหอคอย";
		mes "Einbroch อาคารทั้งหมด";
		mes "ในบ้านเกิดของข้า มีขนาด";
		mes "มีขนาดเล็ก เมื่อเปรียบเทียบ...";
		close;
	}
}
