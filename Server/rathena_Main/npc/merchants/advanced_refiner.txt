//===== rAthena Script =======================================
//= Advanced Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Changelog: ===========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.6 Added VIP features. [Euphy]
//= 1.7 Removed re-roll behavior. [Secret]
//============================================================

payon,157,146,6	script	Suhnbi#cash	85,{
	disable_items;
	mes "[Suhnbi]";
	mes "ข้าคือ Armsmith";
	mes "ข้าจะบอกให้รู้ ว่าข้าสามารถตีบวก,";
	mes "อาวุธ ชุดเกราะ และของสวมใส่ได้";
	mes "เจ้าต้องการตีบวกอะไร.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i])) {
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[Suhnbi]";
		mes "ข้าไม่คิดว่าข้าจะตีบวกไอเท็มที่เจ้ามีได้...";
		close;
	}
	set .@part, .@indices[ select(.@menu$) ];

	if (!getequipisequiped(.@part)) //custom check
		close;
	if (!getequipisenableref(.@part)) {
		mes "[Suhnbi]";
		mes "ไปหาช่างคนอื่น. เจ้าไม่สามารถตีบวกได้หรอก.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Suhnbi]";
		mes "หืมม... มันยอดเยี่ยมอยู่แล้ว. ข้าไม่คิดว่าข้าจะตีบวกต่อได้.";
		close;
	}

	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	.@price = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);

	// Make sure you have the necessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	callsub S_RefineValidate,getequipweaponlv(.@part),.@material,.@price,.@part,.@refineitemid,.@refinerycnt;

	mes "[Suhnbi]";
	mes "แคร้ง! แคร้ง! แคร้ง!";
	if (getequippercentrefinery(.@part, REFINE_COST_ENRICHED) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[Suhnbi]";
		mes "เอาไป! มันเสร็จแล้ว.";
		mes "เป็นเวลานานแล้วที่ข้าไม่ได้ตีบวก "+((getequipweaponlv(.@part))?"weapon":"armor")+". เจ้าต้องมีความสุขแน่ๆเพราะว่ามันแข็งแกร่งขึ้น!";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?ET_MONEY:ET_HUK;
	mes "[Suhnbi]";
	mes "อุปสสสสสสสสสส์!!!";
	next;
	mes "[Suhnbi]";
	mes "...";
	mes ".....";
	mes ".......หุหุหุหุหุ~";
	mes "........เจ้าเลือกที่จะทำเองน่ะ, อย่าเสียใจเลย.";
	close;

S_RefineValidate:
	.@weapon_lvl = getarg(0);
	.@item_req = getarg(1);
	.@price = getarg(2);
	.@part = getarg(3);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(.@weapon_lvl){
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
		}
	}

	mes "[Suhnbi]";
	if (.@weapon_lvl)
		mes "เจ้าต้องการตีบวกอาวุธเลเวล "+ .@weapon_lvl +" ใช่ไหม ?";
	mes "ในการตีบวก, เจ้าต้องมี ^ff9999"+ getitemname(.@item_req) +"^000000 และ "+ .@price +" zeny.";
	mes "เจ้าอยากจะทำต่อไหม ?";
	next;
	if(select("ใช่:ไม่") == 1) {
		if (getequippercentrefinery(.@part,REFINE_COST_ENRICHED) < 100) {
			if (.@weapon_lvl) {
				mes "[Suhnbi]";
				mes "ว้าว!!";
				mes "อาวุธนี้";
				mes "ดูเหมือนจะตีบวก...";
				mes "มาหลายครั้ง...";
				mes "มันจะแตกได้ถ้า";
				mes "เจ้าตีบวกมันอีก.";
				next;
				mes "และถ้ามันแตก,";
				mes "เจ้าจะไม่ได้ใช้มันอีกเลย!";
				mes "การ์ดทั้งหมดและออฟ ^ff0000จะหายไป^000000!";
				mes "^ff0000พร้อมกับ, ของสวมใส่ที่แตกไป!^000000";
				mes "เจ้าแน่ใจที่จะทำต่อหรือไม่ ?";
				next;
				if(select("ใช่:ไม่") == 2) {
					mes "[Suhnbi]";
					mes "เยี่ยม.";
					mes "เพราะถ้าอาวุธแตกจากการตีบวก, ข้าคงรู้สึกไม่ดี, แน่ๆ.";
					close;
				}
			} else {
				mes "[Suhnbi]";
				mes "หึ. หึ. โอ้, เจ้ามีความกล้า, ที่จะเสี่ยงตีบวก.";
				mes "ข้ารู้ว่ามันเสี่ยงมาก, หรือว่าเจ้าไม่ใช่ ?";
				next;
				mes "ถ้าเครื่องป้องกันเจ้าแตก, เจ้าจะไม่ได้ใช้มันอีกเลย.";
				mes "ไม่ว่าจะการ์ดและออฟ ^ff0000มันจะหายไป^000000.";
				//mes "ทุกอย่างจะหายไป. หาย... ไป!";
				mes "เจ้ายังอยากทำต่อไหม ?";
				next;
				if(select("ใช่:ไม่") == 2) {
					mes "[Suhnbi]";
					mes "ไร้สาระ. เจ้าทำให้ข้าเสียเวลา.";
					mes "ไปตายซะ.";
					close;
				}
			}
		}
		if (countitem(.@item_req) > 0 && Zeny > .@price) {
			delitem .@item_req,1;
			set Zeny, Zeny - .@price;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, getarg(4)) ||
				callfunc("F_IsEquipRefineHack", .@part, getarg(5)) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
				mes "[Holink]";
				emotion ET_FRET;
				mes "รอสักครู่...";
				mes "เจ้าคิดว่าข้าโง่หรอ?!";
				mes "เจ้าแอบสลับของตอนที่ข้าไม่เห็น! ไปให้พ้นซะ!";
				close;
			}

			return;
		}
		mes "[Suhnbi]";
		mes "นี้คือทั้งหมดที่เจ้ามี ?";
		mes "มันน่าเศร้านัก, แต่ข้าต้องบอกเจ้าว่าเจ้ามีแร่หรือเงินไม่พอสำหรับค่าใช้จ่าย เข้าใจ ?";
		close;
	}
	mes "[Suhnbi]";
	mes "ข้าช่วยเจ้าไม่ได้แม้ว่าเจ้าจะไม่พอใจก็ตาม...";
	close;
}
