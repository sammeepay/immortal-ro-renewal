//===== rAthena Script =======================================
//= Inn Npcs
//===== By: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: =====================================
//= 2.9a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morocc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morocc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//============================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Inn Employee#Sammy	53,{

	callfunc "F_InnMaid","[Employee Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Inn Employee#Ahlma	53,{

	callfunc "F_InnMaid","[Employee Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	Inn Employee#Jennie	53,{

	callfunc "F_InnMaid","[Employee Jennie]","'โรงแรม Fisherman'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Inn Employee#Cena	53,{

	callfunc "F_InnMaid","[Employee Cena]","'Ifrit' เป็นโรงแรมแห่งเดียวในเมือง Geffen","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	Inn Employee#Ahee	53,{

	callfunc "F_InnMaid","[Employee Ahee]","โรงแรม Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	Inn Employee#Hasna	53,{

	callfunc "F_InnMaid","[Employee Hasna]","โรงแรม Morocc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	Inn Employee#Manar	53,{

	callfunc "F_InnMaid","[Employee Manar]","โรงแรม Morocc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Inn Maid#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","โรงแรม Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Inn Keeper#Annie	931,{

	callfunc "F_InnMaid","[Annie]","โรงแรม Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Hotel Employee#01	86,{
	mes "[Hotel Employee]";
	mes "ยินดีต้อนรับสู่";
	mes "มังกรหลวง";
	mes "ที่คุณสามารถหา";
	mes "ที่พักที่ดีที่สุด";
	mes "และบริการที่ดีที่สุด";
	next;
	switch(select("บันทึกจุดเซฟ:Rest - 5,000 zeny:ยกเลิก")) {
	case 1:
		savepoint "lhz_in02",209,275,1,1;
		mes "[Hotel Employee]";
		mes "ขอบคุณคุณ";
		mes "จุดเกิดใหม่ได้รับการบันทึกที่นี่ในมังกรหลวง";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[Hotel Employee]";
			mes "ฉันขอโทษ แต่";
			mes "คุณต้องการ 5,000 zeny";
			mes "เพื่อเช็คอิน";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[Hotel Employee]";
		mes "ขอบคุณ ~";
		mes "ฉันหวังว่าคุณจะสนุก";
		mes "เจ้าอยู่ใน";
		mes "Royal Dragon.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[Hotel Employee]";
		mes "ขอบคุณและ";
		mes "ขอให้เป็นวันที่ดีนะ";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Inn Maid#Receptionist	53,{

	callfunc "F_InnMaid","[Receptionist]","โรงแรม Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Inn Master#Receptionist	709,{
	mes "[Inn Master]";
	mes "วันดี ~";
	mes "ยินดีต้อนรับสู่";
	mes "สบายที่สุด";
	mes "อินน์ที่นี่ใน Veins ~";
	next;
	switch(select("เซฟ:พักเอาแรง -> 5000 zeny:ออก")) {
	case 1:
		mes "[Inn Master]";
		mes "จุดเกิดใหม่ของคุณ";
		mes "ได้รับการช่วยชีวิตในหลอดเลือดดำ";
		mes "เพลิดเพลินกับการพักผ่อนในเมือง ~";
		savepoint "ve_in",157,209,1,1;
		close;
	case 2:
		mes "[Inn Master]";
		if(Zeny < 5000){
			mes "ฉันขอโทษ แต่ฉันไม่ได้";
			mes "คิดว่าเจ้ามีเพียงพอ";
			mes "เงินที่ใช้เช็คอิน";
			mes "ค่าบริการคือ 5,000 zeny";
			close;
		}
		mes "เพลิดเพลินกับการพักผ่อนของคุณ ~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[Inn Master]";
		mes "ได้โปรดกลับมาอีกครั้ง";
		close;
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	.@npc_name$ = getarg(0);

	mes .@npc_name$;
	mes "ยินดีต้อนรับสู่";
	mes getarg(1) + ".";
	mes "ฉันจะช่วยคุณได้อย่างไร";
	next;
	switch(select("เซฟ:พักผ่อน -> 5000 zeny:ยกเลิก")) {
	case 1:
		mes .@npc_name$;
		mes "จุดเกิดใหม่ของคุณ";
		mes "ได้รับการบันทึกแล้ว";
		mes "ขอบคุณ";
		mes "ได้โปรดกลับมาอีกครั้ง";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close;
	case 2:
		mes .@npc_name$;
		if(Zeny < 5000){
			mes "ฉันขอโทษ แต่ค่าบริการอยู่ที่ 5,000 zeny โปรดตรวจสอบให้แน่ใจว่าคุณมีเงินมากพอที่จะเช็คอินในครั้งต่อไปได้ไหม";
			close;
		}
		mes "ขอบคุณ";
		mes "ฉันหวังว่าคุณ";
		mes "เพลิดเพลินไปกับการพักผ่อนของคุณ ~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
