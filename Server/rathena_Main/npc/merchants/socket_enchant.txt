//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอก่อน !! -";
		mes "- กระเป๋าของเจ้า -";
		mes "- มีของมากเกินไป. -";
		mes "- ได้โปรดลอง -";
		mes "- ลดจำนวนของลงก่อน. -";
		close;
	}
	mes "[Seiyablem]";
	mes "ข้าคือช่างอัจฉริยะที่สามารถเจาะรูบนอาวุธและเกราะได้.";
	mes "ค่าใช้จ่ายในการทำงานของข้า, ขอเพียงแค่แร่และโอกาสสำเร็จของไอเท็มเท่านั้นพอ.";
	next;
	mes "[Seiyablem]";
	mes "การเจาะรูอาจดูเหมือนง่าย, แต่มันก็ซับซ้อนกว่าที่คิดน่ะ.";
	mes "ถ้าเจ้าสนใจบริการของข้า, ให้บอกข้า.";
	next;
	switch(select("เจาะรูอาวุธ:เจาะรูเกราะ:รายละเอียดการเจาะรู:ยกเลิก"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "เจ้าอยากเจาะรูอาวุธหรอ?";
			mes "ข้าได้แบ่งระดับความยากของการเจาะรูเอาไว้, C Class ง่ายสุดและ S Class โครตยากเลย.";
			mes "เจ้าอยากจะทำในคลาสไหนล่ะ ?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "C Class, เอ้อ ?";
					mes "ตอนนี้, ข้าสามารถอัพเกรดอุปกรณ์บางอย่างในคลาสนี้ได้, เพราะงั้นเลือกของมาหนึ่งชิ้น, ที่เจ้าต้องการจะเจาะรู.";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "B Class?";
					mes "ได้เลย, มันเป็นของสวมใส่ที่ค่อนข้างโอเคน่ะ.";
					mes "เลือกอาวุธที่เจ้าต้องการจะเจาะรูได้เลย.";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "อูวว, A Class.";
					mes "นี่ค่อนข้างแพงเอาการเลย!";
					mes "ได้เลย, เลือกอาวุธที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Hunter Bow:Survivor's Rod(INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "ว้าว, ว้าว, ว้าว...";
					mes "S class? สุดยอด...";
					mes "มันจะโหดมากถ้าข้าดึงความสามารถมันออกมาได้.";
					mes "Okay... เลือกอาวุธที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Gungnir:Poison Knife:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "Armor, จริงดิ ?";
			mes "ได้เลย, ไม่มีปัญหา.";
			mes "ข้าได้แบ่งเกราะออกเป็น 4 คลาส, ตามระดับความยากง่าย.";
			mes "C Class ทำง่ายสุด, และ S Class ทำยากสุด.";
			mes "เลือกคลาสที่เจ้าต้องการทำสิ ?";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "C Class Armor, เอ้อ ?";
					mes "ก็โอน่ะ.";
					mes "เลือกเกราะที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "เจ้าเลือก B Class.";
					mes "งั้น, เกราะไหนล่ะที่เจ้ามี ?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "อูวว, A Class.";
					mes "ค่อนข้างแพงเอาเรื่อง!";
					mes "ได้เลย, เลือกเกราะที่เจ้าต้องการจะเจาะรูสิ ?";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "อืม... S Class?";
					mes "เจ้าแน่ใจน่ะ ?";
					mes "ได้เลย... บอกข้ามาเลยว่าเจ้าจะเจาะรูเกราะชิ้นไหน, และข้าจะทำตามความคาดหวังของเจ้าเอง.";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "ดีเลย, ข้าจะบอกว่าข้ายังไม่ชำนาญในการเจาะรูสักเท่าไหร่.";
			mes "ถ้าข้าบอกเจ้าว่ามันมีหลายขั้นตอนในการทำ มันจะดูเหมือนข้าโกหกเจ้าหรือเปล่า.";
			mes "แต่ตอนนี้, ข้าได้สังเกตเห็นบางอย่าง...";
			next;
			mes "[Seiyablem]";
			mes "ในบางเมือง, การเจาะรูจะสำเร็จมากยิ่งขึ้นสำหรับอุปกรณ์ที่มีการอัพเกรดน้อย.";
			mes "ส่วนเมืองอื่นๆ, มักจะตรงกันข้ามกัน.";
			mes "มันดูแปลกๆนะว่าไหม ?";
			next;
			mes "[Seiyablem]";
			mes "โอ้, มีเรื่องสำคัญที่เจ้าควรจะรู้ไว้.";
			mes "ถ้าเจ้าจะเจาะรูของบางชิ้น, ดูให้แน่ใจก่อนว่าไม่มีของเหมือนกันในกระเป๋าของเจ้า.";
			next;
			mes "[Seiyablem]";
			mes "ยกตัวอย่างเช่น: ถ้าเจ้ามี +7 Manteau และ Manteau แบบไม่บวกในกระเป๋าของเจ้า, ดังนั้นข้าจะสุ่มเจาะรูของชิ้นไหนก็ได้ในกระเป๋าของเจ้า.";
			mes "ได้โปรดจำเอาไว้ด้วย.";
			next;
			mes "[Seiyablem]";
			mes "อีกครั้ง, ^FF0000ให้เจ้าเอาของที่ต้องการจะเจาะรูมาแค่ชิ้นเดียวเท่านั้น^000000.";
			mes "ส่วนที่เหลือที่มีชื่อเหมือนกันให้เก็บไว้ในคลังของเจ้า, เข้าใจ๊ ?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "ทำให้มันง่าย, นักผจญภัย.";
			mes "ถ้าเจ้าสนใจที่จะเจาะรู, ให้กลับมาหาข้า.";
			mes "แล้วเจอกันใหม่~";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Seiyablem]";
	mes "เจ้าต้องการที่จะเจาะ "+ getitemname(.@item_id) +"?";
	mes "ได้เลย, โปรดนำ ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "และ ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 เป็นค่าบริการ.";
	mes "อาห์, และอย่าลืม "+ getitemname(.@item_id) +"!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(.@item_id,5) & EQP_HAND_R) // weapon
	{
		mes "ข้าสามารถเจาะรูตอนนี้ได้เลยถ้าเจ้ามีของและเงิน.";
		mes "อย่างไรก็ตาม, เจ้าต้องรู้ไว้ว่าข้ามีโอกาสทำของแตกได้.";
		mes "ข้าคิดว่ามันแฟร์ดีที่จะเตือนเจ้าไว้...";
		next;
		mes "[Seiyablem]";
		mes "ถ้าการเจาะรูล้มเหลว, มันจะทำให้ ^FF0000อาวุธ^000000, ^FF0000และการ์ดที่ใส่โดนทำลายไปด้วย^000000.";
	}
	else // armor
	{
		mes "ถ้าเจ้ามีแร่, เงินและเกราะพร้อมแล้ว, งั้นพวกเรามาเริ่มเจาะรูกัน.";
		mes "แต่ก่อนหน้านั้น, ข้าต้องเตือนเจ้าก่อน.";
		next;
		mes "[Seiyablem]";
		mes "ถ้าการเจาะรูล้มเหลว, มันจะทำให้ ^FF0000เกราะ^000000, ^FF0000ค่าบวก^000000 ^FF0000และการ์ดที่ใส่โดนทำลายไปด้วย^000000.";
	}
	mes "เจ้ายังอยากเจาะรูอยู่ไหม ?";
	next;
	switch(select("เจาะรูไอเท็ม:ยกเลิก"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "ได้เลย, มาเริ่มงานกัน!";
				mes "เจ้าจะภาวนาให้สำเร็จก็ได้น่ะ.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "เยี่ยม, มันสำเร็จ.";
					mes "มันดูสุดยอดมากๆ!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "แล้วพบกันใหม่, เจ้าเกลอ!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "หว่า! ...ข้าขอโทษ, มันล้มเหลว.";
					mes "อย่างไรก็ตาม, ข้าว่ามันปกติแล้ว.";
					mes "เพราะเจ้ามันไม่มีโชคเอง, หรือว่าไม่ใช่ ?";
					mes "อย่าเศร้าไปเลย, ลองใหม่ครั้งหน้า.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Seiyablem]";
					mes "ข้าจะอวยพรให้ในครั้งหน้า!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "ข้าอยากจะเจาะรูน่ะ, แต่เจ้ามีของไม่ครบ.";
				mes "เจ้าอาจจะขาดของสวมใส่ แร่ หรือเงิน ?";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "ต้องการเวลาคิดสักหน่อย, หรอ ?";
			mes "ได้เลย, ข้าเข้าใจดี.";
			mes "แต่ชีวิตมันจะไม่สนุกถ้าเจ้าเอาแต่เล่นแบบปลอดภัย~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "ฉันกำลังพิจารณาที่จะเป็นวิศวกร Slotting";
	mes "มันเป็นช่องใหม่ที่คุณสามารถเพิ่มสล็อตลงในอุปกรณ์ได้และมันจะเจ๋งมากถ้าฉันสามารถทำงานในอาชีพที่ร่ำรวยได้";
	next;
	mes "[ชายหนุ่ม]";
	mes "มีโชคมากมายที่เกี่ยวข้องกับการเพิ่มช่องจากสิ่งที่ฉันเข้าใจได้สำเร็จ";
	mes "คุณล้มเหลวในบางครั้ง แต่ถ้าคุณโชคดีมากคุณสามารถเพิ่มสองช่องได้";
	mes "Crazy. Huh?";
	next;
	mes "[ชายหนุ่ม]";
	mes "แต่อย่าตื่นเต้นมาก";
	mes "มีเพียงอุปกรณ์บางอย่างเท่านั้นที่สามารถรองรับการเพิ่มช่องพิเศษสองช่องได้";
	mes "มีอะไรอีกมากมายที่ฉันต้องเรียนรู้ก่อนที่ฉันจะได้เป็นเด็กฝึกงาน ... ";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	97
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
