//===== rAthena Script ======================================= 
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอก่อน !! -";
		mes "- กระเป๋าของเจ้า -";
		mes "- มีของมากเกินไป. -";
		mes "- ได้โปรดลอง -";
		mes "- ลดจำนวนของลงก่อน. -";
		close;
	}
	mes "[Leablem]";
	mes "หวัดดี. ข้าชื่อว่า Leablem,";
	mes "และข้าเป็นช่างเทคนิคในการ";
	mes "เจาะรูอาวุธและเกราะ.";
	mes "มันอาจจะดูยาก, แต่ข้าชอบทำมันน่ะ";
	mes "ข้าค่อนข้างเก่งในเรื่องนี้ทีเดียว.";
	next;
	mes "[Leablem]";
	mes "ข้าได้เรียนรู้วิธีทำมาจากพี่ชายข้า, Seyablem,";
	mes "แต่ข้าคิดว่าข้าต้องศึกษาเพิ่มเติม.";
	mes "เพราะความรู้ของข้าถึงทางตัน,";
	mes "เป็นเหตุให้ข้าไม่ชอบพี่ชายข้า Seyablem.";
	next;
	mes "[Leablem]";
	mes "พี่ชายข้าไม่ชอบเวลาที่สอนข้า";
	mes "เพราะเค้ากังวลเกี่ยวกับทักษะของเค้า";
	mes "มันไม่ดีพอ. เค้าคิดแบบนั้น, อย่างไรก็ตาม, เค้าเป็นคนที่ยอดเยี่ยม";
	mes "ที่สอนข้ามากพอดู.";
	next;
	mes "[Leablem]";
	mes "ขอบคุณเค้าจริงๆ, ที่ทำให้ข้าได้เรียนรู้.";
	mes "ข้าไม่ค่อยมั่นใจนัก";
	mes "ที่จะทำสิ่งที่แตกต่าง,";
	mes "แต่หลังจากที่ข้าได้ลองทำดู";
	mes "ตอนนี้มันทำให้ข้าสามารถทำมันได้.";
	next;
	mes "[Leablem]";
	mes "ที่จริงแล้ว";
	mes "พี่ชายข้าไม่อยากให้ข้าทำ.";
	mes "ข้าไม่อยากให้เค้าเสียใจ, ดังนั้นแล้ว...ข้าจึงข้ามอะไรบางอย่างไป.";
	mes "ซึ่งข้าบอกไม่ได้!";
	next;
	mes "[Leablem]";
	mes "ถ้าเจ้ามีของสวมใส่";
	mes "และอยากจะลองที่จะเจาะรูดู ?";
	mes "ค่าบริการของข้า, ขอแค่แร่,";
	mes "และโอกาสทำสำเร็จ";
	mes "กับไอเท็มเท่านั้นพอ.";
	next;
	switch(select("อาวุธ:เกราะ:เกี่ยวกับสิ่งที่ข้ามไป:รายละเอียดเพิ่มเติม:ออก"))
	{
		case 1:
			mes "[Leablem]";
			mes "อาวุธ, แน่ใจ ? ได้, ไม่มีปัญหา.";
			mes "อาวุธแบ่งออกเป็น 4 คลาส";
			mes "ตามความยากง่าย.";
			mes "C class ง่ายสุด, และ S class ยากสุด.";
			mes "คลาสไหนที่เจ้าต้องการทำ ?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes "C class, อืมมมม.";
					mes "อาวุธอะไรที่เจ้ามีละ ?";
					next;
					switch(select("Book of Mother Earth:Book of Billows:Book of Gust of Wind:Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าเลือก B class.";
					mes "งั้น, อาวุธอะไรที่เจ้ามีละ ?";
					next;
					switch(select("Orcish Axe:Scimiter:Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "A class? เจ้าแน่ใจน่ะ. ได้เลย, ไม่มีปัญหา.";
					mes "งั้น, อาวุธอะไรที่เจ้ามีละ ?";
					next;
					switch(select("Dragon Killer:Katar of Quaking:Katar of Raging Blaze:Katar of Frozen Icicle:Katar of Piercing Wind:Golden Mace:Oriental Lute:Queen's Whip:Spectral Spear:Gae Bolg:Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "หูวว, S Class?! เจ้าเอาจริงดิ ?*ตึกตัก*";
					mes "งั้น...อาวุธ S class ชิ้นไหนที่เจ้าต้องการจะเจาะรู ?!";
					next;
					switch(select("Zephyrus:Mailbreaker:Dragon Slayer:Swordbreaker:Assasin Dagger:Grand Cross:Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "เกราะ, แน่ใจ ? ได้, ไม่มีปัญหา.";
			mes "เกราะแบ่งออกเป็น 4 คลาส";
			mes "ตามความยากง่าย.";
			mes "C class ง่ายสุด, และ S class ยากสุด.";
			mes "คลาสไหนที่เจ้าต้องการทำ ?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes "C class, อืมมมม.";
					mes "เกราะอะไรที่เจ้ามีละ ?";
					next;
					switch(select("Sunflower:Ph.D Hat:Big Ribbon:Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าเลือก B class.";
					mes "งั้น, เกราะอะไรที่เจ้ามีละ ?";
					next;
					switch(select("Skull Ring:High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "A class? เจ้าแน่ใจน่ะ. ได้เลย, ไม่มีปัญหา.";
					mes "งั้น, เกราะอะไรที่เจ้ามีละ ?";
					next;
					switch(select("Pirate Bandana:Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[Leablem]";
					mes "หูวว, S Class?! เจ้าเอาจริงดิ ?*ตึกตัก*";
					mes "งั้น...เกราะ S class ชิ้นไหนที่เจ้าต้องการจะเจาะรู ?!";
					next;
					switch(select("Mage Coat:Holy Robe:Sacred Mission:Undershirt:Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "โอ้, มันไม่พิเศษอะไรหรอก, แต่เค้าคิดว่า";
			mes "มันอาจทำให้ผู้คนแตกตื่นได้. ";
			mes "ข้าไม่อยากให้พี่ชายข้าเสียใจ,";
			mes "ดังนั้นแล้วลืมมันไปเถอะ.";
			next;
			mes "[Leablem]";
			mes "...มันมีของอยู่สิ่งหนึ่งที่มีโอกาสทำสำเร็จสูงมากๆ,";
			mes "แต่ก็แลกมากับแร่และค่าใช้จ่ายที่สูงพอๆกัน. ";
			mes "ข้าสงสัยว่าจะมีใครใช้บริการนี้กัน";
			mes "ถ้าข้าเปิดให้ทำ.";
			next;
			mes "[Leablem]";
			mes "...อืมมมม...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "เจ้าดูเหมือนจะสนใจน่ะ.";
			mes "ถ้าเจ้าอยาก, ข้าอาจบอกเจ้าก็ได้. ";
			mes "แต่เจ้าต้องสัญญาก่อนว่าจะเก็บเป็นความลับ.";
			mes "ถ้าพี่ชายข้ารู้ว่าข้าบอกเจ้า,";
			mes "เค้าต้องฆ่าข้าแน่ๆ!";
			next;
			mes "[Leablem]";
			mes "นี้เป็นบริการพิเศษที่โอกาสสำเร็จสูงถึง 90%,";
			mes "แต่ต้องการ 2 Gold และ 2 ร้อยล้าน zeny:";
			mes "ข้าจะเจาะรู Hat of the Sun God ให้.";
			next;
			mes "[Leablem]";
			mes "พี่ชายข้าบอกว่า, ในสมัยก่อน, ห้ามไม่ให้ดัดแปลงหมวกใบนั้น. ";
			mes "แต่ข้าเชื่อว่ามนุษย์นั้นอยากจะรู้";
			mes "ทดลอง, โดยเฉพาะอย่างยิ่งเมื่อมันเป็นสิ่งต้องห้าม.";
			mes "ข้าไม่อยากให้พี่ชายข้ารู้. สิ่งที่ข้ากำลังทำ.";
			next;
			switch(select("ใช้บริการ","ออก"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...อะ... อะไรนะ ? ม่าย!";
					mes "ให้ข้าเป็นคนทำ";
					mes "มันเป็นไปไม่ได้!";
					mes "ม่าย, ข้าบอกว่าไม่! ไม่ เข้าใจไหม! ม่าย!";
					next;
					mes "[Leablem]";
					mes "ไม่, ข้าไม่อาจ! ไม่!";
					next;
					mes "[Leablem]";
					mes "ข้าทำไม่ได้...";
					next;
					mes "[Leablem]";
					mes "ข้าทำไม่... ได้...";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "อืมมม...";
					next;
					mes "[Leablem]";
					mes "เจ้าดูมั่นใจ. Okay, ข้าจะทำตามความคาดหวังของเจ้า.";
					mes "อย่าไปบอกใคร,";
					mes "โดยเฉพาะพี่ชายข้า. สัญญาสิ ?";
					next;
					mes "[Leablem]";
					mes "Okay งั้น, เจ้าต้องมี";
					mes "^FF00002 Gold, 2 ร้อยล้าน zeny -- เป็นค่าบริการ --,";
					mes "^FF0000และหมวก Hat of the Sun God.";
					mes "^FF0000เจ้ารู้ไหมว่าโอกาสสำเร็จอยู่ที่ 90%, ข้าบอกไปยัง ?^000000";
					mes "พวกเราคงไม่อยากเสียเวลาไปมากกว่านี้แล้ว,";
					mes "งั้นมาเริ่มธุรกิจกันเลย.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "เอาละนะ ?";
						mes "ขอให้พระเจ้าอยู่ข้างเจ้า.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "ยอดเยี่ยม! ว้าว, ข้าเดาว่าโอกาสสำเร็จ 90% เป็นสิ่งที่ถูกต้อง!";
							mes "ดูสิ, เป็นการเจาะรูที่เพอร์เฟกต์จริงๆ.";
							mes "สำเร็จอย่างงดงาม.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "นี่จะเป็นครั้งแรกและครั้งสุดท้ายที่ข้าจะทำให้เจ้า.";
							mes "แล้วอย่าไปบอกใครล่ะ";
							mes "กับสิ่งที่เกิดขึ้น. Okay?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "จ้ากก! พระเจ้าช่วยกล้วยทอด, มันล้มเหลว!";
							mes "ข้าคิดว่าเพราะดวงซวยมากกว่าดวงดี...";
							mes "เราควรทำอย่างไรดี? ...ในเมื่อ";
							mes "ข้าไม่อยากจะทำมันเลย!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "ม่าย, อย่ามองข้าด้วยสายตาแบบนั้น!";
							mes "บร้า~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "อืมมม...";
						next;
						mes "[Leablem]";
						mes "เจ้ามีของไม่ครบ.";
						mes "แบบนี้ก็แย่สิ ?";
						mes "เจ้าไปหาของก่อนแล้วค่อยกลับมาใหม่,";
						mes "เมื่อเจ้ามีของครบ. เฮ้อ~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "อย่าไปบอกใคร";
					mes "กับเรื่องที่ข้าบอกกับเจ้า. Okay?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "เจ้าอยากทราบรายละเอียดเพิ่มเติม ?";
			mes "เยี่ยม, ข้าไม่คิดว่า";
			mes "มันจะมีอะไรเป็นพิเศษหรอก.";
			mes "แต่ข้าจะบอกเจ้าเอง.";
			next;
			mes "[Leablem]";
			mes "ช่างเจาะรูในบางเมือง";
			mes "อาจจะเจาะรูสำเร็จมากยิ่งขึ้น";
			mes "ถ้าของสวมใส่มีการอัพเกรด.";
			mes "ข้าไม่รู้ว่ามันเป็นเรื่องจริงหรือเปล่า,";
			mes "แต่ข้าก็พอจะเห็นอยู่บ้าง.";
			next;
			mes "[Leablem]";
			mes "อย่างเช่น, ช่างเจาะรูเมืองหนึ่ง";
			mes "มีโอกาสเจาะรูสำเร็จ";
			mes "ถ้าของสวมใส่มีการอัพเกรดที่สูง,";
			mes "ส่วนช่างเจาะรูที่อื่น";
			mes "โอกาสเจาะรูสำเร็จอาจขึ้นอยู่กับของอัพเกรดที่ต่ำ.";
			next;
			mes "[Leablem]";
			mes "เมื่อมันล้มเหลว, ของสวมใส่นั้น";
			mes "จะแตกและหายไป.";
			mes "ข้าจะพยายามเต็มที่เพื่อไม่ให้มันแตก, แต่";
			mes "การเจาะรูเป็นงานที่ละเอียดอ่อน.";
			next;
			mes "[Leablem]";
			mes "โอ้, มีสิ่งสำคัญที่ต้องบอกไว้.";
			mes "^FF0000อย่านำของที่เหมือนกันมาในกระเป๋าของเจ้า";
			mes "^FF0000เพราะเมื่อเจ้าเจาะรู.";
			mes "^FF0000Oมันจะสุ่มเจาะรูของในกระเป๋าของเจ้า,";
			mes "^FF0000เพื่อไม่ให้มันผิดพลาด, เจ้าควรนำของมาแค่ชิ้นเดียว.^000000";
			next;
			mes "[Leablem]";
			mes "ข้าหวังว่าเจ้าจะได้รับประโยชน์จากสิ่งที่ข้าบอกน่ะ.";
			mes "บาย.";
			close;
		case 5:
			mes "[Leablem]";
			mes "อืม.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Leablem]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"... Okay, เจ้าต้องนำ";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny เป็นค่าบริการ,";
		mes "^FF0000"+ .@req_amount[0] +"ea "+ getitemname(.@item_req[0]) +" and "+ .@req_amount[1] +"ea "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000เจ้าต้องมี "+ getitemname(.@item_id) +".^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"? Okay, เจ้าต้องนำ";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny เป็นค่าบริการและ "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +" ด้วย.";
		mes "^FF0000เจ้าต้องไม่ลืม "+ getitemname(.@item_id) +".";
		mes "^FF0000เจ้าต้องมีของให้ครบน่ะ.^000000";
	}
	next;
	mes "[Leablem]";
	mes "เจ้าพร้อมแล้วรึยัง ?";
	mes "ในรายละเอียด, ถ้าเจาะรูล้มเหลว,";
	mes "เจ้าจะสูญเสียของไป "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "weapon" : "armor") +".";
	mes "จำเอาไว้, ถ้า "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "weapon" : "armor") +" มีการอัพเกรด, และได้ใส่การ์ดไปแล้ว,";
	mes "มันจะหายไปทั้งหมดถ้าเจาะรูสำเร็จ.";
	next;
	switch(select("เจาะรูเลย.:ไว้โอกาสหน้า."))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no .@req_amount[0] and .@req_amount[1] provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes "ได้เลย, มาเริ่มงานกัน!";
				mes "เจ้าจะภาวนาให้สำเร็จก็ได้น่ะ.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "เยี่ยม, มันสำเร็จ.";
					mes "มันดูสุดยอดมากๆ!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "แล้วเจอกันใหม่, เจ้าเกลอ!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "หว่า! ...ข้าขอโทษ, มันล้มเหลว.";
					mes "อย่างไรก็ตาม, ข้าว่ามันปกติแล้ว.";
					mes "เพราะเจ้ามันไม่มีโชคเอง, หรือว่าไม่ใช่ ?";
					mes "อย่าเศร้าไปเลย.";
					mes "ลองใหม่ครั้งหน้า.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "แล้วเจอกันใหม่, เจ้าเกลอ!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "เจ้ามีของไม่ครบ!";
				mes "ไปเอามาให้ครบ!";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "แล้วพบกันใหม่.";
			close;
	}
}
