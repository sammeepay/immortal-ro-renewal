//===== rAthena Script ======================================= 
//= Renewal Mage Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change to Mage Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//============================================================ 
geffen_in,164,124,4	script	Mage Guildsman#mage	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Mage Guildsman]";
				mes "โอ้โหไม่เห็นแล้วนะ แต่เจ้าไม่ควรตายเหรอ?";
				next;
				mes "[Mage Guildsman]";
				mes "อาเจ้าจะต้องเกิดใหม่ดีฉันดีใจที่ได้เจ้ากลับมา";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Mage Guildsman]";
					mes "ฉันขอโทษ แต่ฉันยังไม่คิดว่าคุณพร้อมที่จะเรียนรู้เวทย์มนตร์ทำไมคุณไม่ลองเรียนรู้ทักษะพื้นฐานเสียก่อน?";
					next;
					mes "[Mage Guildsman]";
					mes "ใช้เวลาของคุณยิ่งเรียนรู้มากเท่าไหร่คุณก็ยิ่งพร้อมที่จะเรียนรู้เวทมนตร์อีกครั้ง";
					close;
				}
				mes "[Mage Guildsman]";
				mes "เพราะเจ้าผ่านการทดสอบผู้วิเศษครั้งหนึ่งข้าจะไม่ถามคุณสมบัติของเจ้าเลยเจ้าต้องการที่จะได้รับทักษะเวทย์มนตร์ของเจ้ากลับมาทันทีใช่ไหม?";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Mage_High;
				skill "MG_ENERGYCOAT",1,SKILL_PERM;
				mes "[Mage Guildsman]";
				mes "ว้าว, ด้วยเหตุผลบางอย่าง, เจ้าดูดีกว่าที่เจ้าเคยทำมาก่อน, อย่างไรก็ตามข้าเชื่อว่าเจ้าจะทำงานได้ดีขึ้นในฐานะ Mage เช่นกัน";
				close;
			}else{
				mes "[Mage Guildsman]";
				mes "มีอะไรอีกที่ฉันสามารถช่วยคุณได้บ้างถ้าไม่ใช่ทำไมคุณไม่ลองทดสอบทักษะของคุณล่ะโลกกำลังรอคุณอยู่ ~!";
				close;
			}
		}else{
			mes "[Mage Guildsman]";
			mes "ท่านสนใจในสมาคม Mage หรือไม่? ข้าไม่ต้องการที่จะบอกคุณ แต่คุณไม่ได้อยู่ที่นี่";
			next;
			mes "[Mage Guildsman]";
			mes "ฉันไม่แน่ใจว่าทำไมคุณถึงยังยืนอยู่ต่อหน้าฉัน แต่ฉันบอกได้เลยว่าคุณไม่ได้เป็นผู้วิเศษ";
			close;
		}
	}
	mes "[Mage Guildsman]";
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Mage) {
			mes "เฮ้เจ้าไม่เคยรู้มาก่อนหรือว่าเจ้าเป็นผู้วิเศษเจ้าเล่ห์!";
			mes "สักวันหนึ่งเจ้าจะตระหนักถึงพลังภายในของเจ้าเมื่อเจ้าสามารถสร้าง Fire ด้วยความคิดของเจ้า!";
		} else
			mes "เฮ้ ~ C'mon ออกจากการเล่นเกมคุณไม่สามารถเป็น Mage ได้เพราะคุณเป็นอาชีพอื่นอยู่แล้ว";
		close;
	}else{
		mes "เฮ้?";
		next;
		switch(select("ฉันต้องการเป็น Mage:อะไรคือข้อกำหนดของ Mage?:ไม่มีอะไรขอบคุณ")) {
		case 1:
			mes "[Mage Guildsman]";
			mes "อยากเป็นนักเวทย์หรือไม่เอ๊ะ ... ";
			next;
			mes "[Mage Guildsman]";
			if (Sex == SEX_MALE) {
				mes "เฮ้ดูนายสิเจ้าน่ารักจัง ~!";
			}else{
				mes "Oooh เธอช่างเป็นเด็กที่ร้อนแรง ~!";
				mes "ฉันชอบผู้หญิงที่ชอบคุณ ~";
			}
			mes "ถูกต้องเจ้าพูดว่าเจ้าอยากเป็นนักเวทย์หรือ";
			next;
			if(select("ฉันต้องการเป็น Mage:ไม่มีอะไรขอบคุณ") == 2) {
				mes "[Mage Guildsman]";
				mes "อะไรรรร~?! หลังจากที่คุณบอกฉันว่าคุณต้องการที่จะเป็นผู้วิเศษคุณเปลี่ยนความคิดของคุณหรือไม่! เด็ดขาด!";
				close;
			}
			mes "[Mage Guildsman]";
			if (!callfunc("F_CanChangeJob")) {
				mes "โอ้ระดับความสามารถพื้นฐานของคุณไม่สามารถที่จะเป็น Mage ได้";
				mes "ย้อนกลับไปและเพิ่มระดับทักษะพื้นฐานของคุณ";
				close;
			}
			mes "ได้เลย.. ลงชื่อในแบบฟอร์มใบสมัคร.... ว้าว ~ คุณมีลายมือดี! อ่าโอเคแค่นั้นแหละ";
			mes "คุณคือ... " + strcharinfo(0) + ".";
			next;
			mes "[Mage Guildsman]";
			mes "อืมข้าเห็นว่าเจ้าได้พยายามอย่างหนักในแบบของเจ้าแล้วดูเหมือนว่ามันจะค่อนข้างงุ่มง่าม แต่ข้าคิดว่ามันโอเคแล้ว!";
			mes "ดีมากการยึดติดกับพื้นฐานเป็นสิ่งที่ดีที่สุดเสมอ! ฉันจะเปลี่ยนคุณทันที";
			next;
			mes "[Mage Guildsman]";
			mes "Hahh.. ! ตอนนี้คุณเป็นผู้วิเศษหนึ่งในเพื่อนร่วมงานของเรา!";
			mes "เรายินดีต้อนรับคุณสู่ Mage Guild เพื่อนใหม่ของเรา!";
			next;
			mes "[Mage Guildsman]";
			mes "'ยินดีต้อนรับสู่ Mage Guild~'";
			mes "ขอแสดงความยินดีกับการเป็นสมาชิกของ Mage Guild! Go for it!";
			callfunc "Job_Change",Job_Mage;
			getitem 1639,1;	// N_Rod
			close;
		case 2:
			mes "[Mage Guildsman]";
			mes "อยากเป็นผู้วิเศษใช่มั้ย";
			mes "ฉันยินดีที่จะอธิบายข้อกำหนดสำหรับเด็กผู้หญิงที่น่ารักอย่างคุณ!";
			next;
			mes "[Mage Guildsman]";
			mes "ก่อนอื่นคุณต้องไปให้ถึงสามเณรงานระดับ 10 และเรียนรู้ทักษะพื้นฐานทั้งหมด";
			next;
			mes "[Mage Guildsman]";
			mes "ในอดีตมีการทดสอบยาที่ซับซ้อนเพราะเหตุนี้เราจึงสูญเสีย aplicant ช้า";
			mes "ดังนั้นเราตัดสินใจที่จะยอมรับผู้ใช้ที่มีคุณสมบัติตรงตามข้อกำหนดขั้นพื้นฐาน";
			next;
			mes "[Mage Guildsman]";
			mes "อย่าลังเลเพียงเป็นนักมายากล!";
			close;
		case 3:
			mes "[Mage Guildsman]";
			mes "ไม่มีอะไร... ?";
			close;
		}
	}
}
