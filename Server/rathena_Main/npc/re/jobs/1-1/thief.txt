//===== rAthena Script ======================================= 
//= Renewal Thief Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change to Thief Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//============================================================ 
moc_prydb1,39,129,2	script	Thief Guide#thief	69,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[Thief Guide]";
				mes "Huh? ฉันรู้จักคุณหรือเปล่ามันดูน่าขนลุกจนคุณคุ้นเคยคุณไม่มีคู่คุณใช่ไหม";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Thief Guide]";
					mes "เจ้าต้องการเป็นโจรหรือเปล่าข้าขอโทษ แต่ดูเหมือนว่าคุณต้องฝึกฝนมากกว่านี้";
					next;
					mes "[Thief Guide]";
					mes "ใช้เวลาของคุณและเรียนรู้ทักษะพื้นฐานทั้งหมดใช่มั้ยถ้างั้นมาเจอกันทีหลัง ~!";
					close;
				}
				mes "[Thief Guide]";
				mes "เอาล่ะฉันรู้สึกแบบนี้มาตลอดชีวิตของการต่อสู้ดังนั้นฉันจึงกำลังโปรโมตคุณให้กับ Thief ในนาทีนี้ฉันจะให้สิ่งที่คุณต้องการแก่คุณ... ";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Thief_High;
				skill "TF_SPRINKLESAND",1,SKILL_PERM;
				skill "TF_BACKSLIDING",1,SKILL_PERM;
				skill "TF_PICKSTONE",1,SKILL_PERM;
				skill "TF_THROWSTONE",1,SKILL_PERM;
				mes "[Thief Guide]";
				mes "เมื่อเจ้าเป็น Thief จงใช้ชีวิตเหมือน Thief จงไปหามันต่อไป!~";
				close;
			}else{
				mes "[Thief Guide]";
				if (Sex == SEX_MALE)
					mes "เฮ้เพื่อน";
				else
					mes "เฮ้ที่รัก~";
				close;
			}
		}else{
			mes "[Thief Guide]";
			if (Sex == SEX_MALE)
				mes "เฮ้เพื่อน";
			else
				mes "เฮ้ที่รัก";
				mes "... เฮ้! คุณดูดีเกินไปแล้ว - ดีเกินกว่าที่จะต้องการเป็นโจร !! ตอนนี้คุณกำลังยุ่งอยู่กับการต่อไป!";
			close;
		}
	}
	if(q_job_thief == 1) {
		mes "[Thief Guildsman]";
		mes "อ๊ะ ... ดูเหมือนคนที่รัก" + (Sex == SEX_MALE? "ลูกชาย": "ลูกสาว") + "ของครอบครัวที่ร่ำรวย แต่ทำไมคุณถึงมาอยู่ที่นี่ในที่สกปรกแห่งนี้";
		next;
		mes "[Thief Guildsman]";
		mes "เอาล่ะฉันไม่ได้คิดที่จะทำให้เจ้าเป็นขโมยฉันแค่ยอมรับแอพพลิเคชั่นเอามาใช่ไหม";
		mes "ถ้าเจ้าอยากเป็นโจรให้ถามคนที่มีตาแหลมข้างข้า";
		close;
	}
	mes "[Thief Guildsman]";
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Thief) {
			mes "เฮ้ ~ ถ้าเจ้ามีปัญหาอะไรให้ฉันออกไปได้ทุกเวลาเหรอ?";
			close;
		}else{
			mes "What the heck...?";
			if(Sex == SEX_MALE) {
				mes "Huh... คุณคือ" + jobname(Class) + ", blockhead!";
				next;
				mes "[Thief Guildsman]";
				mes "เฮ้พี่ชาย";
			}else{
				next;
				mes "[Thief Guildsman]";
				mes "เฮ้ผู้หญิง";
			}
			mes "เจ้ามาที่นี่ทำไมกลับไปยังที่ของเจ้า~ กลับไป~~";
			close;
		}
	}
	mes "อ๊ะ... ดูเหมือนคนที่รัก" + (Sex == SEX_MALE? "ลูกชาย": "ลูกสาว") + "ของครอบครัวที่ร่ำรวย แต่ทำไมคุณถึงมาอยู่ที่นี่ในที่สกปรกแห่งนี้";
	next;
	if(select("ฉันอยากเป็นโจร:ไม่มีอะไรเลย") == 2) {
		mes "[Thief Guildsman]";
		mes "คุณรู้ว่าคุณไม่สามารถเป็นขโมยได้หากไม่มีใบสมัคร ... ";
		mes "คุณคิดอะไรอยู่ ... ?";
		close;
	}
	mes "[Thief Guildsman]";
	mes "เอ่อเจ้าภูมิใจขนาดนั้นเหรอ?";
	mes "เจ้าบอกข้าอย่างภูมิใจว่าเจ้าอยากเป็นขโมย! ทำไมเจ้าไม่ไปที่หมู่บ้านทั้งหมดและโฆษณาตัวเองเพื่อเป็นขโมย";
	mes "'Ha ha ha! Go ติดป้ายที่บอกว่า' ฉันจะเป็นขโมยที่ภูมิใจที่ขโมยสิ่งของของคนอื่น '";
	next;
	mes "[Thief Guildsman]";
	mes "เจ้าต้องการที่จะเป็นโจรอย่างเลวร้ายงั้นเหรอ?";
	next;
	switch(select("ใช่แล้ว:ไม่เลย:แล้วคุณล่ะ?")) {
	case 1:
		mes "[Thief Guildsman]";
		mes "โอ้, คุณ...? Huh..ดี... ฉันมีชีวิตอยู่และเรียนรู้ที่จะเห็นคนแปลกหน้าเช่นคุณ";
		next;
		break;
	case 2:
		mes "[Thief Guildsman]";
		mes "ถ้าอย่างนั้นทำไมเจ้ามาที่นี่ทำไมเจ้าคิดว่าเจ้าสามารถกลายเป็นขโมยได้อย่างง่ายดายเหรอ?";
		next;
		break;
	case 3:
		mes "[Thief Guildsman]";
		mes "เอ๊ะ...? ฉัน? ฉัน?";
		mes "อืม...ฉันแค่พอดีที่จะเป็นโจร...โดยเฉพาะ...ฉันไม่สนใจเรื่องโง่ ๆ แบบนี้";
		next;
		break;
	}
	mes "[Thief Guildsman]";
	mes "อย่างไรก็ตามในโลกภายนอกอย่าพูดว่าคุณต้องการเป็นโจร !!";
	next;
	mes "[Thief Guildsman]";
	mes "งั้นคุณต้องการสมัครเพื่อเป็นโจรหรือไม่?";
	next;
	if(select("Yes, I do.:No.") == 2) {
		mes "[Thief Guildsman]";
		mes "เอ่อ...ทำในสิ่งที่เจ้าอยากจะทำ~ไปตามทาง~";
		close;
	}
	mes "[Thief Guildsman]";
	mes "ได้โปรดบอกชื่อของคุณหน่อยสิอะไรนะ?";
	mes "" + strcharinfo(0) + " ดังนั้น " + strcharinfo(0) + ", ขวา ... ?";
	next;
	if (!callfunc("F_CanChangeJob")) {
		mes "[Thief Guildsman]";
		mes "ข้าเห็นความตั้งใจอันแรงกล้าของคุณที่จะเป็นจอมโจร ...... ";
		mes "แต่ด้วยความตั้งใจของเจ้าเท่านั้นเจ้าไม่สามารถสู้มันได้จริงหรือ?";
		mes "จงไปให้ถึงระดับทักษะขั้นพื้นฐานอย่างน้อย 9";
		close;
	}
	mes "[Thief Guildsman]";
	mes "อืม .... ฉันพบประวัติอาชญากรรมที่น่าสนใจของคุณจากข้อมูลของสำนักงานนักสืบคุณดูเหมือนจะมีสิ่งที่จะเป็นขโมยที่ดี";
	mes "" + strcharinfo(0) + "ตอนนี้ฉันประกาศว่าคุณผ่านการสัมภาษณ์อาชีพแล้ว!";
	next;
	mes "[Thief Guildsman]";
	mes "เอาล่ะฉันไม่ได้คิดที่จะทำให้เจ้าเป็นขโมยฉันแค่ยอมรับแอพพลิเคชั่นเอามาใช่ไหม";
	mes "ถ้าคุณอยากเป็นโจรให้ถามคนที่อยู่ข้างๆฉัน";
	set q_job_thief,1;
	close;
}

moc_prydb1,42,133,2	script	Thief Guildsman#thief	118,{
	if(q_job_thief == 1) {
		mes "[Thief Guildsman]";
		mes "ได้เลยเจ้าต้องผ่านการสัมภาษณ์อาชีพเหรอ?";
		mes "ดีฉันจะยอมรับคุณ";
		mes "มาเริ่มพิธีเปลี่ยนอาชีพของกิลด์ของเรา!";
		next;
		mes "[Thief Guildsman]";
		mes "'" + strcharinfo(0) + ".'";
		mes "'สำหรับผู้ที่มีความพร้อมแสงจันทร์จะเปิดทางตอนนี้คุณพร้อมแล้วตอนนี้ฉันอนุญาตให้คุณเป็นสมาชิกของสมาคมโจรได้อย่างเป็นทางการ'";
		callfunc "Job_Change",Job_Thief;
		set q_job_thief,0;
		getitem 13041,1;	// N_Main_Gauche
		next;
		mes "[Commander of Thief Guild]";
		mes "'ขอแสดงความยินดีกับการเป็นโจร'";
		mes "'จากนี้ไปให้รักษากฎของกิลด์ของเราและเป็นสมาชิกที่มีเกียรติ'";
		mes "'ถ้าคุณนำความอับอายมาให้เราด้วยการทำผิดกฎของเราคุณควรระวังหลังเจ้าด้วย'";
		mes "'อย่างไรก็ตามข้าหวังว่าเจ้าจะเป็นขโมยที่ยิ่งใหญ่'";
		next;
		mes "[Brad]";
		mes "Heee ~ Yaaaa ~! ขอแสดงความยินดีด้วยเพื่อนของฉัน";
		mes "ฉันชื่อ 'Brad' ฉันรับผิดชอบทรัพยากรมนุษย์ที่นี่";
		mes "ตอนนี้ฉันยังไม่แน่ใจ แต่คุณจะมีโอกาสได้พบฉันอีกในภายหลัง";
		next;
		mes "[Brad]";
		mes "โอเคฉันได้ทำในสิ่งที่ฉันสามารถทำกับคุณได้แล้วไปเถอะฉันเป็นคนค่อนข้างยุ่ง";
		mes "แล้วเจอกันอีก";
		close;
	}
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Thief) {
			mes "[Brad]";
			mes "ตอนนี้ฉันไม่มีกิจกรรมพิเศษดังนั้นไปทางแล้วกลับมาใหม่ในภายหลัง";
		}else{
			mes "[Thief Guildsman]";
			mes "เฮ้~ เฮ้~ เจ้าไม่ใช่มือใหม่หรือขโมย!";
			mes "เจ้ามาทำอะไรที่นี่? เจ้าไม่ยินดีที่จะทำให้ที่นี่เป็นบ้านของเจ้า~ Hweeeee~ ออกไปที่นี่~";
		}
		close;
	}
	mes "[Thief Guildsman]";
	mes "โฮทำไมเป็นสามเณรอย่างคุณที่มาที่นี่";
	mes "ถ้าเจ้าอยู่ที่นี่เพื่อเป็นขโมยจงถามผู้หญิงที่น่ารังเกียจที่อยู่ข้างๆข้า";
	close;
}
