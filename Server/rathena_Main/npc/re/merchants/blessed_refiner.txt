//===== rAthena Script ======================================= 
//= Blessed Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiners that use Blessed ores to refine equipment.
//= Upon failure, the equipment is not destroyed. The success
//= rate is identical to that for Enriched ores.
//= - "Blacksmith Dister" only refines from +6~12.
//= NOTE: This NPC is currently disabled on official servers.
//===== Changelog: ===========================================
//= 1.0 First version. [Euphy]
//= 1.1 Removed re-roll behavior. [Secret]
//============================================================

// Main NPC :: new_smelting612
//============================================================
-	script	::BlacksmithDister	-1,{
	disable_items;
	mes "[Blacksmith Dister]";
	mes "ในสังคมที่มีการแข่งขันที่สูง, เพื่อความอยู่รอด!";
	mes "ข้าขอเสนอการตีบวกในระดับที่ +6 ~ 12.";
	next;
	mes "[Blacksmith Dister]";
	mes "ข้าต้องการแค่ ^ff9999Blessed Oridecon^000000 สำหรับบวกอาวุธ, และ ^ff9999Blessed Elunium^000000 สำหรับบวกชุดเกราะ.";
	mes "ถ้าการบวกล้มเหลว ^FF0000ของจะไม่สูญหายและค่าตีบวกก็คงเดิม^000000.";
	next;
	mes "[Blacksmith Dister]";
	mes "ว่าไงละ ? เจ้าอยากจะตีบวกไหม ?";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[ไม่ได้สวมใส่]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Blacksmith Dister]";
		switch(.@part) {
		case EQI_HEAD_TOP:
			mes "ข้าเป็น blacksmith, ไม่ใช่ช่างทำผม.";
			break;
		case EQI_ARMOR:
			mes "ค้อนในมือข้า, อาจทำให้เจ้าเป็นดาวบนฟ้าก็ได้.";
			break;
		case EQI_HAND_L:
		case EQI_HAND_R:
			mes "มันไม่ใช่งานที่ข้าถนัด.";
			break;
		case EQI_GARMENT:
			mes "เอาไอเท็มมาข้าอยากจะตีบวก!";
			break;
		case EQI_SHOES:
			mes "เจ้าได้กลิ่นอะไรไหม ?";
			break;
		case EQI_ACC_L:
		case EQI_ACC_R:
			mes "ประดับอยู่ไหน ?";
			break;
		case EQI_HEAD_MID:
			mes "เจ้าอยากให้ข้าบวกอะไร ?";
			break;
		case EQI_HEAD_LOW:
			mes "หือ ? เจ้าต้องการให้ข้าทำอะไร ?";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[Blacksmith Dister]";
		mes "ไอเท็มนี้มันบวกไม่ได้.";
		close;
	}
	.@equip_id = getequipid(.@part);
	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	if (.@equip_refine < 6 || .@equip_refine > 12) {
		mes "[Blacksmith Dister]";
		mes "ของสวมใส่นี้มีค่าบวกที่ "+.@equip_refine+". ข้าบวกเฉพาะ +6 ถึง +12 เท่านั้น!";
		close;
	}
	set .@equip_lv, getequipweaponlv(.@part);
	.@material = getequiprefinecost(.@part, REFINE_COST_BLESSED, REFINE_MATERIAL_ID);
	.@price = getequiprefinecost(.@part, REFINE_COST_BLESSED, REFINE_ZENY_COST);
	switch(.@equip_lv) {
	default:
	case 0:
		set .@type$,"Armor";
		break;
	case 1:
		set .@type$,"Weapon";
		break;
	case 2:
		set .@type$,"Weapon";
		break;
	case 3:
		set .@type$,"Weapon";
		break;
	case 4:
		set .@type$,"Weapon";
		break;
	}
	set .@ore$,"^ff9999Blessed "+.@type$+" Ore^000000";
	mes "[Blacksmith Dister]";
	mes "ของที่จะบวกคือ "+.@type$+" มีค่าตีบวกอยู่ที่ "+.@equip_refine+". ในการตีบวก, ข้าต้องการ "+.@ore$+" และ "+callfunc("F_InsertComma",.@price)+" zeny เป็นค่าบริการ.";
	mes "เจ้าจะทำต่อไหม ?";
	next;
	if(select("ใช่:ไม่") == 2) {
		mes "[Blacksmith Dister]";
		mes "ข้าไม่ค่อยมีเวลาว่างนัก, อย่ามาทำเป็นเล่นตลกกับข้า!";
		close;
	}
	if (getequippercentrefinery(.@part, REFINE_COST_BLESSED) < 100) {
		mes "[Blacksmith Dister]";
		mes "ของที่จะบวกคือ "+.@type$+" ได้ถูกตีบวกมาหลายครั้งแล้ว.แม้ว่ามันจะไม่ล้มเหลว, แต่แร่ "+.@ore$+" ก็จะหายไป!";
		next;
		mes "[Blacksmith Dister]";
		mes "แน่นอน, ว่าเจ้าจะไม่ได้เงินคืนหรอกน่ะ! ยังอยากจะทำต่อไหม ?";
		next;
		if(select("ใช่:ไม่") == 2) {
			mes "[Blacksmith Dister]";
			mes "ก็ดี. เพราะข้าคงเสียใจแย่ถ้ามันบวกล้มเหลว.";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[Blacksmith Dister]";
		mes "มีของไม่พอ.";
		mes "ในการบวก "+((.@equip_lv)?"ที่ระดับ "+.@equip_lv+" weapon":"armor")+", ข้าต้องการ "+.@ore$+" และ "+callfunc("F_InsertComma",.@price)+" zeny เป็นค่าบริการ.";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	mes "[Blacksmith Dister]";
	mes "ก๊อง! ก๊อง! ก๊อง!";
	if (getequippercentrefinery(.@part, REFINE_COST_BLESSED) > rand(100)) {
		specialeffect EF_BLESSING;
		successrefitem .@part;
		next;
		mes "[Blacksmith Dister]";
		mes "ฮ่าฮ่าฮาฮาฮาฮา!!!";
		next;
		mes "[Blacksmith Dister]";
		mes "มันประสบความสำเร็จล่ะ~ ฮ่าฮา!";
		close;
	}
	specialeffect EF_CURSEATTACK;
	specialeffect2 EF_SUI_EXPLOSION;
	next;
	emotion (!rand(5))?ET_ANGER:ET_HUK;
	mes "[Blacksmith Dister]";
	mes "โอ้ ม่ายน่าาาาา!!!!";
	next;
	mes "[Blacksmith Dister]";
	mes "พลังที่สิงสถิตใน "+.@ore$+" มันมีไม่พอ...";
	next;
	mes "[Blacksmith Dister]";
	mes "ข้าอารมณ์บ่จอยทุกครั้งที่ล้มเหลว!!";
	close;
}
prt_in,52,56,3	duplicate(BlacksmithDister)	Blacksmith Dister#prt	826
payon,148,172,3	duplicate(BlacksmithDister)	Blacksmith Dister#pay	826
alberta_in,20,56,3	duplicate(BlacksmithDister)	Blacksmith Dister#alb	826
yuno_in01,175,18,3	duplicate(BlacksmithDister)	Blacksmith Dister#yuno	826
ein_in01,26,82,3	duplicate(BlacksmithDister)	Blacksmith Dister#ein	826
lhz_in02,280,15,3	duplicate(BlacksmithDister)	Blacksmith Dister#lhz	826
