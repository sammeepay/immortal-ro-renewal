//===== rAthena Script =======================================
//= Upgrade Weapon Enchants
//===== By: ==================================================
//= Skorm
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds enchantments to Upgrade weapons.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Standardizing, grammar and bug fixes. [Euphy]
//============================================================

// Main NPC :: 201105_luk_enc
//============================================================
prt_in,28,73,3	script	Devil Enchant Master#prq	63,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "เจ้าแบกของมามากเกิน, กรุณาไปลดของก่อนแล้วค่อยกลับมาใหม่!";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "กระเป๋าเจ้ามีน้ำหนักเกิน, กรุณาไปลดน้ำหนักก่อนแล้วค่อยกลับมาใหม่!";
		close;
	}
	mes "[Devil Enchant Master]";
	mes "เอ๊ะ ?";
	mes "เจ้ามองเห็นข้าหรอ ?";
	next;
	switch(select("นี้เป็นครั้งแรกเลยที่เห็นเจ้า!:ข้าอยากออฟอาวุธ!:โปรดลบออฟด้วย.")) {
	case 1:
		mes "[Devil Enchant Master]";
		mes "555~ แน่นอน, ข้าไม่ใช่คนประเภทที่จะปรากฎตัวออกมาบ่อยนักหรอก.";
		next;
		mes "[Devil Enchant Master]";
		mes "สำหรับเจ้าที่เห็นข้า! ถ้าเจ้าทำข้อตกลงกับข้า, ข้าจะออฟของให้เจ้า!!";
		next;
		mes "[Devil Enchant Master]";
		mes "เจ้าเห็นฉายาของข้าไหม ? มีไม่กี่คนหรอกที่มีฉายานี้!";
		next;
		mes "[Devil Enchant Master]";
		mes "ข้าจะออฟได้ก็ต่อเมื่อเจ้ามี ^0000ffEnchant Book^000000 มาด้วย!";
		next;
		mes "[Devil Enchant Master]";
		mes "ไม่งั้น, ข้าไม่สามารถออฟของให้เจ้าได้....";
		close;
	case 2:
		if (!countitem(6484)) {
			mes "[Devil Enchant Master]";
			mes "เจ้าได้ยินที่ข้าบอกไหม ? ข้าจะออฟได้ก็ต่อเมื่อเจ้ามี Enchant Book!";
			close;
		}
		set .@select,1;
		break;
	case 3:
		mes "[Devil Enchant Master]";
		if (Zeny < 100000) {
			mes "ค่าลบออฟ 100,000 zeny. ดูเหมือนว่าเจ้ามีเงินไม่พอ...";
			close;
		}
		mes "ค่าลบออฟ 100,000 zeny และอาวุธที่เจ้าต้องการจะลบออฟ!";
		next;
		if (select("ขอคิดดูก่อน.:ลบเลย!") == 1) {
			mes "[Devil Enchant Master]";
			mes "กลับมาใหม่เมื่อเจ้าแน่ใจ!";
			close;
		}
		set .@select,2;
		break;
	}
	set .@part, EQI_HAND_R;

	mes "[Devil Enchant Master]";
	if (!getequipisequiped(.@part)) {
		mes "เจ้าใส่อาวุธมาหรือเปล่า ?";
		close;
	}
	setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	// if (!getequipisequiped(.@part)) {
	// 	mes "มันอันตรายถ้าเจ้าเอาอาวุธออกขณะกำลังออฟ!";
	// 	close;
	// }
	set .@equip_id, getequipid(.@part);
	.@equip_refine = getequiprefinerycnt(.@part);
	set .@item$, "|1292|1394|1491|1585|2015|13071|13115|16019|18112|21000|";
	if (!compare(.@item$,"|"+.@equip_id+"|")) {
		mes "ข้าไม่อยากออฟอาวุธอันนี้!";
		close;
	}

	if (.@select == 1) {
		if (!countitem(6484)) {
			mes "เจ้าได้ยินที่ข้าบอกไหม ? ข้าจะออฟได้ก็ต่อเมื่อเจ้ามี Enchant Book!";
			close;
		}
		mes "เลือกค่าที่ต้องการออฟ ?";
		next;
		switch(select("ขอคิดดูก่อน.:Physical Series:Magical Series")) {
		case 1:
			mes "[Devil Enchant Master]";
			mes "กลับมาใหม่เมื่อเจ้าแน่ใจ!";
			close;
		case 2:
			set .@enc_type,1;
			break;
		case 3:
			set .@enc_type,2;
			break;
		}
		mes "[Devil Enchant Master]";
		if (.@equip_card[3]) {
			mes "อาวุธนี้มีออฟอยู่แล้ว! เจ้าต้องลบออฟออกก่อนที่จะออฟอีกครั้ง.";
			close;
		}
		mes "ถ้าออฟล้มเหลว, ^ff0000มันจะลดค่าตีบวกลง^000000, แต่ช่องใส่การ์ดและอาวุธจะไม่หายไป! อยากทำต่ออยู่ไหม ?";
		next;
		if (select("โอกาสหน้าล่ะกัน!:เริ่มเลย!") == 1) {
			mes "[Devil Enchant Master]";
			mes "กลับมาใหม่เมื่อเจ้าแน่ใจ!";
			close;
		}
		if (.@equip_card[3]) {
			mes "[Devil Enchant Master]";
			mes "มันจะเป็นปัญหาน่ะ, ถ้าอาวุธนี้มีออฟอยู่แล้ว.";
			close;
		}

		if (.@enc_type == 1) { // Physical Series
			set .@i, rand(1,1300);
			     if (.@i < 51)    set .@enchant,4734; //Agility5
			else if (.@i < 76)    set .@enchant,4735; //Agility6
			else if (.@i < 88)    set .@enchant,4736; //Agility7
			else if (.@i < 93)    set .@enchant,4737; //Agility8
			else if (.@i < 95)    set .@enchant,4738; //Agility9
			else if (.@i < 96)    set .@enchant,4739; //Agility10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4704; //Strength5
			else if (.@i < 341)   set .@enchant,4705; //Strength6
			else if (.@i < 366)   set .@enchant,4706; //Strength7
			else if (.@i < 378)   set .@enchant,4707; //Strength8
			else if (.@i < 383)   set .@enchant,4708; //Strength9
			else if (.@i < 384)   set .@enchant,4709; //Strength10
			else if (.@i < 434)   set .@enchant,4754; //Luck5
			else if (.@i < 459)   set .@enchant,4755; //Luck6
			else if (.@i < 471)   set .@enchant,4756; //Luck7
			else if (.@i < 476)   set .@enchant,4757; //Luck8
			else if (.@i < 478)   set .@enchant,4758; //Luck9
			else if (.@i < 479)   set .@enchant,4759; //Luck10
			else if (.@i < 679)   set .@enchant,4744; //Vitality5
			else if (.@i < 779)   set .@enchant,4745; //Vitality6
			else if (.@i < 829)   set .@enchant,4746; //Vitality7
			else if (.@i < 854)   set .@enchant,4747; //Vitality8
			else if (.@i < 866)   set .@enchant,4748; //Vitality9
			else if (.@i < 867)   set .@enchant,4749; //Vitality10
			else if (.@i < 967)   set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 992)   set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 1092)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1117)  set .@enchant,4836; //Expert_Archer5
			else if (.@i < 1217)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1242)  set .@enchant,4836; //Expert_Archer5
			else set .@enchant,0;
		} else if (.@enc_type == 2) { // Magical Series
			set .@i, rand(1,1200);
			     if (.@i < 51)    set .@enchant,4714; //Inteligence5
			else if (.@i < 76)    set .@enchant,4715; //Inteligence6
			else if (.@i < 88)    set .@enchant,4716; //Inteligence7
			else if (.@i < 93)    set .@enchant,4717; //Inteligence8
			else if (.@i < 95)    set .@enchant,4718; //Inteligence9
			else if (.@i < 96)    set .@enchant,4719; //Inteligence10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4734; //Agility5
			else if (.@i < 341)   set .@enchant,4735; //Agility6
			else if (.@i < 366)   set .@enchant,4736; //Agility7
			else if (.@i < 378)   set .@enchant,4737; //Agility8
			else if (.@i < 383)   set .@enchant,4738; //Agility9
			else if (.@i < 384)   set .@enchant,4739; //Agility10
			else if (.@i < 484)   set .@enchant,4754; //Luck5
			else if (.@i < 534)   set .@enchant,4755; //Luck6
			else if (.@i < 559)   set .@enchant,4756; //Luck7
			else if (.@i < 571)   set .@enchant,4757; //Luck8
			else if (.@i < 576)   set .@enchant,4758; //Luck9
			else if (.@i < 577)   set .@enchant,4759; //Luck10
			else if (.@i < 777)   set .@enchant,4744; //Vitality5
			else if (.@i < 877)   set .@enchant,4745; //Vitality6
			else if (.@i < 927)   set .@enchant,4746; //Vitality7
			else if (.@i < 952)   set .@enchant,4747; //Vitality8
			else if (.@i < 964)   set .@enchant,4748; //Vitality9
			else if (.@i < 969)   set .@enchant,4749; //Vitality10
			else if (.@i < 1069)  set .@enchant,4812; //Spell4
			else if (.@i < 1094)  set .@enchant,4826; //Spell5
			else if (.@i < 1119)  set .@enchant,4761; //Matk2
			else if (.@i < 1124)  set .@enchant,4806; //Matk3
			else set .@enchant,0;
		} else {
			mes "[Devil Enchant Master]";
			mes "หืม! อาวุธนี้มีปัญหา, กรุณาตรวจดูใหม่!";
			close;
		}
		mes "[Devil Enchant Master]";
		if (.@equip_card[3]) {
			mes "อาวุธนี้มีออฟอยู่แล้ว!";
			close;
		}
		if (!countitem(6484)) {
			mes "เจ้าได้ยินที่ข้าบอกไหม ? ข้าจะออฟได้ก็ต่อเมื่อเจ้ามี Enchant Book!";
			close;
		}
		if (.@enchant == 0) {
			specialeffect EF_SHIELDCHARGE;
			mes "โอ้! ไม่น่าเชื่อ!! มันล้มเหลว!! โปรดกลับมาใหม่!";
			set .@lost_refine, rand(0,.@equip_refine);
			set .@equip_refine, .@equip_refine - .@lost_refine;
		} else {
			specialeffect EF_REPAIRWEAPON;
			mes "ช่องที่ ^9900004^000000 ได้ออฟแล้ว!";
		}
		delitem 6484,1; //Enchant_Book

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@enchant
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;

		if (.@lost_refine) {
			next;
			mes "[Devil Enchant Master]";
			mes "เมื่อล้มเหลวมันจะลดค่าตีบวกลง "+callfunc("F_InsertPlural",.@lost_refine,"level")+"! อย่าน้อยใจเลย!";
		}
		close;
	} else if (.@select == 2) {
		if (Zeny < 100000) {
			mes "เจ้าต้องมีเงินเพื่อลบออฟ!!";
			close;
		}
		if (callfunc("F_IsCharm",.@equip_card[3]) == false) {
			mes "ของนี้ลบออฟไม่ได้!";
			close;
		}
		if (!getequipisequiped(.@part)) {
			mes "เจ้าไม่ได้ใส่อาวุธมา ?";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		mes "ข้าลบออฟให้แล้ว.";
		set Zeny, Zeny - 100000;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] 0
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],0;

		close;
	} else {
		mes "เจ้ามาผิดที่แล้ว??";
		close;
	}
}
