//===== rAthena Script =======================================
//= Guild Warehouse Manager
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Gives players access to their guild storage and log.
//===== Changelogs: ==========================================
//= 1.0 First Version. [Lemongrass]
//============================================================

-	script	GuildWarehouse	-1,{
	mes "[Warehouse Manager]";
	mes "สบายดีไหม ? พวกเราเป็นผู้เชี่ยวชาญในคลังกิลด์. นี่เป็นเรื่่องราวของ ^0000cdพวกเรา^000000:";
	mes "ทำไมสมาชิกกิลด์ถึงไม่สามารถแบ่งปันคลังร่วมกันได้ ? เราเริ่มต้นกิจการด้วยคำถามง่ายๆแบบนั้น.";
	next;
	mes "[Warehouse Manager]";
	mes "เจ้าสามารถเปิดคลังด้วยเงิน 1000 Zeny หรือค้นหาประวัติการใช้งานของกิลด์ได้.";
	mes "มีอะไรให้ข้าช่วยไหม ?";
	next;
	if( select( "เปิดคลังกิลด์:ดูุประวัติการใช้คลัง" ) == 1 ){
		.@guildid = getcharid( 2 );

		if( .@guildid == 0 ){
			mes "[Warehouse Manager]";
			mes "คลังกิลด์ใช้ได้เฉพาะกับสมาชิกกิลด์เท่านั้น.";
			close;
		}

		if( getgdskilllv( .@guildid, "GD_GUILD_STORAGE" ) == 0 || !guild_has_permission( GUILD_PERM_STORAGE ) ){
			mes "[Warehouse Manager]";
			mes "ดูเหมือนว่าจะไม่สามารถใช้งานคลังกิลด์ได้หรือเจ้าไม่มีสิทธิ์เข้าถึงคลังกิลด์.";
			mes "กรุณามาใหม่หลังจากตรวจดูสกิลกิลด์และสิทธิ์การใช้คลัง.";
			close;
		}

		if( Zeny < 1000 ){
			mes "[Warehouse Manager]";
			mes "น่าเกลียดมากที่เจ้าไม่มีเงินจ่าย.";
			mes "มันแค่ 1000 Zeny เอง.";
			close;
		}

		mes "[Warehouse Manager]";
		mes "ข้าจะเปิดคลังกิลด์ให้เจ้า. จดจำช่วงเวลานี้ไว้!";
		close2;

		if( Zeny < 1000 ){
			// Cheat prevention
			end;
		}

		if( guildopenstorage() == GSTORAGE_OPEN ){
			Zeny -= 1000;
			end;
		}else{
			mes "[Warehouse Manager]";
			mes "ข้าเกรงว่ามีคนอื่นใช้คลังกิลด์อยู่ในขณะนี้.";
			mes "กรุณามาลองใหม่ในภายหลัง.";
			close;
		}
	}else{
		.@guildid = getcharid( 2 );

		if( .@guildid == 0 ){
			mes "[Warehouse Manager]";
			mes "คลังกิลด์ใช้ได้เฉพาะกับสมาชิกกิลด์เท่านั้น.";
			close;
		}

		if( getgdskilllv( .@guildid, "GD_GUILD_STORAGE" ) == 0 || !guild_has_permission( GUILD_PERM_STORAGE ) ){
			mes "[Warehouse Manager]";
			mes "ดูเหมือนว่าจะไม่สามารถใช้งานคลังกิลด์ได้หรือเจ้าไม่มีสิทธิ์เข้าถึงคลังกิลด์.";
			mes "กรุณามาใหม่หลังจากตรวจดูสกิลกิลด์และสิทธิ์การใช้คลัง.";
			close;
		}

		mes "[Warehouse Manager]";
		mes "ข้าจะแสดงประวัติการใช้งานของคลังให้เจ้าดูุ. โดยที่ประวัติการใช้งานจะถูกเก็บไว้นานถึง 3 เดือน.";
		mes "จดจำช่วงเวลานี้ไว้!";
		close2;
		guildopenstorage_log();
		end;
	}
}

alberta,114,65,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#alberta	896
aldebaran,146,122,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#aldebaran	896
amatsu,100,156,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#amatsu	896
ayothaya,203,173,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#ayothaya	896
brasilis,204,227,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#brasilis	896
comodo,204,153,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#comodo	896
dewata,196,193,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#dewata	896
einbech,182,124,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#einbech	896
einbroch,238,203,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#einbroch	896
geffen,128,68,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#geffen	896
gonryun,164,127,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#gonryun	896
harboro1,288,212,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#harboro1	896
hugel,91,158,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#hugel	896
izlude,133,149,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#izlude	896
lighthalzen,162,102,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#lighthalzen	896
louyang,210,111,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#louyang	896
malaya,238,206,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#malaya	896
morocc,168,107,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#morocc	896
moscovia,211,200,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#moscovia	896
niflheim,200,184,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#niflheim	896
payon,180,106,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#payon	896
prontera,150,191,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#prontera	896
rachel,123,145,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#rachel	896
umbala,106,160,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#umbala	896
veins,205,130,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#veins	896
xmas,155,140,5	duplicate(GuildWarehouse)	Guild Warehouse Manager#xmas	896
yuno,176,187,3	duplicate(GuildWarehouse)	Guild Warehouse Manager#yuno	896
