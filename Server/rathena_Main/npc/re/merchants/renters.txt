//===== rAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Renewal-specific breeder NPCs.
//===== Additional Comments: ================================= 
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Updated to match the official scripts. [Euphy]
//============================================================ 

// Dragon Breeder :: dragon
//============================================================
job3_rune01,88,62,5	script	Dragon Breeder	105,{
	mes "[Dragon Breeder]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune_Knight) {
		mes "ยินดีต้อนรับคุณต้องการเช่ามังกรไหม?";
		next;
		switch(select("ใช่ข้าต้องการ:ไม่")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Dragon Breeder]";
				mes "โปรดเรียนรู้วิธีขี่มังกรก่อน";
				close;
			}
			else if (checkdragon()) {
				mes "[Dragon Breeder]";
				mes "เจ้ามีมังกรอยู่แล้ว";
				close;
			}
			else if (ismounting()) {
				mes "[Dragon Breeder]";
				mes "กรุณาลงจากขี่สัตว์ของคุณออกก่อน";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Dragon Breeder]";
			mes "ฉันเห็นแล้วมีวันที่ดี";
			close;
		}
	}
	mes "เจ้ามาทำอะไรที่นี่";
	mes "มีเพียง Rune Knights เท่านั้นที่สามารถเช่ามังกรได้";
	close;
}

// Dragon/Gryphon Master :: dragon
//============================================================
prontera,130,213,5	script	Riding Creature Master	105,{
	mes "[Riding Creature Master]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune_Knight) {
		mes "ยินดีต้อนรับคุณต้องการเช่ามังกรไหม?";
		next;
		switch(select("ใช่ข้าต้องการ:ไม่")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "โปรดเรียนรู้วิธีขี่มังกรก่อน";
				close;
			}
			else if (checkdragon()) {
				mes "[Riding Creature Master]";
				mes "เจ้ามีมังกรอยู่แล้ว";
				close;
			}
			else if (ismounting()) {
				mes "[Riding Creature Master]";
				mes "กรุณาลงจากขี่สัตว์ของคุณออกก่อน";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "ฉันเห็นแล้วมีวันที่ดี";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Royal_Guard) {
		mes "ยินดีต้อนรับคุณต้องการเช่า Gryphon ไหม?";
		next;
		switch(select("ใช่ข้าต้องการ:ไม่")) {
		case 1:
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "โปรดเรียนรู้วิธีขี่ Gryphon ก่อน";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "เจ้ามี Gryphon อยู่แล้ว";
				close;
			}
			else if (ismounting()) {
				mes "[Riding Creature Master]";
				mes "กรุณาลงจากขี่สัตว์ของคุณออกก่อน";
				close;
			}
			setriding;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "ฉันเห็นแล้วมีวันที่ดี";
			close;
		}
	}
	mes "ข้าอยู่ที่นี่เพื่อมอบ Rune Knights และ Royal Guards ให้กับสัตว์ที่ขี่";
	close;
}
geffen,100,55,3	duplicate(Riding Creature Master)	Riding Creature Master#2	105
payon,166,102,5	duplicate(Riding Creature Master)	Riding Creature Master#3	105
aldebaran,133,109,5	duplicate(Riding Creature Master)	Riding Creature Master#4	105
yuno,171,187,3	duplicate(Riding Creature Master)	Riding Creature Master#5	105
rachel,106,130,5	duplicate(Riding Creature Master)	Riding Creature Master#6	105

// Peco removing NPC :: dragon
//============================================================
prontera,125,208,5	script	Peco Peco Remover	105,{
	mes "[Soldier]";
	mes "ถ้าเจ้าไม่สามารถลงจาก Peco Peco";
	mes "ด้วยเหตุผลบางอย่างที่ไม่ทราบ";
	mes "ได้โปรดใช้บริการของข้าด้วย";
	if (checkriding()) {
		next;
		mes "[Soldier]";
		mes "คุณกำลังขี่ Peco Peco";
		mes "คุณต้องการลงจากหลังม้าหรือไม่";
		next;
		switch(select("ใช่ข้าต้องการ:ไม่")) {
		case 1:
			setriding 0;
			mes "[Soldier]";
			mes "พูดแล้วรู้สึกยังไง";
			mes "เหยียบพื้นด้วยตัวเอง";
			mes "เท้าอีกครั้งเหรอ?";
			close;
		case 2:
			mes "[Soldier]";
			mes "ฉันเข้าใจแล้วโปรดถามฉัน";
			mes "ถ้าคุณเปลี่ยนใจ";
			close;
		}
	}
	close;
}

// Mado Gear Renter :: madogear
//============================================================
-	script	::mgm	-1,{
	mes "[Mado Gear Armorer]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "ยินดีต้อนรับ, Mechanic";
		mes "คุณต้องการเช่ารถเข็นหรือ";
		mes "ขี่ Mado Gear?";
		next;
		switch(select("เช่ารถเข็น:ขี่ Mado Gear:ซื้อ Emergency Magic Gear:อัพเกรดอุปกรณ์ระบายความร้อน:ยกเลิก")) {
		case 1:
			if (checkcart()) {
				mes "[Mado Gear Armorer]";
				mes "ฉันขอโทษ แต่คุณมีรถเข็นแล้ว";
				close;
			}
			setcart;
			mes "[Mado Gear Armorer]";
			mes "เจ้าไปแล้ว!";
			close;
		case 2:
			if (checkmadogear()) {
				mes "[Mado Gear Armorer]";
				mes "ฉันขอโทษ แต่คุณกำลังขี่ Mado Gear อยู่แล้ว";
				close;
			}
			else if (!getskilllv("NC_MADOLICENCE")) {
				mes "[Mado Gear Armorer]";
				mes "โปรดเรียนรู้ทักษะเพื่อรับ Mado Gear License ก่อน";
				close;
			}
			else if(ismounting()) {
				mes "[Mado Gear Armorer]";
				mes "กรุณาลงจากขี่สัตว์ของคุณออกก่อน";
				close;
			}
			setmadogear;
			mes "[Mado Gear Armorer]";
			mes "ขอให้สนุกและได้โปรดกลับมาอีกครั้ง!";
			close;
		case 3:
			mes "[Mado Gear Armorer]";
			mes "Emergency Magic Gear มีประโยชน์จริง ๆ สำหรับสถานการณ์ฉุกเฉินและขายที่ 1,000,000 Zeny";
			next;
			if (select("ซื้อ:ยกเลิก") == 2) {
				mes "[Mado Gear Armorer]";
				mes "ฉันเข้าใจแล้วโปรดถามฉัน";
				mes "ถ้าคุณเปลี่ยนใจ";
				close;
			}
			if (countitem(23277) > 0) {
				mes "[Mado Gear Armorer]";
				mes "ฉันขอโทษ แต่คุณมี Emergency Mado Gear อยู่แล้ว";
				close;
			}
			if (Zeny < 1000000) {
				mes "[Mado Gear Armorer]";
				mes "ฉันขอโทษ, แต่คุณไม่มี Zeny เพียงพอที่จะซื้อ Emergency Mado Gear";
				close;
			}
			Zeny -= 1000000;
			getitem 23277,1; //Mado_Box
			mes "[Mado Gear Armorer]";
			mes "เจ้าไปแล้ว!";
			close;
		case 4:
			mes "[Mado Gear Armorer]";
			mes "อุปกรณ์ใดที่คุณต้องการอัพเกรด";
			next;
			if (select("อุปกรณ์ทำความเย็น:เครื่องทำความเย็นคุณภาพสูง") == 1) {
				mes "[Mado Gear Armorer]";
				mes "การอัพเกรดอุปกรณ์ทำความเย็นเป็นอุปกรณ์ทำความเย็นคุณภาพสูงต้องใช้อุปกรณ์ระบายความร้อน 1 เครื่องและ 2,000,000 Zeny";
				next;
				.@itemid = 2804;
				.@cost = 2000000;
			} else {
				mes "[Mado Gear Armorer]";
				mes "การอัพเกรดตัวทำความเย็นคุณภาพสูงไปเป็นตัวทำความเย็นแบบพิเศษนั้นต้องใช้ตัวทำความเย็นคุณภาพสูง 1 ตัวและ Zeny 4,000,000 ตัว";
				next;
				.@itemid = 2809;
				.@cost = 4000000;
			}
			if (select("อัปเกรด:ยกเลิก") == 2) {
				mes "[Mado Gear Armorer]";
				mes "ฉันเข้าใจแล้วโปรดถามฉัน";
				mes "ถ้าคุณเปลี่ยนใจ";
				close;
			}
			if (!countitem(.@itemid)) {
				mes "[Mado Gear Armorer]";
				mes "ฉันขอโทษ แต่คุณไม่มี" + getitemname(.@itemid) + ".";
				close;
			}
			if (Zeny < .@cost) {
				mes "[Mado Gear Armorer]";
				mes "ฉันขอโทษ, แต่คุณมี Zeny ไม่เพียงพอที่จะอัพเกรดอุปกรณ์";
				close;
			}
			Zeny -= .@cost;
			delitem .@itemid,1;
			getitem (.@itemid == 2804 ? 2809 : 2810),1; //High_Quality_Cooler,Special_Cooler
			mes "[Mado Gear Armorer]";
			mes "นี่ไง! " + getitemname(.@itemid) + " ของเจ้า";
			close;
		case 5:
			close;
		}
	}
	mes "ฉันจะช่วยคุณได้อย่างไร";
	mes "Mado Gears มีให้เฉพาะกับ Mechanics";
	close;
}
prontera,163,178,3	duplicate(mgm)	Mado Gear Armorer#prt	105
geffen,103,55,5	duplicate(mgm)	Mado Gear Armorer#gef	105
payon,166,106,5	duplicate(mgm)	Mado Gear Armorer#pay	105
aldebaran,133,112,5	duplicate(mgm)	Mado Gear Armorer#alde	105
yuno,167,187,3	duplicate(mgm)	Mado Gear Armorer#yuno	105
rachel,106,134,5	duplicate(mgm)	Mado Gear Armorer#ra	105
dicastes01,187,207,3	duplicate(mgm)	Mado Gear Armorer#dic	105
manuk,273,212,5	duplicate(mgm)	Mado Gear Armorer#man	105
splendide,180,174,5	duplicate(mgm)	Mado Gear Armorer#spl	105
mid_camp,242,243,3	duplicate(mgm)	Mado Gear Armorer#mid	105
