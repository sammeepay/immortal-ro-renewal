//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","เรากำลังมุ่งหน้าไปยังเมือง Einbroch",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","เรากำลังจะถึงเมือง Einbroch ในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับเข้าสู่เมือง Einbroch ขอให้เดินทางปลอดภัย",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","ขณะนี้เรากำลังอยู่ที่เมือง Einbroch เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ขณะนี้เรือเหาะออกจากท่าแล้ว  สถานีต่อไปคือเมือง Lighthalzen",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","เรากำลังมุ่งหน้าไปยังเมือง Lighthalzen",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","เรากำลังจะถึงเมือง Lighthalzen ในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับเข้าสู่เมือง Lighthalzen ขอให้เดินทางปลอดภัย",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","ขณะนี้เรากำลังอยู่ที่เมือง Lighthalzen เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","เรากำลังมุ่งหน้าไปยังเมือง Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","เรากำลังจะถึงเมือง Einbroch ในไม่ช้า",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับเข้าสู่เมือง Einbroch ขอให้เดินทางปลอดภัย",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","ขณะนี้เรากำลังอยู่ที่เมือง Einbroch เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","เรากำลังมุ่งหน้าไปยังเมือง Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","เรากำลังจะถึงเมือง Juno ในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับเข้าสู่เมือง Juno ขอให้เดินทางปลอดภัย",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","ขณะนี้เรากำลังอยู่ที่เมือง Juno เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Hugel",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","เรากำลังมุ่งหน้าไปยังเมือง Hugel",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","เรากำลังจะถึงเมือง Hugel ในไม่ช้า",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับเข้าสู่เมือง Hugel ขอให้เดินทางปลอดภัย",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","ขณะนี้เรากำลังอยู่ที่เมือง Hugel เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","เรากำลังมุ่งหน้าไปยังเมือง Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","เรากำลังจะถึงเมือง Juno ในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับเข้าสู่เมือง Juno ขอให้เดินทางปลอดภัย",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","ขณะนี้เรากำลังอยู่ที่เมือง Juno เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Einbroch",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	Airship Crew#ein-1	852,{
	mes "[Airship Crew]";
	mes "ถ้าพวกเราได้ลงจอด";
	mes "ที่ปลายทางของคุณ";
	mes "และคุณต้องการออกจาก";
	mes "เรือเหาะ กรุณาใช้บันได";
	mes "ทางด้านหน้า ขอบคุณ";
	mes "สำหรับการใช้บริการค่ะ";
	close;
}

airplane,64,94,1	script	Umbala Kid#ein_p	787,{
	emotion e_swt2;
	mes "[Kid]";
	if (event_umbala >= 3) {
		mes "ว้าววว  แม่!";
		mes "ดู นี้ ซิ!";
		mes "พวกเรา กำลัง บินนน";
		mes "บิน อยู่ บน ท้องฟ้าาา!!!";
	}
	else {
		mes "มาคูมาลากู!";
		mes "แซมปา โจจิมโบ";
		mes "คาคู นา เจดิ โซโล";
		mes "ฮ่าๆๆๆๆๆๆๆๆ!";
	}
	close;
}

airplane,66,93,3	script	Umbala Lady#ein_p	783,{
	emotion e_dots;
	mes "[Lady]";
	if (event_umbala >= 3) {
		mes "จุ๊ๆๆๆ...";
		mes "ลูกรัก เราไม่ควร";
		mes "แสดงท่าทางตื่นเต้น";
		mes "ขนาดนั้น เวลาเรา";
		mes "อยู่ในที่สาธารณะนะจ้ะ!";
	}
	else {
		mes "ชุคทู !";
		mes "สะคราอัพ แมทท!";
		mes "ชาคา กรุฟทาฟิ";
		mes "ฮันทิกิ แมนโจจ!";
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "เฮ้ สวัสดี~";
		mes "ดูจากหน้าคุณแล้ว";
		mes "ฉันคิดว่าคุณต้องเข้าใจ";
		mes "ฉันแน่ๆ ^333333*Whew...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "ฉันได้รับการช่วยเหลือจาก";
		mes "บริษัท Airship โดยมี";
		mes "กะลาสี Airship คอยฝึกอบรม";
		mes "เพื่อให้ฉันเอาชนะโรคกลัวความสูง";
		mes "บางที พวกเขาอาจผ่านการกระโดดบันจี้จั้มมาแล้วใช่ไหม ?";
		next;
		mes "[Chrmlim]";
		mes "แต่...";
		mes "บางทีพวกเค้าก็เอาชนะ";
		mes "โรคกลัวความสูงไม่ได้";
		mes "และอาจจะจบลงที่เมือง ^333333Nifflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "บาจู กา";
		mes "นุคกา Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "ชาบาลา มูว บา จามา";
		mes "Airship คูลาฮา กูกูนา ";
		mes "ซาลู. ดามา, คูคู นา นู";
		mes "ยูคูทา. อืม, ฟาชูคู นา เร็ด!";
	}
	close;
}

airplane,250,58,2	script	Airship Staff#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Airship Staff]";
		mes "ยินดีต้อนรับ";
		mes "เข้าสู่ Airship.";
		mes "ต้องการให้ฉันช่วยเหลืออะไรคะ";
		next;
		select("คุณรู้ไหมว่าผู้โดยสารคนหนึ่งชื่อ Thierry?");
		mes "[Airship Staff]";
		mes "ฉันขอโทษ, แต่ฉันไม่คิดว่าเรามีผู้โดยสารชื่อนี้";
		close;
	}
	// Hugel quest end
	mes "[Airship Staff]";
	mes "ยินดีต้อนรับ";
	mes "เข้าสู่ เรือเหาะ";
	mes "ต้องการให้ฉันช่วยเหลืออะไรคะ";
	next;
	switch (select("การใช้ เรือเหาะ:ห้องกัปตัน:สิ่งอำนวยความสะดวก:ยกเลิก")) {
	case 1:
		mes "[Airship Staff]";
		mes "เมื่อคุณเห็นประกาศว่าเราได้มาถึง";
		mes "จุดหมายปลายทางของคุณ";
		mes "โปรดให้ใช้ทางออกที่อยู่";
		mes "ด้านทิศเหนือและทิศใต้ของเรือเหาะ";
		next;
		mes "[Airship Staff]";
		mes "หากคุณพลาดจุดหมายปลายทาง";
		mes "ไม่ต้องเป็นห่วงเพราะเรือเหาะ";
		mes "จะบินวนกลับมาเส้นทางเดิม";
		mes "และกลับมายังจุดหมายของคุณอีกครั้ง";
		close;
	case 2:
		mes "[Airship Staff]";
		mes "ห้องโดยสารของกัปตัน";
		mes "ตั้งอยู่ที่ด้านหน้าของเรือเหาะ";
		mes "คุณสามารถพบกับกัปตันและนักบิน";
		mes "ของเรือเหาะได้ที่นั้น";
		close;
	case 3:
		mes "[Airship Staff]";
		mes "เรือเหาะมีมินิเกมต่างๆ";
		mes "เพื่อความบันเทิงของผู้โดยสารของเรา";
		mes "เราขอเชิญคุณเพื่อทดสอบโชค";
		mes "และทักษะของคุณใน  Airship's Mini Games~";
		close;
	case 4:
		mes "[Airship Staff]";
		mes "ฉันหวังว่าคูณจะกลับมา";
		mes "ใช้บริการเรือเหาะของเราอีก";
		mes "ขอบคุณที่ใช้บริการค่ะ";
		mes "ขอให้วันนี้เป็นวันที่ดีสำหรับคุณ";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "โอ้..  สวัสดีนักผจญภัย";
	mes "ฉันอยู่ในการเดินทางอันศักดิ์สิทธิ์";
	mes "เพื่อสวดอธิษฐานให้แก่";
	mes "ทวีป Rune-Midgard";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "อืม.. เรือเหาะลำนี้จะไปยังเมือง Lutie";
	mes "หรือไม่นะ.. ฉันรอมานานมาก";
	mes "แต่ฉันยังไม่เห็นประกาศถึงเมือง Lutie เลย";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "โอ้.. ว้าววว";
	mes "ท้องฟ้าช่างดูสวยงามมากๆ";
	mes "จากตรงนั้น *";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "ฉันหวังว่าฉันจะได้ดื่มเบียร์สดเย็นๆ";
		mes "การดื่มเป็นหนึ่งในเป้าหมายของชีวิตของฉัน! ดื่มเพื่อพลังงาน!!!!";
		mes "ฉันทำอะไรไม่ได้ถ้าไม่ได้ดื่ม";
		next;
		mes "[Pilot]";
		mes "แต่! การดื่มสุราทำให้ความสามารถในการขับขี่...ลดลง";
		mes "แต่นั่น! กลับทำให้ฉันอยากดื่มมากขึ้นไปอีกฮ่าๆ";
		emotion e_sob;
		next;
		select("คุณรู้ไหมว่าผู้โดยสารคนหนึ่งชื่อ Thierry?");
		mes "[Pilot]";
		mes "ชุดเครื่องแบบนี่ดูเรียบร้อยจริงๆ";
		mes "แต่มันหนาเกินไปที่จะใส่";
		mes "เดินรอบๆเรือเหาะ";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "ไม่เคยมีใครเข้ามาในห้องนี้มาก่อน";
		mes "และกัปตันก้เป็นกวางเรนเดีย";
		mes "ฉันเพิ่งจะถอดบ้อกเซอร์ออกเมื่อกี้";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "เอ่อ!? เขาคือใคร!";
		next;
		mes "- ...เขาไม่ได้ฟังที่คุณพูดเลย... -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "มันนานมากแล้ว";
		mes "ที่ฉันได้มีความสุขจากการดื่ม";
		mes "แต่! งานของฉันนั้นต้องการสติ";
		next;
		mes "[Pilot]";
		mes "ถึงฉันดื่มฉันก็มีความรับผิดชอบ!";
		mes "อา.. ฉันคิดถึงวันหยุด ที่มีเหล้า มีขนม มี TV";
		mes "แต่ถึงอย่างนั้น!  ฉันก็จำไม่ได้ว่าฉันหยุดครั้งสุดท้ายเมื่อไหร่";
		emotion e_sob;
		close;
	case 2:
		mes "[Pilot]";
		mes "เฮ้เพื่อน, วันนี้อากาศดีจริงๆ";
		mes "ท้องฟ้าสดใสมองเห็นทุกอย่างเลย";
		mes "มันดูปลอดถัยมาก ไม่ต้องเป็นห่วงนะ";
		close;
	case 3:
		mes "[Pilot]";
		mes "คุณรู้ไหม ว่ากัปตันเป็นคนที่มีเกียรติ";
		mes "เขาและพี่ชายของเขาเป็นที่รู้จักในวงการ";
		mes "ของนักบิน  ใครจะรู้ว่ากวางเรนเดียเป็นกัปตันที่ดีได้?";
		next;
		mes "[Pilot]";
		mes ".....รู้กันแค่เรานะ.....";
		mes "ฉันอยากจะบอกว่าซานต้าเค้าทำยังไงนะ";
		mes "ถึงได้กวางเรนเดียและเอลฟ์มาทำงานให้เค้า";
		mes "ฉันว่าชายคนนั้นจะต้องเป็นอัจฉริยะแน่ๆ";
		close;
	default:
		mes "[Pilot]";
		mes "คุณรู้ไหม! ว่าการบังคับ ";
		mes "เรือเหาะนั้นเป็นเรื่องใหม่";
		mes "พวกเขาทำมันขึ้นมาด้วยความรีบร้อน";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "แต่ถึงอย่างนั้นพวกเขา";
		mes "ก็ทำมันอย่างจริงจังคิดหน้าคิดหลัง";
		mes "ฉันหมายถึงพวกเขามีการฝึกอบรม";
		mes "พวกเราพร้อมๆไปกับการสร้างเรือเหาะ";
		mes "คิดดูสินี้มันบ้าชัดๆ";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[Fruitz]";
	mes "ยินดีต้อนรับสู่ Fruitz's";
	mes "ร้านค้าที่คุณสามารถซื้อแอปเปิ้ล";
	mes "หรือคุณจะปั้นมันเพื่อทำเป็นน้ำแอปเปิ้ล";
	next;
	switch (select("ซื้อ แอปเปิ้ล:ทำน้ำ แอปเปิ้ล:ทำไมนายถึงอยู่ที่นี่?:ยกเลิก.")) {
	case 1:
		mes "[Fruitz]";
		mes "กรุณาใส่จำนวนของแอปเปิ้ลที่คุณต้องการที่จะซื้อ";
		mes "แอปเปิ้ลแต่ละลูกราคา 15 Zeny";
		mes "และคุณสามารถซื้อได้สูงสุด 500 ลูก";
		mes "ในแต่ละครั้ง กรุณาใส่ '0' เพื่อยกเลิกคำสั่งซื้อ";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "ขอบคุณสำหรับการใช้บริการ";
				mes "ของเรา ลาก่อน!";
				mes "คุณสามารถใช้บริการของเราได้เสมอ";
				mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
				mes "เป็นอาหารว่าง";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "คุณใส่จำนวนของแอปเปิ้ล";
				mes "มากเกินกว่า 500 ลูก";
				mes "กรุณาใส่จำนวนใหม่...";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "คุณต้องการแอปเปิ้ล ^FF0000" + .@input + "^000000 ลูก";
				mes "เป็นจำนวนเงิน ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "รายการถูกต้องหรือไม่?";
				next;
				if (select("ใช่:ไม่ใช่") == 2) {
					mes "[Fruitz]";
					mes "ขอบคุณสำหรับการใช้บริการ";
					mes "ของเรา ลาก่อน!";
					mes "คุณสามารถใช้บริการของเราได้เสมอ";
					mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
					mes "เป็นอาหารว่าง";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "ฉันขอโทษ! แต่คุณมีจำนวน Zeny ไม่พอ";
			mes "ที่จะซื้อแอปเปิ้ลในจำนวนที่ต้องการ";
			mes "กรุณาเช็คจำนวน Zeny";
			mes "หรือซื้อแอปเปิ้ลในจำนวนที่น้อยลง";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "อืมม! ฉันคิดว่าคุณมี";
			mes "ที่ว่างในช่องเก็บของไม่เพียงพอ";
			mes "สำหรับแอปเปิ้ลที่คุณจะซื้อ";
			mes "ทำไมไม่เคลียร์ช่องเก็บของของคุณก่อนล่ะ?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "ขอบคุณสำหรับการใช้บริการ";
			mes "ฉันหวังว่าคุณจะชอบรสชาติ";
			mes "แอปเปิ้ลของเรานะ~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "โอเค! ฉันต้องการ";
		mes "^FF0000แอปเปิ้ล 3 ลูก และ 1 Empty Bottle^000000";
		mes "เพื่อที่จะทำน้ำแอปเปิ้ลให้คุณ";
		mes "คุณต้องการที่จะดำเนินการต่อหรือไม่?";
		next;
		switch (select("ใช่:ไม่ต้องการ")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "ขอโทษนะ! แต่คุณมี";
				mes "ของไม่เพียงพอในการทำน้ำแอปเปิ้ล";
				mes "จำไม่ว่าฉันต้องการ แอปเปิ้ล 3 ลูก ";
				mes "และ 1 Empty Bottleในการทำ";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "ขอบคุณ";
				mes "โปรดรอซักครู่";
				next;
				mes "^3355FF*Grind grind*";
				mes "*Grind grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Fruitz]";
				mes "เรียบร้อยแล้ว~";
				mes "ฉันหวังว่าคุณจะมีความสุข";
				mes "คุณสามารถยกเลิกการทำน้ำแอปเปิ้ล";
				mes "ได้ตลอดเวลานะ นักผจญภัย ~";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "ขอบคุณสำหรับการใช้บริการ";
			mes "ของเรา ลาก่อน!";
			mes "คุณสามารถใช้บริการของเราได้เสมอ";
			mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
			mes "เป็นอาหารว่าง";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "ฉันเคยเป็นคนเร่ร่อนมาก่อน";
		mes "วันหนึ่งฉันนอนหลับ,";
		mes "และมีบางสิ่งตกใส่หัวฉัน";
		mes "ทำให้ฉันตกใจตื่นขึ้น";
		next;
		mes "[Fruitz]";
		mes "มันกลับกลายเป็นว่าฉันกำลังนอนหลับ";
		mes "อยู่ใต้ต้นแอปเปิ้ลและลูกแอปเปิ้ล";
		mes "ตกใส่หัวของฉัน ฉันกำลังจะตายด้วยความหิว";
		mes "ฉันคิดว่าจะกินแอปเปิ้ลลูกนั้น";
		next;
		mes "[Fruitz]";
		mes "แต่จู่ๆ!  Kain, เพื่อนเก่าของฉัน";
		mes "ที่เคยทำงานในเหมืองด้วยกัน";
		mes "ก็พบฉันแล้วบอกให้ฉันมาช่วยเค้า";
		mes "บนเรือเหาะ  ฉันจึงได้มาที่นี้";
		mes "และพบผู้คนมากมายที่มาเล่น";
		mes "เกมส์ ลูกเต๋า";
		next;
		mes "[Fruitz]";
		mes "แต่ด้วยความเบื่อและความ";
		mes "อยากรู้อยากเห็นของฉัน";
		mes "ทำให้ฉันเข้าไปเดิมพันเกมส์ด้วยแอปเปิ้ล";
		mes "ลูกนั้น แต่ด้วยความโชคดี";
		mes "ทำให้จากแอปเปิ้ลลูกเดียว";
		mes "กลายเป็นแอปเปิ้ล2ลูก... ";
		next;
		mes "[Fruitz]";
		mes "และจาก 2 ลูก กลายเป็น 4 ลูก";
		mes "ไปเรื่อยๆ รู้ตัวอีกทีก็มีแอปเปิ้ลมากมายแล้ว";
		mes "ดังนั้นฉันจึงเริ่มต้นธุรกิจของตัวเอง!";
		mes "ที่นี้ที่เรือเหาะลำนี้  แปลกดีไหมล่ะ?";
		next;
		mes "[Fruitz]";
		mes "ดังนั้นแอปเปิ้ลเป็นสิ่งที่ดี";
		mes "สำหรับคุณ และดีมากกก";
		mes "สำหรับฉัน ฮ่าๆๆๆๆ";
		close;
	case 4:
		mes "[Fruitz]";
		mes "ขอบคุณที่ใช้บริการ";
		mes "ลาก่อน";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","เรากำลังมุ่งหน้าไปยังเมือง Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","เรากำลังจะถึงเมือง Izlude ในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับเข้าสู่เมือง Izlude ขอให้เดินทางปลอดภัย",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","ขณะนี้เรากำลังอยู่ที่เมือง Izlude เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Juno",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","เรากำลังมุ่งหน้าไปยังเมือง Juno",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","เรากำลังจะถึงเมือง Juno ในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับเข้าสู่เมือง Juno ขอให้เดินทางปลอดภัย",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","ขณะนี้เรากำลังอยู่ที่เมือง Juno เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Rachel",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","เรากำลังมุ่งหน้าไปยังเมือง Rachel",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","เรากำลังจะถึงเมือง Rachel ในไม่ช้า",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับเข้าสู่เมือง Rachel ขอให้เดินทางปลอดภัย",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","ขณะนี้เรากำลังอยู่ที่เมือง Rachel เรือเหาะของเรากำลังจะออกในไม่ช้า",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","ขณะนี้เรือเหาะออกจากท่าแล้ว สถานีต่อไปคือเมือง Izlude",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	Airship Staff#airplane01	67,{
	mes "[Airship Staff]";
	mes "ยินดีต้อนรับ";
	mes "เข้าสู่ เรือเหาะ";
	mes "ต้องการให้ฉันช่วยเหลืออะไรคะ";
	next;
	switch (select("การใช้ เรือเหาะ:ห้องกัปตัน:สิ่งอำนวยความสะดวก:ยกเลิก")) {
	case 1:
		mes "[Airship Staff]";
		mes "เมื่อคุณเห็นประกาศว่าเราได้มาถึง";
		mes "จุดหมายปลายทางของคุณ";
		mes "โปรดให้ใช้ทางออกที่อยู่";
		mes "ด้านทิศเหนือและทิศใต้ของเรือเหาะ";
		next;
		mes "[Airship Staff]";
		mes "หากคุณพลาดจุดหมายปลายทาง";
		mes "ไม่ต้องเป็นห่วงเพราะเรือเหาะ";
		mes "จะบินวนกลับมาเส้นทางเดิม";
		mes "และกลับมายังจุดหมายของคุณอีกครั้ง";
		close;
	case 2:
		mes "[Airship Staff]";
		mes "ห้องโดยสารของกัปตัน";
		mes "ตั้งอยู่ที่ด้านหน้าของเรือเหาะ";
		mes "คุณสามารถพบกับกัปตันและนักบิน";
		mes "ของเรือเหาะได้ที่นั้น";
		close;
	case 3:
		mes "[Airship Staff]";
		mes "เรือเหาะมีมินิเกมต่างๆ";
		mes "เพื่อความบันเทิงของผู้โดยสารของเรา";
		mes "เราขอเชิญคุณเพื่อทดสอบโชค";
		mes "และทักษะของคุณใน  Airship's Mini Games~";
		close;
	case 4:
		mes "[Airship Staff]";
		mes "ฉันหวังว่าคูณจะกลับมา";
		mes "ใช้บริการเรือเหาะของเราอีก";
		mes "ขอบคุณที่ใช้บริการค่ะ";
		mes "ขอให้วันนี้เป็นวันที่ดีสำหรับคุณ";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- กรุณารอซักครู่ !! -";
		mes "- ขณะนี้คุณแบกน้ำหนักมากเกินไป -";
		mes "- กรุณากลับมาอีกครั้ง -";
		mes "- หลังจากลดน้ำหนักบางส่วนแล้ว -";
		close;
	}
	mes "[Meltz]";
	mes "ยินดีต้อนรับสู่ Meltz's";
	mes "ร้านค้าที่คุณสามารถซื้อแอปเปิ้ล";
	mes "หรือคุณจะปั้นมันเพื่อทำเป็น";
	mes "น้ำแอปเปิ้ล";
	next;
	switch (select("ซื้อ แอปเปิ้ล:ทำน้ำ แอปเปิ้ล:ยกเลิก")) {
	case 1:
		mes "[Meltz]";
		mes "กรุณาใส่จำนวนของแอปเปิ้ลที่";
		mes "คุณต้องการที่จะซื้อ";
		mes "แอปเปิ้ลแต่ละลูกราคา 15 Zeny";
		mes "และคุณสามารถซื้อได้สูงสุด 500 ลูก";
		mes "ในแต่ละครั้ง กรุณาใส่ '0' เพื่อยกเลิกคำสั่งซื้อ";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "ขอบคุณสำหรับการใช้บริการ";
				mes "ของเรา ลาก่อน!";
				mes "คุณสามารถใช้บริการของเราได้เสมอ";
				mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
				mes "เป็นอาหารว่าง";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "คุณใส่จำนวนของแอปเปิ้ล";
				mes "มากเกินกว่า 500 ลูก";
				mes "กรุณาใส่จำนวนใหม่...";
				next;
			}
			else {
				mes "[Meltz]";
				mes "คุณต้องการแอปเปิ้ล ^FF0000" + .@input + "^000000 ลูก";
				mes "เป็นจำนวนเงิน ^FF0000" + .@pay + " Zeny^000000 zeny";
				mes "รายการถูกต้องหรือไม่?";
				next;
				if (select("ใช่:ไม่ใช่") == 2) {
					mes "[Meltz]";
					mes "ขอบคุณสำหรับการใช้บริการ";
					mes "ของเรา ลาก่อน!";
					mes "คุณสามารถใช้บริการของเราได้เสมอ";
					mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
					mes "เป็นอาหารว่าง";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "ฉันขอโทษ! แต่คุณมีจำนวน Zeny ไม่พอ";
			mes "ที่จะซื้อแอปเปิ้ลในจำนวนที่ต้องการ";
			mes "กรุณาเช็คจำนวน Zeny";
			mes "หรือซื้อแอปเปิ้ลในจำนวนที่น้อยลง";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "อืมม! ฉันคิดว่าคุณมี";
			mes "ที่ว่างในช่องเก็บของไม่เพียงพอ";
			mes "สำหรับแอปเปิ้ลที่คุณจะซื้อ";
			mes "ทำไมไม่เคลียร์ช่องเก็บของของคุณก่อนล่ะ?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "ขอบคุณสำหรับการใช้บริการ";
			mes "ฉันหวังว่าคุณจะชอบรสชาติ";
			mes "แอปเปิ้ลของเรานะ~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "โอเค! ฉันต้องการ";
		mes "^FF0000แอปเปิ้ล 3 ลูก และ 1 Empty Bottle^000000";
		mes "เพื่อที่จะทำน้ำแอปเปิ้ลให้คุณ";
		mes "คุณต้องการที่จะดำเนินการต่อหรือไม่?";
		next;
		switch (select("Yes:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "ขอโทษนะ! แต่คุณมี";
				mes "ของไม่เพียงพอในการทำน้ำแอปเปิ้ล";
				mes "จำไม่ว่าฉันต้องการ แอปเปิ้ล 3 ลูก ";
				mes "และ 1 Empty Bottleในการทำ";
				close;
			}
			else {
				mes "[Meltz]";
				mes "ขอบคุณ";
				mes "โปรดรอซักครู่";
				next;
				mes "^3355FF*Grind grind*";
				mes "*Grind grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Fruitz]";
				mes "เรียบร้อยแล้ว~";
				mes "ฉันหวังว่าคุณจะมีความสุข";
				mes "คุณสามารถยกเลิกการทำน้ำแอปเปิ้ล";
				mes "ได้ตลอดเวลานะ นักผจญภัย ~";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "ขอบคุณสำหรับการใช้บริการ";
			mes "ของเรา ลาก่อน!";
			mes "คุณสามารถใช้บริการของเราได้เสมอ";
			mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
			mes "เป็นอาหารว่าง";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "ขอบคุณสำหรับการใช้บริการ";
		mes "ของเรา ลาก่อน!";
		mes "คุณสามารถใช้บริการของเราได้เสมอ";
		mes "เมื่อคุณรู้สึกว่าต้องการ แอปเปิ้ล";
		mes "เป็นอาหารว่าง";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "ลองจิจูด 131 องศาตะวันออก";
		mes "ละติจูด 37 องศาเหนือ";
		mes "พวกเรากำลังไปทางขวาครับ กัปตัน";
		close;
	case 2:
		mes "[Pilot]";
		mes "ดูเหมือนจะเป็นวันที่มีเมฆจริงๆ";
		mes "มันยากที่จะนำทางเมื่อท้องฟ้าเป็นแบบนี้";
		mes "ฉันคิดว่าเราจะต้องใช้เรดาร์";
		close;
	case 3:
		mes "[Pilot]";
		mes "กัปตันเป็นคนดีนะ";
		mes "และเขาออกคำสั่งได้ดี";
		mes "แต่บางทีก็เคร่งเกินไป";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000เฮ้....!^000000";
		mes "^ff0000คุย ให้น้อยหน่อย^000000";
		mes "^ff0000แล้วตั้งใจขับให้มากขึ้น!^000000";
		next;
		mes "[Pilot]";
		mes "ครับผม กัปตัน...!";
		mes "(เห็นไหมละ..?)";
		close;
	default:
		mes "[Pilot]";
		mes "ชุดเครื่องแบบนี่ดูเรียบร้อยจริงๆ";
		mes "แต่มันหนาเกินไปที่จะใส่";
		mes "เดินรอบๆเรือเหาะ";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "ไม่เคยมีใครเข้ามาในห้องนี้มาก่อน";
		mes "และกัปตันก้เป็นกวางเรนเดีย";
		mes "ฉันเพิ่งจะถอดบ้อกเซอร์ออกเมื่อกี้";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "โอ้...! สวัสดี!";
		mes "รู้สึกสนุกไปกับการนั้งเรือเหาะไหม?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "มันแปลกมาก!!";
	mes "ฉันไปเยี่ยมชมห้องกัปตันและ";
	mes "และทั่วทั้งเรือเหาะสิ่งที่ฉันเห็น";
	mes "ก็คือมีกวางเรนเดียเป็นกัปตัน";
	mes "โอ้!  คุณว่าแปลกไหมละ?";
	close;

OnTouch_:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "ฉันคาดหวังว่าจะได้กินอาหารที่ดี";
	mes "ในเที่ยวบินที่มีคุณภาพระดับสามดาว";
	mes "อืมมมมมม...........";
	mes "ฉันว่าฉันหน้าจะเอาพ่อครัวของฉัน";
	mes "มาเอง เพื่อที่ฉันจะได้เพลิดเพลินกับ";
	mes "อาหารจริงๆ";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Swordsman Shimizu]";
	mes "เอาล่ะ จากที่รอมา 5 ปี";
	mes "ในวันนี้ฉันก็ได้กลับไป";
	mes "แก้แค้น...!";
	next;
	mes "[Swordsman Shimizu]";
	mes "ฉันเพียงแค่ ...";
	mes "ต้องให้แน่ใจว่าฉันจะไม่พลาด";
	mes "ใกล้ถึงเวลาแล้ว... อีกนิดเดียว...";
	mes "ฉันกำลังจะได้แก้แค้น...!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "ยินดีต้อนรับ เข้าสู่";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "คุณต้องการเล่นเกมส์";
	mes "แข่งพิมพ์เร็วไหม?";
	next;
	switch (select("เล่น ^ff0000RO Typing Challenge^000000:ดูข้อมูลเกมส์:ดูคะแนนสูงสุด:ยกเลิก")) {
	case 1:
		mes "[Nils]";
		mes "เอาล่ะเรามีผู้ท้าชิงใหม่";
		mes "กรุณาใส่ข้อความต่อไปนี้!";
		mes "ให้เร็วที่สุด และห้ามผิดพลาด";
		mes "Let's start~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Your record is ^ff0000" + .@total_time + " seconds^000000 and";
			mes "the total letters are " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "อืมมม.. เร็วแบบนี้มันเป็นไปไม่ได้";
				mes "คนธรรมดาไม่สามารถเร็วได้ขนาดนี้หรอก";
				mes "คุณ ก้อปปี้ แล้ว วาง ใช่ไหม";
				mes "กรุณาเล่นใหม่อย่างสุจริต";
				mes "อีกครั้ง ....";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "บันทึกด้านบนก่อนหน้านี้";
				mes "เป็นของ ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "มีตัวหนังสือทั้งหมด ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "แต่คุณทำได้ ^ff0000" + strcharinfo(0) + "^000000,";
				mes "นี้เป็นสถิติใหม่่ของคุณ";
				mes "ขอแสดงความยินดีด้วย!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "เป็นผู้ลงทำเบียนล่าสุด";
				mes "มีจำนวนอักษร ^0000ff" + $050320_ein_typing + "^000000";
				mes "พยายามต่อไปนะ";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "โอ้....";
			mes "ขอโทษทีแต่";
			mes "คุณพิมพ์อักษรไม่ถูกต้อง";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "^ff0000RO Typing Challenge^000000";
		mes "เป็นเกมส์แข่งพิมพ์เร็ว";
		mes "ชื่อของผู้เล่นชั้นนำจะถูกบันทึกไว้";
		mes "ให้ลูกหลานดู หากคุณ";
		mes "ต้องการชื่อเสียงละก็นี้คือโอกาศของคุณ";
		next;
		mes "[Nils]";
		mes "ฉันต้องการเพียงเพื่อให้คุณรู้ว่า";
		mes "คุณพิมพ์ข้อความทั้งหมดที่คุณเห็น";
		mes "อยู่ในบรรทัดเดียวกัน เพราะฉะนั้น";
		mes "ไม่ต้องกดปุ่ม Enter แค่";
		mes "เพียงคลิกปุ่ม ok ก็พอแล้ว";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "เป็นผู้ลงทำเบียนล่าสุด";
		mes "มีจำนวนอักษร ^0000ff" + $050320_ein_typing + "^000000";
		mes "พยายามต่อไปนะ";
		close;
	case 4:
		mes "[Nils]";
		mes "ฉันรู้สึกมีอิสระที่ได้เล่น";
		mes "^ff0000RO Typing Challenge^000000";
		mes "ตลอดเวลา! ฉันจะอยู่ที่นี้และเล่นเกมส์นี้!!!!!";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "สวัสดี ฉันชื่อ Clarice~";
	mes "คุฯต้องการที่จะใช้";
	mes "แอปเปิ้ล ในการเล่นเกม";
	mes "ลูกเต๋าไหม?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("เล่นเกม ลูกเต๋า:รายละเอียดเกมส์ลูกเต๋า:ยกเลิก")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "ฉันมาที่นี้เพื่อเล่นเกมส์นี้";
		mes "ถ้าคุณรู้สึกเหมือนฉัน";
		mes "คุณสามารถคุยกับฉันเกี่ยวกับ";
		mes "เกมส์ได้....";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "กฎสำหรับเกมลูกเต๋า";
		mes "ก็ง่ายนิดเดียว คือคุณต้อง";
		mes "วางเดิมพันด้วยแอปเปิ้ล";
		mes "สูงสุดครั้งละ 50 ลูก";
		mes "เพื่อไม่ให้ผิดกฎหมาย";
		mes "เราจึงให้ใช้ได้แต่แอปเปิ้ล";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "แต่เดี๋ยวถ้าคุณอยากเผาเงิน";
		mes "ในกระเป๋าละก็คูณสามารถ";
		mes "เดินไปที่ร้านค้าเพื่อซื้อ";
		mes "แอปเปิ้ลได้มากเท่าที่คุณต้องการ";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เริ่มต้นด้วยการที่ฉัน";
		mes "ทอยลูกเต๋า 2 ลูก";
		mes "และจะถึงตาคุณทอยลูกเต๋า";
		mes "แล้วจะมีตัวเลือกเพื่อทอยอีกครั้ง";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "โอเคและนี้คือสิ่งที่สำคัญ";
		mes "ถ้าคุณได้แต้มเกิน 12 แต้ม";
		mes "คุณจะแพ้ แต่ถ้าไม่ถึง 12 แต้ม";
		mes "ใครที่มีแต้มเยอะกว่าจะชนะ";
		mes "เข้าใจใช่ไหม?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เมื่อคุณทอยเต๋าครั้งแรกแล้ว";
		mes "ตัวเลือกทอยครั้งที่ 2 คุณ";
		mes "สามารถจะทอยอีกหรือไม่ก็ได้";
		mes "ถ้าคุณพอใจในแต้มแล้ว";
		mes "ฉันก็สามารถทอยหรือไม่ก็ได้";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เมื่อคุณชนะคุณจะได้รับ";
		mes "แอปเปิ้ลเป็นสองเท่า";
		mes "แต่ถ้าบังเอิญแต้มของเราเท่ากัน";
		mes "คุณจะได้รับแอปเปิ้ลเท่ากับ";
		mes "ที่คุณลงเดิมพันไว้";
		mes "ยุติธรรมใช่มั้ย?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "โอ้ดีเลย!! คุณต้องการเล่นกับฉันใช่ไหม";
	mes "คุณต้องการเดิมพันด้วยแอปเปิ้ล";
	mes "กี่ลูกละจำไว้ว่าคุณสามารถเดิมพัน";
	mes "ได้ครั้งละไม่เกิน 50 ลูกนะ";
	mes "ถ้าต้องการยกเลิกให้ใส่ '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "คุณยังไม่อยากเล่นหรอ?";
			mes "ฉันเข้าใจ ";
			mes "แต่ฉันหวังว่าจะได้เกมกับคุณสักครั้ง";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "คุณไม่สามารถเดิมพันมากกว่า";
			mes "แอปเปิ้ล 50 ลูก โปรดจำไว้ว่า";
			mes "เราจำเป็นต้องให้เดิมพันเหล่านี้";
			mes "เหมาะสม โปรดใส่เดิมพันใหม่";
			mes "ไม่เกิน 50 ลูก";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "คุณต้องการที่จะ";
		mes "เดิมพันด้วยแอปเปิ้ล ^FF0000"+.@amount+"^000000 ลูก";
		mes "ใช่ไหม?";
		next;
		if (select("ใช่:ไม่") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "อืม.. คุณทำอะไรผิดหรือเปล่า?";
			mes "กรุณาใส่จำนวนของแอปเปิ้ล";
			mes "ที่จะใช้ในการเดิมพัน";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "ฉันขอโทษ แต่คุณ";
			mes "มีแอปเปิ้ลไม่เพียงพอ";
			mes "สำหรับการวางเดิมพัน";
			mes "ถ้าคุณต้องการเล่นให้หา";
			mes "แอปเปิ้ลมาก่อนนะ";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "ดีมาก!";
		mes "ตอนนี้ถึงเวลาที่ฉัน";
		mes "จะทอยลูกเต๋าก่อนนะ";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "ฉันได้  ^0000FF" + .@table1 + "^000000 และ ^0000FF" + .@table2 + "^000000";
	mes "แต้มรวมเท่ากับ ^0000FF" + .@tablesub + "^000000";
	mes "^FF0000" + strcharinfo(0) + "^000000, โอเคถึงตาคุณแล้ว";
	next;
	select("ทอยเต๋า");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player1 + "^000000 และ ^FF0000" + .@player2 + "^000000 คะแนนรวม ^FF0000" + .@playersub + "^000000";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "แต้มทั้งหมดของฉันคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000, แต้มของคุณคือ ^FF0000" + .@playersub + "^000000 แต้มของเราเท่ากัน  คุณต้องการทอยเต๋าอีกครั้งหรือไม่?";
	} else if (.@playersub > .@tablesub) {
		mes "แต้มทั้งหมดของฉันคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000, แต้มของคุณคือ ^FF0000" + .@playersub + "^000000 ^FF0000" + strcharinfo(0) + "^000000, แต้มของคุณนำอยู่ คุณต้องการทอยเต๋าอีกครั้งหรือไม่?";
	} else if(.@tablesub > .@playersub) {
		mes "แต้มทั้งหมดของฉันคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000, แต้มของคุณคือ ^FF0000" + .@playersub + "^000000 แต้มของฉันนำอยู่ คุณต้องการทอยเต๋าอีกครั้งหรือไม่?";
	}
	next;
	switch (select("ทอยเต๋า:ไม่ทอย.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player3 + "^000000 และแต้มของคุณคือ ^FF0000" + .@playertotal + "^000000 คุณแพ้ !!! อิอิ คุณอยากเล่นอีกครั้งไหมล่ะ";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player3 + "^000000 และแต้มของคุณคือ ^FF0000" + .@playertotal + "^000000 ถึงคุณจะทอยลูกเต๋าอีกครั้งแต่แต้มยังขาดอยู่นิดหน่อยนะ คุณแพ้ !!! อิอิ คุณอยากเล่นอีกครั้งไหมล่ะ";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้  ^FF0000" + .@player3 + "^000000 และแต้มของคุณคือ ^FF0000" + .@playertotal + "^000000 ดูเหมือนว่าแต้มของเราจะเสมอกันนะ คุณอยากเล่นอีกครั้งไหมล่ะ";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้  ^FF0000" + .@player3 + "^000000 และแต้มของคุณคือ ^FF0000" + .@playertotal + "^000000 โอเคถึงตาฉันทอยนะ";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "ฉันเห็นว่าคุณไม่อยากทอยเต๋าต่อเพราะกลัวแต้มเกิน  งั้นฉันจะทอยอีกครั้งนะ";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "ฉันเห็นว่าคุณไม่อยากทอยเต๋าต่อเพราะกลัวแต้มเกิน งั้นฉันขอหยุดเกมส์ด้วยคะแนนเสมอกันนะ คุณอยากเล่นอีกครั้งไหมล่ะ";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "เอาล่ะ";
			mes "ถึงตาฉันทอยแล้ว";
		}
		else {
			mes "อืม..มันคุ้มที่จะเสี่ยง";
			mes "เอาละฉันชนะ";
			mes "คุณอยากเล่นอีกครั้งไหมล่ะ";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และแต้มของฉันคือ ^0000FF" + .@tabletotal + "^000000 ฉันแพ้ !!! T^T เอาแอปเปิ้ลของคุณไป คุณอยากเล่นอีกครั้งไหมล่ะ";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และแต้มของฉันคือ ^0000FF" + .@tabletotal + "^000000 แต้มคุณคือ ^FF0000" + .@playertotal + "^000000 คุณชนะ !!! ^FF0000" + strcharinfo(0) + "^000000. เอาแอปเปิ้ลของคุณไป คุณอยากเล่นอีกครั้งไหมล่ะ";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และแต้มของฉันคือ ^0000FF" + .@tabletotal + "^000000 แต้มคุณคือ ^FF0000" + .@playertotal + "^000000 แต้มเราเสมอกัน ^FF0000" + strcharinfo(0) + "^000000. เอาแอปเปิ้ลที่คุณเดิมพันไว้กลับไป คุณอยากเล่นอีกครั้งไหมล่ะ";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และแต้มของฉันคือ ^0000FF" + .@tabletotal + "^000000 แต้มคุณคือ ^FF0000" + .@playertotal + "^000000 คุณแพ้ !!! อิอิ ^FF0000" + strcharinfo(0) + "^000000. คุณอยากเล่นอีกครั้งไหมล่ะ";
		close;
	}
}
