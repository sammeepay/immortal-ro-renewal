-	script	Designer::design5	120,{

	set .@name$,"[^0000FFStylist^000000]";
	setarray .@max[1],550,250,69;		// Number of cloths, haircolors, hairstyles in client
	setarray .@blacklist[0],0;	// NPC will skip any cloth colors in this array.
					// Use for palettes that cause errors or just plain look bad.
					// Leave first value at 0 to disable.
	setarray .@type[1],7,6,1;			// DO NOT EDIT
	set .@style,0;
	
	mes .@name$;
	mes "สวัสดีค่ะ ยินดีให้บริการค่ะ";
	mes "เชิญเสริมสวย แต่งหล่อค่ะ";
	next;
	set .@s,select("^009933^000000 เปลี่ยนสีเสื้อ",
				"^009933^000000 เปลี่ยนสีผม",
				"^009933^000000 เปลี่ยนทรงผม");
	
	mes .@name$;
	mes "เริ่มเลยนะค่ะ";
	next;
	menu "^ff0000^000000 เริ่มกันเลย",L_start,
		"^ff0000^000000 เลือกตำแหน่งที่จะเริ่ม",-;
	
	// CHOOSE WHERE TO START =====================================================
	mes .@name$;
	mes "OK เริ่มระหว่าง ^0000FF0 และ " +.@max[.@s]+ "^000000";
	next;
	input .@style;
	
	if (.@style < 0 || .@style > .@max[.@s]) {
		mes .@name$;
		mes "เกินที่กำหนดค่ะ";
		close;
	}

  L_start:
	setarray @revert[1],getlook(.@type[.@s]),0,0;
	mes .@name$;
	mes "OK เริ่มระหว่าง ^007700" +.@style+ "^000000";
	next;
	
  // BEGINNING OF STYLE MENU LOOP ================================================
  L_menuloop:
	if (.@blacklist[0]) {
		for (set .@f,0; .@f < getarraysize(.@blacklist); set .@f,.@f+1) {
			if (.@style == .@blacklist[.@f] && .@previous == 1 && .@s == 1) {
				set .@style, .@style - 1;
				goto L_menuloop;
			} else if (.@style == .@blacklist[.@f] && .@s == 1) {
				set .@style, .@style +1;
				goto L_menuloop;
			}
		}
	}
	setlook .@type[.@s],.@style;
	mes "นี่คือ Style ที่ ^007700" +.@style+ "^000000";
	
	set .@next, .@style + 1;
	set .@prev, .@style - 1;
	
	// MAXIMUM MENU
	if (.@style == .@max[.@s]) {
		set .@next,0;
	}
	
	// MINIMUM MENU ==============================================================
	if (.@style == 0) {
		set .@prev,.@max[.@s];
	}
	
	// PREVIOUS MENU =============================================================
	if (.@previous) {
		menu "^FF0000<< ก่อนหน้า - " +.@prev+ "^000000",L_prev,
			"^0000FFต่อไป >> - " +.@next+ "^000000",L_next,
			"^ff0000^000000 ใส่ Style เอง",L_jump,
			"^ff0000^000000 บันทึก Style",L_save,
			"^ff0000^000000 โหลด Style",L_load;
	}
	
	// DEFAULT MENU ==============================================================
	menu "^0000FFต่อไป >> - " +.@next+ "^000000",L_next,
		"^FF0000<< ก่อนหน้า - " +.@prev+ "^000000",L_prev,
		"^ff0000^000000 ใส่ Style เอง",L_jump,
		"^ff0000^000000 บันทึก Style",L_save,
		"^ff0000^000000 โหลด Style",L_load;
		
  L_next:
	set .@previous,0;
	set .@style, .@next;
	goto L_menuloop;

  L_prev:
	set .@previous,1;
	set .@style, .@prev;
	goto L_menuloop;
	
  L_jump:
	next;
	mes .@name$;
	mes "ใส่ Style ตามที่ต้องการค่ะ";
	next;
	input .@style;
	if (.@style < 0 || .@style > .@max[.@s]) {
		mes .@name$;
		mes "เกินที่กำหนดค่ะ";
		close;
	}
	goto L_menuloop;
	
  L_save:
	next;
	mes .@name$;
	mes "เลือกช่องจัดเก็บ Style ค่ะ";
	set .@x, select("^ff0000^000000 ช่อง 1 - [" +@revert[1]+ "]",
					"^ff0000^000000 ช่อง 2 - [" +@revert[2]+ "]",
					"^ff0000^000000 ช่อง 3 - [" +@revert[3]+ "]");
					
	setarray @revert[.@x], .@style;
	goto L_menuloop;
	
  L_load:
	next;
	mes .@name$;
	mes "เลือก Style ที่จะโหลดค่ะ";
	set .@x, select("^ff0000^000000 ช่อง 1 - [" +@revert[1]+ "]",
					"^ff0000^000000 ช่อง 2 - [" +@revert[2]+ "]",
					"^ff0000^000000 ช่อง 3 - [" +@revert[3]+ "]");
					
	set .@style, @revert[.@x];
	goto L_menuloop;

OnInit:
	waitingroom "๐ แต่งหล่อเสริมสวย ๐", 0;
	end;
}