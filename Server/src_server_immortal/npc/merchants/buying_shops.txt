//===== rAthena Script =======================================
//= Buying Shop Scripts
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.1b
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Buying Shop licenses.
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Added Catalog Magician. [Euphy]
//= 1.1a Moved Catalog Magician to Renewal path. [Euphy]
//= 1.1b Added missing weight checks. [Euphy]
//============================================================ 

// Black Marketeer (Buy Licenses - Non-Merchant Classes) :: qskill_buy_store
//============================================================ 
que_job01,68,84,1	script	Black Marketeer#Buying	881,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "[Mr. Jass]";
		mes "เจ้านี่โลภมากเกินไป แม้เมื่อมาเทียบกับคนอย่างข้า!";
		mes "อันดับแรกทำไมเจ้าไม่ไปทำให้กระเป๋าเจ้าเบาก่อนล่ะ?";
		close;
	}
	if (getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[Mr. Jass]";
		mes "เฮ่, เจ้าได้";
		mes "ติดต่อกับ Hugh แล้วนิ";
		mes "ข้าไม่มีธุระอะไรกับเจ้าเเล้วหละ";
		close;
	}
	mes "[Mr. Jass]";
	mes "เจ้าคงต้องการบางสิ่งที่ไม่ใช้เรื่องดีนัก.";
	mes "จึงมาพบข้า เจ้าต้องการอะไร?";
	next;
	switch(select("Bulk Buyer Shop License:ท่านคือใคร?:ไม่มี, ไม่มีอะไร!")) {
	case 1:
		mes "[Mr. Jass]";
		mes "ข้ารู้!";
		mes "แน่นอนหละ, ข้าสร้างมันให้เจ้าได้";
		mes "จนดูเหมือนของแท้เลยหละ!";
		mes "ต้องการเท่าไหร่หละ?";
		next;
		mes "[Mr. Jass]";
		mes "อย่างที่เจ้ารู้,";
		mes "ข้าสามารถสร้างมันให้เป็นเรื่องง่ายได้, 10ชิ้นต่อครั้ง";
		mes "ราคา 500 zeny ในทุกครั้ง.";
		next;
		while(1) {
			input .@input;
			mes "[Mr. Jass]";
			if (.@input == 0) {
				mes "เจ้าไม่ต้องการมันหรือ?";
				close;
			} else if (.@input > 10) {
				mes "ข้าสามารถได้ทำเเค่ 10 ชิ้นต่อครั้ง, รุ้มั้ย";
				next;
			} else {
				mes "มันราคา "+(.@input*500)+" Zeny";
				if (Zeny < (.@input*500)) {
					mes "ดูเหมือนว่าท่านจะมีจำนวนเงินไม่เพียงพอนะ.";
					next;
					mes "[Mr. Jass]";
					mes "ดูเหมือนเจ้าจะไม่รู้พื้นฐานของธุรกิจนะ? ทุกสิ่งล้วนมีค่า";
					mes "ถ้าท่านต้องการบางสิ่ง ท่านก็ควรจะยอมจ่ายเพื่อสิ่งนั้น";
				} else {
					mes "ฮ่า... ฮ่า ฮ่า ฮ่า!";
					mes "Mr. Hugh, ข้าจะยึดธุรกิจใบอนุญาตินี้แล้ว!";
					mes "แล้วคอยดู *หุ หุ*";
					getitem 12548,.@input; //Buy_Market_Permit2
					set Zeny,Zeny-(.@input*500);
				}
				close;
			}
		}
	case 2:
		mes "[Mr. Jass]";
		mes "ข้าหลงทางจากบ้านมาเป็นเวลานานดังนั้น";
		mes "ไม่มีประดยชน์ที่จะถามว่าข้าคือใคร เพราะว่าสถานที่ที่ข้าจากมา มีเเต่ความเกลียดชัง";
		next;
		mes "[Mr. Jass]";
		mes "...";
		mes "Hugh คือพ่อค้าเลว และไม่มีแนวคิดเรื่องธุรกิจเลย";
		mes "จุดมุ่งหมายของข้าคือการทำลายชีวิตของ Hugh.";
		next;
		mes "[Mr. Jass]";
		mes "...อ้า, ข้าดื่มมากไป";
		mes "นั่นมันแค่เพราะว่าข้าเมา";
		mes "ลืมเรื่องที่ข้าพูดซะ";
		close;
	case 3:
		mes "[Mr. Jass]";
		mes "ตกลง, ตกลง! ไม่ต้องตะโกนไป";
		mes "ปล่อยข้าอยู่คนเดียว ถ้าเจ้าไม่ต้องการทำธุรกิจกับข้า";
		close;
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================ 
alberta_in,58,52,4	script	Purchasing Team#Buying	59,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "- ท่านไม่ีสามารถพูดคุยหรือดำเนินการเกี่ยวกับเควสได้ เพราะท่านจะแบกสัมภาระมากเกินไป -";
		close;
	}
	mes "[Mr. Hugh]";
	if (BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if (getskilllv("ALL_BUYING_STORE") == 1) {
			mes "ข้ามีนามว่า Huh";
			mes "มาจากทีมงานจัดจำหน่ายสินค้า";
			mes "ท่านมีอะไรให้ข้ารับใช้รึ?";
			next;
			if(select("ขอซื้อใบอนุญาติตั้งร้าน Bulk Buyer Shop:ยกเลิก") == 2) {
				mes "[Mr. Hugh]";
				mes "ถามข้าได้ทุกเมื่อ";
				mes "เมื่อท่านต้องการใบอนุยาตตั้งร้าน  Bulk Buyer Shop";
				mes "ไว้เจอกันใหม่~";
				close;
			}
			mes "[Mr. Hugh]";
			mes "มันราคา 200 zeny สำหรับทุกๆใบอนุญาติตั้งร้าน Bulk Buyer Shop, และท่านสามารถซื้อมันได้แค่ 50 ใบเท่านั้น";
			mes "ท่านต้องการใบอนุญาตินี้ซักกี่ใบละ?";
			next;
			while(1) {
				input .@input;
				mes "[Mr. Hugh]";
				if (.@input == 0) {
					mes "ท่านได้ยกเลิกการแลกเปลี่ยน.";
					mes "ขอให้โชคดี.";
					close;
				} else if (.@input > 50) {
					mes "กรุณาใส่จำนวน 50 หรือน้อยกว่า.";
					next;
				} else {
					mes "It'll cost "+(.@input*200)+" zeny for "+.@input+" licenses.";
					mes "ราคาทั้งหมด "+(.@input*200)+" zeny สำหรับ "+.@input+" ใบ.";
					if (Zeny < (.@input*200)) {
						mes "ดูเหมือนว่าท่านจะมีจำนวนเงินไม่เพียงพอนะ.";
					} else {
						mes "ขอบคุณสำหรับการสนับสนุน";
						getitem 6377,.@input; //Buy_Market_Permit
						set Zeny,Zeny-(.@input*200);
					}
					close;
				}
			}
		} else {
			mes "ข้าคือ Hugh มาจาก";
			mes "ทีมงานจัดจำหน่าสินค้า";
			mes "ของสมาคมพ่อค้าแห่ง Alberta";
			mes "ท่านคือ...";
			next;
			mes "[Mr. Hugh]";
			mes "เอ่อ,ท่านไม่ได้เป็นสมาชิกของทางเรา";
			mes "ช่างมัน,";
			mes "มันไม่ใช้เรื่องที่ท่านต้องสนใจหนัก";
			next;
			mes "[Mr. Hugh]";
			mes "ท่านรู้หรือไม่?";
			mes "สมาคมของเรากำลังพบปัญหาจากใบอนุญาติ";
			mes "ส่วนบุคคลในการซื้อสินค้า";
			next;
			select("ข้าไม่เคยมีปัญหาในการซื้อสินค้าเลย...");
			mes "[Mr. Hugh]";
			mes "ถูกต้อง, เเต่ ท่านลองคิดดูนะ :";
			mes "ท่านไม่เคยเจออุปสรรคในการซื้อสินค้าจำนวนมากเลยใช้ใหม?";
			mes "ท่านต้องทำการตรวจสอบ และ พูดคุยกับทุกคนถึงสินค้า ที่ท่านต้องการ.";
			mes "ดูแล้วมันไม่ค่อยสะดวกเลยใช้ใหมหละ, ท่านว่าอย่างไร?";
			next;
			mes "[Mr. Hugh]";
			mes "การซื้อสินค้าในจำนวนมากนั้นแหละ ปัญหาที่สำคัญของพวกเรา พวกพ่อค้า";
			mes "ข้าจึงเสนอแผนใหม่กับสมาคมของเรา, สำหรับประสบการณ์การค้าขาย 10ปี ของข้า";
			next;
			mes "[Mr. Hugh]";
			mes "'ให้เเต่ละบุคคล";
			mes "รับเปิดร้านค้าส่วนตัว ! '";
			mes "และนั้นคือจุดเริ่มต้นข้อเสนอของข้า.";
			mes "ท่านคิดอย่างไร, เมื่อท่านได้อ่านมัน...";
			next;
			mes "[Mr. Hugh]";
			mes "...";
			mes "...(Mr. Hugh บรรยายรายละเอียดในส่วนต่างๆ.)";
			next;
			select("ตกลง, จุดมุ่งหมายคืออะไร?");
			mes "[Mr. Hugh]";
			mes "อ้อ, สำหรับ โดยสรุป,";
			mes "ท่านสามารถซื้อสินค้าต่างๆผ่านร้านค้า.";
			next;
			mes "[Mr. Hugh]";
			mes "ในรายการที่จะเปิดร้านค้า Bulk Buyer Shop, ท่านต้องมีใบอนุญาติจากสมาคมพ่อค้าเสียกาอน และต้องใช้มันทุกครั้งที่เปิดร้านค้า เราคาดหวังว่าจะมีการเพิ่มขึ้นในผลกำไรผ่านรูปแบบใบอนุญาติอันใหม่นี้.";
			next;
			mes "[Mr. Hugh]";
			mes "และนั้นคือจุดประสงค์ของข้า!";
			mes "เหล่าประธานของพวกเราต่างรู้สึกยินดี";
			mes "เมื่อได้ยินเรื่องที่เรากำลังจะทำนี้";
			mes ".";
			mes "[ "+strcharinfo(0)+" ]";
			mes "กรุณา, ไปเข้าเรื่องหลักได้เเล้ว!";
			next;
			mes "[Mr. Hugh]";
			mes "ท่านอย่าใจร้อนนัก, จะได้มั้ย?";
			mes "เรื่องหลักคือ, เราสามารถให้่านเปิด Bulk Buyer Shop Vending ถ้าท่านเรียนรู้ Vending.";
			next;
			mes "[Mr. Hugh]";
			mes "แน่นอน,";
			mes "เราคิดค่าธรรมเนียมเพียงครั้งละ 10,000 zeny,";
			mes "และท่านจะต้องมี ^4A4AFFBulk Buyer Shop License^000000 ทุกครั้งที่เปิดร้าน.";
			next;
			mes "[Mr. Hugh]";
			mes "ที่กล่าวไป,?";
			mes "ท่านต้องการจะสมัครตอนนี้เลยหรือไม่?";
			mes "ถ้าท่านตกลง, ข้าจะสอนท่านเกี่ยวกับ Bulk Buyer Shop นี้ทันที";
			next;
			if(select("เรียนรู้การเปิดร้าย Bulk Buyer Shop:ยกเลิก") == 2) {
				mes "[Mr. Hugh]";
				mes "เฮ้อ, ช่างน่าผิดหวัง";
				mes "ทั้งๆที่การมีตัวเลือกที้ให้ผลประโยชน์กับท่านขนาดนี้ , เเต่ท่านก็ยัง...";
				mes "ข้าจะคอยให้คำปรึกษาเสมอ!";
				close;
			}
			mes "[Mr. Hugh]";
			if (Zeny < 10000) {
				mes "ค่าลงทะเบียน 10,000 zeny.";
				mes "กรุณาเตรียมความพร้อมสำหรับการลงทะเบียนครั้งเเรก";
				close;
			} else {
				mes "นับว่าเป็นการตัดสินใจที่ดี";
				mes "กรุณากรอกใบสมัครนี้,";
				mes "และลงชื่อตรงนี้....";
				next;
				input .@name$;
				mes "[Mr. Hugh]";
				mes .@name$+"....";
				mes "ข้าชอบลายมือท่านจัง.";
				mes "ตกลง, ตอนนี้ท่านสามารถใช้บริการ Bulk Buyer Shop ได้เเล้ว.";
				set Zeny,Zeny-10000;
				getitem 6377,5; //Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,SKILL_PERM_GRANT;
				next;
				mes "[Mr. Hugh]";
				mes "ในขณะนี้, เฉพาะ items ทั่วไป ยกเว้น  equipment, บาง potions, และ item ที่สร้างขึ้น";
				mes "สามารถซื้อได้ในนี้ , เเต่มันก้ยังมีประโยชนืกับท่าน, โดยขึ้นอยู่กับการใช้งาน.";
				next;
				mes "[Mr. Hugh]";
				mes "โอ้,";
				mes "และต้องไม่น้อยกว่าหนึ่งรายการที่ท่านต้องการซื้อ";
				mes "เพราะว่าท่านได้แสดงมันบนร้านค้าของ ท่านแล้ว";
				next;
				mes "[Mr. Hugh]";
				mes "ข้าจะมอบ 5 Bulk Buyer Shop Licenses นี้สำหรับการเริ่มต้น.";
				mes "ขอให้สนุกกับมันนะ!";
				close;
			}
		}
	}
	mes "ข้าคือ Hugh มาจาก";
	mes "ทีมงานจัดจำหน่ายสินค้า ของสมาคมพ่อค้า";
	mes "แห่ง Alberta ข้ารักในการสนทนา,";
	mes "เเต่ตอนนี้ข้ายุ่งมาก";
	next;
	mes "[Mr. Hugh]";
	mes "เวลาของข้าจะมอบให้แก่ ลูกค้าของสมาคมพ่อค้าของเราเท่านั้น";
	close;
}
