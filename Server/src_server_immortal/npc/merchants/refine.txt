//===== rAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 3.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//= 3.4 Added VIP features. [Euphy]
//============================================================

// Christopher: Geffen Blacksmith
//============================================================
geffen_in,110,172,0	script	Christopher#1	63,{
	mes "[Christopher Guillenrow]";
	mes "Welcome to Christopher's Workshop. Ye can get all yer stuff for forging here. What business";
	mes "brings ye to me?";
	next;
	switch(select("Purchase Anvil:Purchase Forging Item:Purchase Metal:Purify Rough Ores:Cancel")) {
	case 1:
		mes "[Christopher Guillenrow]";
		mes "A better Anvil gives ye a greeeater chance to make better weapons, ye know? But they'll cost ye more zeny. Just get it off yer chest and buy what fits your purposes best, laddy.";
		next;
		switch(select("Anvil - 30,000 zeny:Oridecon Anvil - 120,000 zeny:Golden Anvil - 300,000 zeny:Better Anvil than the others.:Cancel.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 986,1; // Anvil
			set Zeny, Zeny-30000;
			mes "[Christopher Guillenrow]";
			mes "This is the cheapest one, but efficient enough to forge most items. Thank ye fer shopping at me workshop.  Feel free to come anytime, whenever ye need.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			set Zeny, Zeny-120000;
			mes "[Christopher Guillenrow]";
			mes "Aye, friend ye have an eye for the anvil. This must be the proper anvil for a Blacksmith, eh? Thank ye fer shopping at me workshop.  Feel free to come anytime, whenever ye need.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 988,1; // Golden_Anvil
			set Zeny, Zeny-300000;
			mes "[Christopher Guillenrow]";
			mes "This one is the best among all me stuffs in me workshop! With this, ye can rule the Blacksmith world! Thank ye fer shopping at me workshop.  Feel free to come anytime, whenever ye need.";
			close;
		case 4:
			mes "[Christopher Guillenrow]";
			mes "Well, sorry. But I don't have anythin' harder' than the Golden Anvil.";
			next;
			mes "[Christopher Guillenrow]";
			mes "Me thinks 'Ringgel,' the Legendary Anvil Maker would have one. But, I don't think ye can find him, though he be somewhere in this world.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Okay, feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 2:
		mes "[Christopher Guillenrow]";
		mes "A respectable blacksmith uses fine tools. Ye can become one o'those with me Stuff. Choose anything ye want.";
		next;
		switch(select("Mini-Furnace - 150 zeny:Iron Hammer - 1000 zeny:Golden Hammer - 3000 zeny:Oridecon Hammer - 5000 zeny:Cancel.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "It's a much needed tool fer refining metal! So, How many do ye wish to buy? If ye want to quit, just type the number '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Aye, the deal is canceled. Fare ye well.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Ye can buy 500, er less.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 150;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Ye look like you don't got enough room in yer inventory. Put some stuff into your Kafra Storage, why don't ye?";
				close;
			}
			getitem 612,.@input; // Portable_Furnace
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 613,1; // Iron_Hammer
			set Zeny, Zeny-1000;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 3:
			if (Zeny < 3000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 614,1; // Golden_Hammer
			set Zeny, Zeny-3000;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			getitem 615,1; // Oridecon_Hammer
			set Zeny, Zeny-5000;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 3:
		mes "[Christopher Guillenrow]";
		mes "I prepare every Metal, and only the high quality ones o'course. Now then, which one do ye need?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1000z.:Cancel.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "So, How many do ye wish to buy? If ye dont want anything, just type the number as '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Deal has";
					mes "been canceled.";
					mes "Fare ye well.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Ye can buy 500, er less.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 200;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Ye don't have enough money. Ye know I can't sell this at a lower price... You know how the wifey nags about Zeny.";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Ye look like you don't have the room to carry anythin' new. Why don't ye put some things into Kafra Storage n' come back.";
				close;
			}
			getitem 1010,.@input; // Phracon
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need.";
			close;
		case 2:
			mes "[Christopher Guillenrow]";
			mes "So, how many do ye wish to buy? If ye dont want anything at all, just type the number as '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Deal has";
					mes "been canceled.";
					mes "Fare ye well.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Ye can buy 500, er less.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 1000;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "I don't think I can let ye have this with the zeny ye have. I can't lose me money because of ye.";
				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Me friend... Seems to me ye don't have Inventory space. Why doncha put some things into Kafra Storage first?";
				close;
			}
			getitem 1011,.@input; // Emveretarcon
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Thank ye fer shopping at me workshop. Feel free to come anytime, whenever ye need, whenever ye want.";
			close;
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 4:
		mes "[Christopher Guillenrow]";
		mes "I can purify yer Oridecon and Elunium. I make a refined Ore out of 5 o'each rough ones. Well... Which one do ye want to make?";
		next;
		switch(select("Make Oridecon:Make Elunium:Cancel.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Christopher Guillenrow]";
				mes "I told ye, I need 5 o'the rough Oridecons fer one Oridecon.";
				close;
			}
			else {
				delitem 756,5;  //Oridecon_Stone
				getitem 984,1; // Oridecon
				mes "[Christopher Guillenrow]";
				mes "Here's an Oridecon fer ye. Ye will be always welcome here, I'll be waitin' for ye.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Christopher Guillenrow]";
				mes "I told ye, I need 5 rough Eluniums fer one Elunium.";
				close;
			}
			else {
				delitem 757,5;  //Elunium_Stone
				getitem 985,1; // Elunium
				mes "[Christopher Guillenrow]";
				mes "Arrr, here's yer Elunium. Yer business is always welcome here, so feel free to come again.";
				close;
			}
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Feel free to come anytime, whenever ye need. Fare ye well.";
			close;
		}
	case 5:
		mes "[Christopher Guillenrow]";
		mes "Feel free to come anytime, whenever ye need and whenever ye want. Fare ye well.";
		close;
	}
}

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script	Paul Spanner	63,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Paul Spanner]";
	mes "Welcome, my friend.";
	mes "In my shop, you will find everything that you need in forging.";
	mes "Tell me what you need.";
	next;
	switch(select("Purchase Anvil.:Purchase Forging Items.:Purchase Metal.:Process Ores.:Quit.")) {
	case 1:
		mes "[Paul Spanner]";
		mes "Anvil is the most necessary item for Blacksmiths.";
		mes "Since you will use an Anvil more than once, you'd better buy a nice one.";
		next;
		switch(select("Anvil - 30,000z.:Oridecon Anvil - 120,000z.:Golden Anvil - 300,000z.:I need a better anvil.:Cancel.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 986,1; //Anvil
			set Zeny, Zeny-30000;
			mes "[Paul Spanner]";
			mes "It is the cheapest anvil which has the most basic ability.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			set Zeny, Zeny-120000;
			mes "[Paul Spanner]";
			mes "Ah, you have an eye for anvil. A Blacksmith needs an anvil at least as good as this.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 988,1; //Golden_Anvil
			set Zeny, Zeny-300000;
			mes "[Paul Spanner]";
			mes "I can tell your ambition to become a good Blacksmith just by looking at you to choose this Golden Anvil!";
			mes "This anvil will surely aid you in creating the best weapons.";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "I am sorry, but I do not sell better anvils than Golden Anvil.";
			mes "Unless you find the legendary anvil of 'Linggell', I don't think that you could find better one than Golden Anvil in any other places.";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "You need various materials to process ores and to forge weapons.";
		mes "I have everything that you need. Take a look.";
		next;
		switch(select("Mini Furnace - 150z.:Iron Hammer - 1,000z.:Golden Hammer - 3,000z.:Oridecon Hammer - 5,000z.:Cancel.")) {
		case 1:
			set .@item,612;
			set .@item_cost,150;
			set .@item_weight,200;
			mes "[Paul Spanner]";
			mes "You definately need this furnce to process ores!";
			next;
			break;
		case 2:
			set .@item,613;
			set .@item_cost,1000;
			set .@item_weight,200;
			break;
		case 3:
			set .@item,614;
			set .@item_cost,3000;
			set .@item_weight,300;
			break;
		case 4:
			set .@item,615;
			set .@item_cost,5000;
			set .@item_weight,400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "You have canceled the trade. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can only buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first.";
			close;
		}
		set Zeny, Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "I have high quality metal.";
		mes "So, which metal would you like to buy?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1,000z.:Quit.")) {
		case 1:
			set .@item,1010;
			set .@item_price,200;
			break;
		case 2:
			set .@item,1011;
			set .@item_price,1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many of them do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "The trade has been canceled. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first?";
			close;
		}
		getitem .@item,.@input;
		set Zeny, Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "I can process Oridecon and Elunium for you.";
		mes "You need 5 ores to process them into one Oridecon or Elunium.";
		mes "So, which one do you want to process?";
		switch(select("Oridecon:Elunium:Quit.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Oridecon.";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Elunium.";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "If you need anything, just let me know.";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	Hollgrehenn	85,{
	callfunc "refinemain","Hollgrehenn",0;
	end;
}
morocc_in,73,38,6	script	Aragham	99,{
	callfunc "refinemain","Aragham",0;
	end;
}
payon,144,173,5	script	Antonio	88,{
	callfunc "refinemain","Antonio",0;
	end;
}
alberta_in,28,58,0	script	Fredrik	85,{
	callfunc "refinemain","Fredrik",0;
	end;
}
yuno_in01,171,21,4	script	Lambert	88,{
	callfunc "refinemain","Lambert",0;
	end;
}
ein_in01,24,87,5	script	Manthasman	826,{
	callfunc "refinemain","Manthasman Pruhag",0;
	end;
}
lhz_in02,282,20,7	script	Fulerr	869,{
	callfunc "refinemain","Fulerr",0;
	end;
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine_db.txt as well.
//============================================================
function	script	refinemain	{
	disable_items;
	.@npc_name$ = getarg(0);
	set .@features,getarg(1);
	mes "["+ .@npc_name$ +"]";
	mes "ไหนส่งอุปกรณ์มาให้ข้าดูสิ";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<getarraysize(.@indices); set .@i,.@i+1) {
		if(getequipisequiped(.@indices[.@i])) {
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "ไม่เห็นหรือว่าข้ายุ่งมาก";
		mes "ไม่มีเวลามาล้อเล่นกับท่าน?";
		mes "โปรดนำศีรษะเหม็นๆของท่านกลับไปเถอะ";
		close;
	}
	set .@part, .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "["+ .@npc_name$ +"]";
		mes "You're not wearing";
		mes "anything there that";
		mes "I can refine.";
		emotion e_an;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "["+ .@npc_name$ +"]";
		mes "ไปที่อื่นเถอะ";
		mes "ของชิ้นนี้ตีไม่ได้หรอก";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "["+ .@npc_name$ +"]";
		mes "I can't refine this";
		mes "any more. This is as";
		mes "refined as it gets!";
		close;
	}
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	switch(getequipweaponlv(.@part)){
	case 0: 	//Refine Armor
		set .@price,2000;
		set .@material,985; //Elunium
		set .@safe,4;
		break;
	case 1: 	//Refine Level 1 Weapon
		set .@price,50;
		set .@material,1010; //Phracon
		set .@safe,7;
		break;
	case 2: 	//Refine Level 2 Weapon
		set .@price,200;
		set .@material,1011; //Emveretarcon
		set .@safe,6;
		break;
	case 3: 	//Refine Level 3 Weapon
		set .@price,5000;
		set .@material,984; //Oridecon
		set .@safe,5;
		break;
	case 4: 	//Refine Level 4 Weapon
		set .@price,20000;
		set .@material,984; //Oridecon
		set .@safe,4;
		break;
	case 5: 	//Refine other stuff?
		set .@price,2000;
		set .@material,985; //Elunium
		set .@safe,4;
		break;
	}

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(1)) {
		switch(getequipweaponlv(.@part)) {
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
			case 5: set .@price, .@price * 10; break;
		}
	}

	if(.@features != 1) {
		mes "["+ .@npc_name$ +"]";
		mes "^003366"+getitemname(.@material)+"^000000 1 ชิ้น และค่าจ้าง " + .@price + " เซนี ไม่มีการต่อรอง";
		mes "สำหรับข้า นี่เป็นงานที่หมูมาก..ว่าไงล่ะ?!";
		next;
		if(select("ตกลง:ไม่") == 2){
			mes "["+ .@npc_name$ +"]";
			mes "อย่ามาล้อเล่นกับข้า ขอร้อง!!!";
			close;
		}
		if(getequippercentrefinery(.@part) < 100) {
			mes "["+ .@npc_name$ +"]";
			mes "เฮอะ เฮอะ...";
			mes "ดูเหมือนท่านจะกล้ามากที่ต้องการตีของชิ้นนี้";
			mes "ถ้าตีอีกมันอาจเสียได้ รู้ไหม? แล้วยังจะตีอีกรึ?";
			switch(.@material) {
			case 985:
				mes "^FF0000ถ้าอุปกรณ์เสียหายหรือถูกทำลาย! นั่นหมายความว่าอุปกรณ์นี้และ Card ใดๆ หรือคุณสมบัติพิเศษที่เพิ่มในเกราะนี้จะหายไป^000000";
				break;
			default:
				mes "ข้าต้องเตือนท่านก่อนนะ ^FF0000ว่าอาวุธของท่านมันอาจจะเสียหายไปอย่างถาวร... หากมี Card อยู่ในอาวุธนั้น มันก็จะเสียหายไปด้วยเช่นกัน^000000";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "ข้าต้องเตือนท่านก่อนนะ";
			mes "^FF0000อาวุธหรืออุปกรณ์ของท่านมันอาจจะเสียหายไปอย่างถาวร...";
			mes "นั้นคือของชิ้นนั้นจะหายไปจากโลกนี้เลย แล้วท่านยังอยากจะตีอีกหรือ^000000?";
			next;
			if(select("ตกลง:ไม่") == 2){
				mes "["+ .@npc_name$ +"]";
				mes "ไร้สาระจริงๆ";
				mes "เจ้าทำให้ข้าเสียเวลามากไปไกลๆเลย ไป๊";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "["+ .@npc_name$ +"]";
			mes "บอกแล้วไงว่าห้ามต่อรอง";
			mes "ไปหาเงินเซนี หรือ "+getitemname(.@material)+" ก่อนไป๊";
			close;
		}
		set Zeny, Zeny-.@price;
		delitem .@material,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
			mes "["+ .@npc_name$ +"]";
			emotion e_an;
			mes "รอสักครู่...";
			mes "ท่านคิดว่าข้าโง่ ?!";
			mes "ท่านเปลี่ยนรายการในขณะที่ข้าไม่ได้มองหรอ! ออกไปจากที่นี่ซะ!";
			close;
		}

		if(getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "["+ .@npc_name$ +"]";
			emotion (!rand(5))?e_cash:e_omg;
			set .@lose,rand(1,3);
			if (.@lose == 1) {
				mes "OH! MY GOD!";
				mes "Damn it! Not again!";
				mes "I'm terribly sorry, but you know practice does make perfect.";
				mes "Um, right? Heh heh...";
			} else if(.@lose == 2) {
				mes "Nooooooo!";
				mes "It broke!";
				mes "I-I'm sorry!";
			} else {
				mes "Crap!";
				mes "It couldn't take";
				mes "much more tempering!";
				mes "Sorry about this...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion e_heh;
		set .@win,rand(1,3);
		if (.@win == 1) {
			mes "สุดยอด!";
			mes "หึ หึ!";
			mes "เป็นอีกครั้งที่";
			mes "การทำงานอย่างไร้ที่ติ";
			mes "จากต้นแบบ~";
		} else if(.@win == 2) {
			mes "การตีอุปกรณหรืออาวุธนี้ประสบความสำเร็จ... !";
			mes "อีกครั้งที่น่าตื่นตาตื่นใจของข้า";
			mes "ความสามารถอย่างแท้จริง";
		} else {
			mes "หึ หึ!";
			mes "ข้าทำทุกอย่าง";
			mes "ไม่มีข้อสงสัยในการทำงานของข้า";
			mes "คือการพึ่งพอใจของท่าน~";
		}
		close;
	}

// New Refining Functions ========================
	if(getequiprefinerycnt(.@part) < .@safe) {
		mes "["+ .@npc_name$ +"]";
		mes "ข้าสามารถปรับแต่งนี้ไปยังที่ปลอดภัยหรือขีดจำกัด จำนวนครั้งที่ต้องการของ มันเป็นทางเลือกของท่าน";
		next;
		set .@menu2,select("ขีดจำกัดที่ปลอดภัยได้โปรด","ข้าจะตัดสินใจว่ากี่ครั้งที่","ข้าได้เปลี่ยนความคิดของข้า...");
	} else
		set .@menu2,2;
	switch(.@menu2){
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		next;
		mes "["+ .@npc_name$ +"]";
		mes "กี่ครั้งที่คุณต้องการให้ข้าตีอาวุธหรืออุปกรณ์ของท่านหรือไม่?";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "["+ .@npc_name$ +"]";
			mes "ข้าไม่สามารถตีอาวุธหรืออุปกรณ์ของชิ้นนี้หลายครั้งได้อีกแล้ว";
			close;
		}
		if(.@refinecheck > .@safe) {
			set .@refinecheck,.@refinecheck - .@safe;
			mes "["+ .@npc_name$ +"]";
			mes "นี้จะพยายามที่จะตีอาวุธหรืออุปกรณ์ให้ถึง " + .@refinecheck + " ครั้ง โดยที่จำกัดจำนวนไว้แล้วที่ปลอดภัย อาวุธหรืออุปกรณ์ของท่านอาจจะเสียหายหรือถูกทำลายได้... คือว่าท่าน OK?";
			next;
			if(select("ตกลง...","ไม่...") == 2){
				mes "["+ .@npc_name$ +"]";
				mes "เจ้าทำให้ข้าเสียเวลามากไปไกลๆเลย ไป๊";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เจ้าทำให้ข้าเสียเวลามากไปไกลๆเลย ไป๊";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes "["+ .@npc_name$ +"]";
	mes "จำนวนครั้งที่ท่านต้องการตีอาวุธหรืออุปกรณ์ " + .@refinecnt + " ครั้ง  สิ่งของที่ต้องใช้ " + getitemname(.@material) + " และจำนวนเงิน " + .@fullprice + " Zeny คือว่าท่านโอเคไหม?";
	next;
	if(select("ตกลง..โอเค","ไม่...") == 2){
		mes "["+ .@npc_name$ +"]";
		mes "เจ้าทำให้ข้าเสียเวลามากไปไกลๆเลย ไป๊";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "["+ .@npc_name$ +"]";
		mes "นั่นคือทั้งหมดที่ท่านต้องการได้? แต่น่าเสียดายที่ข้าไม่สามารถทำงานให้ท่านในราคาที่ต่ำ ลองใส่ตัวเองในรองเท้าของข้า";
		close;
	}
	set Zeny, Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "ดูก่อน... ท่านไม่ได้มีไอเท็มอะไรเลย...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "["+ .@npc_name$ +"]";
			mes "ก๊อง... ไม่มี แต่ไม่ที่ท่านคิดว่าข้าอาจจะโง่ดังนั้น ?!";
			mes "ท่านเปลี่ยนมัน...";
			mes "รีบออกมาก่อนที่ข้าจะ Stun ท่านด้วยค้อนของข้า !!";
			close;
		} 
		mes "ก๊อง ก๊อง!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion e_omg;
			mes "["+ .@npc_name$ +"]";
			mes "อุ้ยยย!!! ขอโทษด้วย... ข้าเตือนท่านแล้วนี้อาจเกิดขึ้น...";
			set .@refinecnt,.@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "นี่คือเงิน Zeny และวัตถุดิบที่ไม่ได้ใช้เอากลับไป...";
			getitem .@material,.@refinecnt;
			set .@fullprice,.@refinecnt * .@price;
			set Zeny, Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion e_no1;
		set .@refinecnt,.@refinecnt - 1;
		next;
	}
	mes "["+ .@npc_name$ +"]";
	mes "ทั้งหมดเสร็จเรียบร้อยแล้ว... มาอีกครั้งเร็วๆ คิวมันยาวลูกค้าข้ารออยู่!";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	Vurewell	86,{
	callfunc "phramain","Vurewell";
	end;
}
payon,145,178,3	script	Begnahd	88,{
	callfunc "phramain","Begnahd";
	end;
}
morocc_in,63,32,6	script	Sade	99,{
	callfunc "phramain","Sade";
	end;
}
alberta_in,13,71,3	script	Kahlamanlith	86,{
	callfunc "phramain","Kahlamanlith";
	end;
}
yuno_in01,171,27,4	script	Dilemma	88,{
	callfunc "phramain","Dilemma";
	end;
}
ein_in01,15,87,3	script	Tirehaus	86,{
	callfunc "phramain","Tirehaus";
	end;
}
lhz_in02,278,24,3	script	Krugg	86,{
	callfunc "phramain","Krugg";
	end;
}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน !! -";
		mes "- ขณะนี้ท่านกำลังแบก -";
		mes "- ไอเท็มมากเกินไปกับท่าน -";
		mes "- โปรดลองอีกครั้ง -";
		mes "- หลังจากที่ท่านลดน้ำหนักไปบางแล้ว -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "ข้าขายโลหะ 2 ชนิด";
	mes "ที่ใช้ในการหลอมอาวุธที่เป็นเหล็กหรือเหล็กกล้า";
	mes "ข้ามี ^007777Phracon^000000 สำหรับอาวุธ Level 1";
	mes "...และ ^007777Emveretarcon^000000";
	mes "สำหรับอาวุธ Level 2";
	mes "ข้ามีวัตถุดิบมากมายเลย...ฮ่าๆ";
	next;
	switch(select("Phracon - 200 Zeny:Emveretarcon - 1000 Zeny:ถามว่ามีหินประเภทอื่นหรือเปล่า")) {
	case 1:
		set .@material,1010;
		set .@price,200;
		break;
	case 2:
		set .@material,1011;
		set .@price,1000;
		break;
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "โอ้!!!?";
		mes "ท่านต้องการโลหะสำหรับอาวุธระดับสูงแต่ว่า  Oridecon และ Elunium";
		mes "เป็นของหายากมาดเลยนะ...";
		close;
	}
	mes "["+ .@npc_name$ +"]";
	mes "ท่านต้องการซื้อเท่าไหร่ล่ะ?";
	mes "ถ้าต้องการยกเลิก โปรดระบุตัวเลขให้เป็น '0'";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "การแลกเปลี่ยนถูกยกเลิก";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "["+ .@npc_name$ +"]";
			mes "ท่านสามารถซื้อทีละ 500 หรือต่ำกว่า";
			next;
		}
		else {
			break;
		}
	}
	set .@sell,.@input * .@price;
	if (Zeny < .@sell) {
		mes "["+ .@npc_name$ +"]";
		mes "เอ่อ...ดูเหมือนท่านจะมีเงินไม่พอที่จะซื้อ "+ .@input +" ชิ้น จากข้า";
		mes "ขอโทดด้วย แต่ข้าขายต่ำกว่าทุนไม่ได้";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "อืม...";
		mes "ข้าไม่สามารถให้อะไรท่านได้ ถ้าท่านมีช่องเก็บของไม่เพียงพอสำหรับท่าน ทำไมท่านไม่นำสิ่งของไปจัดเก็บหรือฝากไว้ที่ Kafra ล่ะแล้วกลับมาหาข้าอีกครั้ง";
		close;
	}
	getitem .@material,.@input;
	set Zeny, Zeny-.@sell;
	mes "["+ .@npc_name$ +"]";
	mes "นี่ครับ!";
	mes "ขอบคุณที่ซื้อของจากร้านของข้า";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	Dietrich	84,{
	callfunc "orimain","Dietrich";
	end;
}
payon,137,178,5	script	Hakhim	88,{
	callfunc "orimain","Hakhim";
	end;
}
morocc_in,72,32,6	script	Abdula	99,{
	callfunc "orimain","Abdula";
	end;
}
alberta_in,21,63,5	script	Xenophon	84,{
	callfunc "orimain","Xenophon Zolotas";
	end;
}
yuno_in01,164,27,4	script	Delight	88,{
	callfunc "orimain","Delight";
	end;
}
ein_in01,18,82,6	script	Matestein	84,{
	callfunc "orimain","Matestein";
	end;
}
lhz_in02,281,24,5	script	Fruel	84,{
	callfunc "orimain","Fruel";
	end;
}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน !! -";
		mes "- ขณะนี้ท่านกำลังแบก -";
		mes "- ไอเท็มมากเกินไปกับท่าน -";
		mes "- โปรดลองอีกครั้ง -";
		mes "- หลังจากที่ท่านลดน้ำหนักไปบางแล้ว -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "ถ้าท่านนำ Rough Oridecons หรือ Rough Eluniums มาให้ข้า";
	mes "ข้าจะทำเป็นแร่บริสุทธิ์ให้ท่านและขอให้แน่ใจว่าข้าต้องการรับ";
	mes "แร่ดิบ 5 ชิ้นเพื่อเปลี่ยนเป็นแร่บริสุทธิ์ 1 ชิ้นให้กับท่าน";
	next;
	switch(select("ทำ Oridecon:ทำ Elunium:ถามเรื่อง Enchanted Stones")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "["+ .@npc_name$ +"]";
			mes "นี่คือ Oridecon ของท่าน";
			mes "ท่านยินดีที่จะมา";
			mes "กลับมาเมื่อใดก็ตามที่ท่านต้องการ";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "ฮ่าฮ่าา..ท่านล้อข้าเล่นหรือไง?";
			mes "ข้าบอกแล้วไงว่าข้าต้องการมี  Rough Oridecons 5 ชิ้นเพื่อทำเป็นแร่บริสุทธิ์ Oridecon 1 ชิ้น";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "["+ .@npc_name$ +"]";
			mes "นี่คือ Elunium ของท่าน";
			mes "ท่านยินดีที่จะมา";
			mes "กลับมาเมื่อใดก็ตามที่ท่านต้องการ";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "ฮ่าฮ่าา..ท่านล้อข้าเล่นหรือไง?";
			mes "ข้าบอกแล้วไงว่าข้าต้องการมี  Rough Elunium 5 ชิ้นเพื่อทำเป็นแร่บริสุทธิ์ Elunium 1 ชิ้น";
			close;
		}
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Enchanted Stones...?";
		mes "เอ่อ ใน 20 ปีของชีวิตช่างหิน ข้าได้ยินเรื่องเกี่ยวกับของสิ่งนี้มาหลายครั้ง...แต่ยังไม่เคยเห็นมันมาก่นเลย";
		mes "จากคำบอกเล่า มี Enchanted Stones 4 ชนิด";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Enchanted Stone แต่ละชนิดจะมีคุณสมบัติของธาตุแตกต่างกันไป- ดิน น้ำ ลม ไฟ";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ถ้ามีคนผสม Enchanted Stone ลงไปในอาวุธที่กำลังตี อาวุธชิ้นนั้นจะได้รับคุณสมบัติเช่นเดียวกับหิน";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "และ..ที่แน่นอน ผู้ที่ตีจะต้องมีฝีมือเพียงพอในการผสมหินเข้าไปในอาวุธนั้นๆ";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	Repairman#alb	86,{
	callfunc "repairmain","Repairman";
	end;
}

moc_ruins,107,94,4	script	Repairman#moc	99,{
	callfunc "repairmain","Repairman";
	end;
}

payon,143,165,4	script	Repairman#pay	88,{
	callfunc "repairmain","Repairman";
	end;
}

prt_in,63,54,2	script	Repairman#prt	86,{
	callfunc "repairmain","Grendal";
	end;
}

yuno_in01,175,28,3	script	Repairman#juno	86,{
	callfunc "repairmain","Repairman";
	end;
}

geffen_in,34,166,3	script	Repairman#gef	99,{
	callfunc "repairmain","Repairman";
	end;
}

aldeba_in,38,60,3	script	Repairman#alde	86,{
	callfunc "repairmain","Repairman";
	end;
}

lhz_in02,284,14,3	script	Repairman#lhz	86,{
	callfunc "repairmain","Repairman";
	end;
}

prt_gld,139,117,4	script	Repairman#prt_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

gef_fild13,263,117,4	script	Repairman#gef_fild	86,{
	callfunc "repairmain","Repairman";
	end;
}

pay_gld,295,183,4	script	Repairman#pay_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

alde_gld,220,152,4	script	Repairman#alde_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

aru_gld,189,336,4	script	Repairman#aru_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

sch_gld,340,80,7	script	Repairman#sch_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
	set .@repairprice,5000;
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "เฮ เป็นยังไงบ้าง!";
	mes "อยากซ่อมแซมไอเท็ม";
	mes "ถ้าอย่างนั้นก็มาถูกที่แล้ว";
	mes "ข้าทำให้ท่านได้ ส่งมาให้ข้า";
	next;
	switch(select("ใช่แล้วข้ามีไอเท็มที่ชำรุด...:ไม่เอา ไม่ต้อง")) {
	case 1:
		set .@checkitem,1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			set .@checkitem,.@checkitem+1;
		}
		set .@checkitem,.@checkitem-1;
		if (!.@checkitem) {
			mes "["+ .@npc_name$ +"]";
			mes "ว้าว..!";
			mes "ท่านรักษาของได้เีมากเลยทีเดียว ไม่มีไอเท็มที่ชำรุด เสียหายเลยสักชิ้นเดียว!";
			next;
			mes "["+ .@npc_name$ +"]";
			mes "ถ้ามีคนแบบท่านมากๆ ข้าต้องการตกงานแน่ๆ...!! Haha~!";
			close;
		}
		mes "["+ .@npc_name$ +"]";
		mes "อืม...";
		mes "มาดูกัน...";
		mes "มีไอเท็ม " + .@checkitem + " ชิ้น ในตัวของท่านทั้งหมด";
		mes "ที่ได้รับความเสียหายอยู่";
		mes "ท่านต้องการที่จะให้ข้าซ่อมแซมไหม?";
		next;
		set .@totalcost,.@repairprice*.@checkitem;
		mes "["+ .@npc_name$ +"]";
		mes "ค่าใช้จ่ายในการซ่อมแซมต่อชิ้นราคา " + .@repairprice + " Zeny เพื่อซ่อมแซมความเสียหายไอเท็มของท่านทั้งหมดจะเสียค่าใช้จ่าย " + .@totalcost + " Zeny! ท่านต้องการที่จะซ่อมแซมไอเท็มพวกนี้ไหม?";
		next;
		switch(select("ใช่ต้องการ:ไม่เอาดีกว่า..")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+ .@npc_name$ +"]";
				mes "โอ้โฮโอ้โฮ...";
				mes "ตรวจสอบกระเป๋าสตางค์ของท่านก่อนไหม ที่ท่านจะได้รับการซ่อมแซม! ข้าไม่สามารถซ่อมแซมอะไรเพราะท่านมีเงินไม่เพียงพอสำหรับการซ่อมแซมอาวุธหรืออุปกรณ์ในครั้งนี้";
				close;
			}
			set .@checkitem2,1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				set .@checkitem2,.@checkitem2+1;
			}
			set .@checkitem2,.@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				set Zeny, Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					set .@checkitem,.@checkitem-1;
				}
				mes "["+ .@npc_name$ +"]";
				mes "เอาล่ะ! ทุกอย่างเสร็จเรียบร้อย. ตอนนี้ก็พยายามที่จะระมัดระวังให้มากขึ้นอีกเล็กน้อย ไอเท็มที่มีอายุการใช้งานนานมากเกินไปจนทำให้ได้รับความเสียหาย ท่านก็รู้อยู่แล้วนิ";
				close;
			}
			else {
				mes "["+ .@npc_name$ +"]";
				mes "อืมมม? บางสิ่งบางอย่างผิดปกติ รอสักครู่... ไอเท็มอาวุธหรืออุปกรณ์ที่ท่าจำเป็นต้องซ่อมแซมแล้วกลับมาหาข้า";
				close;
			}
		case 2:
			mes "["+ .@npc_name$ +"]";
			mes "เอ้ออดี... พื้นผิวอาวุธหรืออุปกรณ์ดูไม่ค่อยได้เลย ไอเท็มได้รับความเสียหายหนักพดูคงจะใช้งานได้ไม่ค่อยดีหนัก ท่านควรจะซ่อมแซมมันนะ!";
			close;
		}
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "เฮ้อ...";
		mes "ท่านคงไม่มี";
		mes "ธุระกับข้า";
		mes "ถ้าท่านไม่มีไอเท็มจะซ่อมแซม";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in morroc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed repairman prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the repairman function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morroc repairman to Morroc Ruins. [L0ne_W0lf]
//============================================================
